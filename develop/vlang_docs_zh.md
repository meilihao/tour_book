# V Documentation
> version: 21.3.23@35c60cf

## ä»‹ç»

Væ˜¯ä¸€ç§é™æ€ç±»å‹çš„ç¼–è¯‘å‹ç¼–ç¨‹è¯­è¨€ï¼Œæ—¨åœ¨æ„å»ºå¯ç»´æŠ¤çš„è½¯ä»¶.

å®ƒä¸Goç±»ä¼¼ï¼Œå…¶è®¾è®¡ä¹Ÿå—åˆ°Oberon, Rust, Swift, Kotlin å’Œ Python çš„å½±å“.

Væ˜¯ä¸€ç§éå¸¸ç®€å•çš„è¯­è¨€. çœ‹å®Œè¿™ç¯‡æ–‡æ¡£å¤§çº¦éœ€è¦ä¸€ä¸ªå°æ—¶ï¼Œç»“æŸåä½ å°±ä¼šæŠŠè¿™é—¨è¯­è¨€å­¦å¾—å·®ä¸å¤šäº†.

æœ¬è¯­è¨€æå€¡ç”¨æœ€å°çš„æŠ½è±¡æ¥ç¼–å†™ç®€å•æ¸…æ™°çš„ä»£ç .

å°½ç®¡ç®€å•ï¼Œä½†Vç»™äº†å¼€å‘è€…å¾ˆå¤§çš„æƒåŠ›. ä»»ä½•ä½ åœ¨å…¶ä»–è¯­è¨€ä¸­å¯ä»¥åšåˆ°çš„äº‹æƒ…ï¼Œä½ ä¹Ÿéƒ½å¯ä»¥åœ¨Vä¸­åšåˆ°.

## ä»æºç å®‰è£…
è·å¾—æœ€æ–°, æœ€å®Œå–„çš„Vçš„ä¸»è¦æ–¹å¼æ˜¯__ä»æºç å¼€å§‹__. è¿™å¾ˆ__ç®€å•__ï¼Œé€šå¸¸__åªéœ€è¦å‡ ç§’é’Ÿ__.

### Linux, macOS, FreeBSDç­‰:
ä½ éœ€è¦`git`å’Œä¸€ä¸ªCç¼–è¯‘å™¨, æ¯”å¦‚`tcc`, `gcc` æˆ– `clang`, ä»¥åŠ`make`:
```bash
git clone https://github.com/vlang/v
cd v
make
```

### Windows:
ä½ éœ€è¦`git`å’Œä¸€ä¸ªCç¼–è¯‘å™¨, æ¯”å¦‚`tcc`, `gcc`, `clang` æˆ– `msvc`:
```bash
git clone https://github.com/vlang/v
cd v
make.bat -tcc
```
NB: å¦‚æœä½ å–œæ¬¢ä½¿ç”¨ä¸åŒçš„Cç¼–è¯‘å™¨ï¼Œä¹Ÿå¯ä»¥å°†`-gcc`, `-msvc`, `-clang`ä¸­çš„ä¸€ä¸ªä½œä¸ºå‚æ•°ä¼ å…¥make.batï¼Œä½†æ˜¯`-tcc`ä½“ç§¯å°, é€Ÿåº¦å¿«, å®‰è£…æ–¹ä¾¿(å› ä¸ºVä¼šè‡ªåŠ¨ä¸‹è½½ä¸€ä¸ªå·²é¢„åˆ¶çš„äºŒè¿›åˆ¶æ–‡ä»¶).

å»ºè®®å°†è¿™ä¸ªæ–‡ä»¶å¤¹æ·»åŠ åˆ°ç¯å¢ƒå˜é‡çš„PATHä¸­. è¿™å¯ä»¥é€šè¿‡å‘½ä»¤`v.exe symlink`æ¥å®Œæˆ.

### Android
é€šè¿‡[vab](https://github.com/vlang/vab)ä¹Ÿå¯ä»¥åœ¨Androidä¸Šè¿è¡ŒVå›¾å½¢åº”ç”¨.

V Android ä¾èµ–: **V**, **Java JDK** >= 8, Android **SDK + NDK**.

  1. Install dependencies (see [vab](https://github.com/vlang/vab))
  2. Connect your Android device
  3. Run:
  ```bash
  git clone https://github.com/vlang/vab && cd vab && v vab.v
  ./vab --device auto run /path/to/v/examples/sokol/particles
  ```
æ›´å¤šç»†èŠ‚å’Œæ•…éšœæ’æŸ¥, è¯·æµè§ˆ[vab GitHub repository](https://github.com/vlang/vab).

## Table of Contents

<table>
    <tr><td width=33% valign=top>

* [Hello world](#hello-world)
* [Running a project folder](#running-a-project-folder-with-several-files)
* [Comments](#comments)
* [Functions](#functions)
    * [Returning multiple values](#returning-multiple-values)
* [Symbol visibility](#symbol-visibility)
* [Variables](#variables)
* [Types](#types)
    * [Strings](#strings)
    * [Numbers](#numbers)
    * [Arrays](#arrays)
    * [Fixed size arrays](#fixed-size-arrays)
    * [Maps](#maps)
* [Module imports](#module-imports)
* [Statements & expressions](#statements--expressions)
    * [If](#if)
    * [In operator](#in-operator)
    * [For loop](#for-loop)
    * [Match](#match)
    * [Defer](#defer)
* [Structs](#structs)
    * [Embedded structs](#embedded-structs)
    * [Default field values](#default-field-values)
    * [Short struct literal syntax](#short-struct-initialization-syntax)
    * [Access modifiers](#access-modifiers)
    * [Methods](#methods)

</td><td width=33% valign=top>

* [Unions](#unions)
* [Functions 2](#functions-2)
    * [Pure functions by default](#pure-functions-by-default)
    * [Mutable arguments](#mutable-arguments)
    * [Variable number of arguments](#variable-number-of-arguments)
    * [Anonymous & high order functions](#anonymous--high-order-functions)
* [References](#references)
* [Constants](#constants)
* [Builtin functions](#builtin-functions)
* [Printing custom types](#printing-custom-types)
* [Modules](#modules)
* [Types 2](#types-2)
    * [Interfaces](#interfaces)
    * [Enums](#enums)
    * [Sum types](#sum-types)
    * [Option/Result types & error handling](#optionresult-types-and-error-handling)
* [Generics](#generics)
* [Concurrency](#concurrency)
    * [Spawning Concurrent Tasks](#spawning-concurrent-tasks)
    * [Channels](#channels)
    * [Shared Objects](#shared-objects)
* [Decoding JSON](#decoding-json)
* [Testing](#testing)
* [Memory management](#memory-management)
* [ORM](#orm)

</td><td valign=top>

* [Writing documentation](#writing-documentation)
* [Tools](#tools)
    * [v fmt](#v-fmt)
    * [Profiling](#profiling)
* [Advanced Topics](#advanced-topics)
    * [Memory-unsafe code](#memory-unsafe-code)
    * [Structs with reference fields](#structs-with-reference-fields)
    * [sizeof and __offsetof](#sizeof-and-__offsetof)
    * [Calling C from V](#calling-c-from-v)
    * [Debugging generated C code](#debugging-generated-c-code)
    * [Conditional compilation](#conditional-compilation)
    * [Compile time pseudo variables](#compile-time-pseudo-variables)
    * [Compile-time reflection](#compile-time-reflection)
    * [Limited operator overloading](#limited-operator-overloading)
    * [Inline assembly](#inline-assembly)
    * [Translating C to V](#translating-c-to-v)
    * [Hot code reloading](#hot-code-reloading)
    * [Cross compilation](#cross-compilation)
    * [Cross-platform shell scripts in V](#cross-platform-shell-scripts-in-v)
    * [Attributes](#attributes)
    * [Goto](#goto)
* [Appendices](#appendices)
    * [Keywords](#appendix-i-keywords)
    * [Operators](#appendix-ii-operators)

</td></tr>
</table>

<!--
NB: there are several special keywords, which you can put after the code fences for v:
compile, live, ignore, failcompile, oksyntax, badsyntax, wip, nofmt
For more details, do: `v check-md`
-->

## Hello World


```v
fn main() {
	println('hello world')
}
```

å°†ä¸Šé¢çš„ç‰‡æ®µä¿å­˜åˆ°`hello.v`, å†æ‰§è¡Œ`v run hello.v`.

> ä¸Šé¢å‡è®¾ä½ å·²æŒ‰ç…§[è¿™é‡Œ](https://github.com/vlang/v/blob/master/README.md#symlinking)æ‰€è¿°, ç”¨`v symlink`ä¸ºVå»ºç«‹äº†è½¯è¿æ¥. å¦‚æœè¿˜æ²¡æœ‰ï¼Œåˆ™å¿…é¡»æ‰‹åŠ¨è¾“å…¥Vçš„è·¯â€‹â€‹å¾„.

ç¥è´ºä½ : ä½ åˆšåˆšç¼–å†™å¹¶æ‰§è¡Œäº†ä½ çš„ç¬¬ä¸€ä¸ªVç¨‹åº!

ä½ ä¹Ÿå¯ä»¥é€šè¿‡`v hello.v`å®ç°ä»…ç¼–è¯‘è€Œä¸æ‰§è¡Œ. æŸ¥çœ‹`v help`å¯è·å¾—æ‰€æœ‰å—æ”¯æŒçš„å‘½ä»¤ä¿¡æ¯.

ä»ä¸Šé¢çš„ç¤ºä¾‹ä¸­, ä½ å¯ä»¥çœ‹åˆ°æ˜¯ä½¿ç”¨`fn`å…³é”®å­—æ¥å£°æ˜å‡½æ•°. è¿”å›ç±»å‹åœ¨å‡½æ•°åç§°ä¹‹åæŒ‡å®š. åœ¨è¿™ä¸ªä¾‹å­ä¸­, `main`ä¸è¿”å›ä»»ä½•å†…å®¹, å› æ­¤æ²¡æœ‰è¿”å›ç±»å‹.

ä¸è®¸å¤šå…¶ä»–è¯­è¨€(ä¾‹å¦‚C, Goå’ŒRust)ä¸€æ ·, `main`æ˜¯ç¨‹åºçš„å…¥å£.

`println`æ˜¯ä¸ºæ•°ä¸å¤šçš„å†…ç½®å‡½æ•°ä¹‹ä¸€. å®ƒå°†ä¼ é€’ç»™å®ƒçš„å€¼æ‰“å°åˆ°stdout.

å¯ä»¥åœ¨ä¸€ä¸ªæ–‡ä»¶ç¨‹åºä¸­èˆå¼ƒ`fn main()`å£°æ˜. åœ¨ç¼–å†™å°å‹ç¨‹åºï¼Œ"è„šæœ¬"æˆ–ä»…å­¦ä¹ è¯­è¨€æ—¶å¾ˆæœ‰ç”¨. ä¸ºç®€ä¾¿èµ·è§, æœ¬æ•™ç¨‹ä¸­å°†è·³è¿‡`fn main()`.

è¿™æ„å‘³ç€Vä¸­çš„`hello world`ç¨‹åºéå¸¸ç®€å•.

```v
println('hello world')
```

## è¿è¡ŒåŒ…å«å¤šä¸ªæ–‡ä»¶çš„é¡¹ç›®æ–‡ä»¶å¤¹

å‡è®¾ä½ æœ‰ä¸€ä¸ªåŒ…å«å¤šä¸ª`.v`æ–‡ä»¶çš„æ–‡ä»¶å¤¹, å…¶ä¸­ä¸€ä¸ªæ–‡ä»¶åŒ…å«`main()`å‡½æ•°, å…¶ä»–æ–‡ä»¶å…·æœ‰å…¶ä»–è¾…åŠ©å‡½æ•°. å®ƒä»¬å¯èƒ½æ˜¯æŒ‰åŠŸèƒ½è¿›è¡Œç»„ç»‡çš„, ä½†ä»ä¸è¶³ä»¥ä½¿å…¶æˆä¸ºå„è‡ªç‹¬ç«‹çš„å¯é‡ç”¨æ¨¡å—ï¼ŒåŒæ—¶ä½ å¸Œæœ›å°†å®ƒä»¬å…¨éƒ¨ç¼–è¯‘åˆ°ä¸€ä¸ªç¨‹åºä¸­.

åœ¨å…¶ä»–è¯­è¨€ä¸­, ä½ å°†å¿…é¡»ä½¿ç”¨includeæˆ–æ„å»ºç³»ç»Ÿæ¥æšä¸¾æ‰€æœ‰æ–‡ä»¶, å°†å®ƒä»¬åˆ†åˆ«ç¼–è¯‘ä¸ºç›®æ ‡æ–‡ä»¶, ç„¶åå°†å®ƒä»¬é“¾æ¥ä¸ºä¸€ä¸ªæœ€ç»ˆçš„å¯æ‰§è¡Œæ–‡ä»¶.

ä½†æ˜¯, åœ¨Vä¸­ä»…ä½¿ç”¨`v run`å³å¯ä¸€èµ·ç¼–è¯‘å¹¶è¿è¡Œæ•´ä¸ªæ–‡ä»¶å¤¹ä¸­çš„.væ–‡ä»¶. è¯¥å‘½ä»¤ä¹Ÿæ”¯æŒä¼ é€’å‚æ•°, å› æ­¤å¯æ‰§è¡Œæ“ä½œ: `v run . --yourparam some_other_stuff`.

ä¸Šé¢çš„å‘½ä»¤é¦–å…ˆä¼šå°†è¿™äº›æ–‡ä»¶ç¼–è¯‘ä¸ºä¸€ä¸ªç¨‹åº(ä»¥æ–‡ä»¶å¤¹/é¡¹ç›®å‘½å), ç„¶åæ‰§è¡Œè¯¥ç¨‹åºæ—¶ä¼šå°†`--yourparam some_other_stuff`ä½œä¸ºCLIå‚æ•°ä¼ é€’ç»™å®ƒ.

ä½ çš„ç¨‹åºå¯ä»¥å¦‚ä¸‹æ–¹å¼ä½¿ç”¨CLIå‚æ•°ï¼š 
```v
import os

println(os.args)
```
NB: è¿è¡ŒæˆåŠŸå, Vä¼šåˆ é™¤ç”Ÿæˆçš„å¯æ‰§è¡Œæ–‡ä»¶. å¦‚æœä½ æƒ³ä¿ç•™å®ƒ, å¯ä»¥ä½¿ç”¨`v -keepc run .`ä»£æ›¿, æˆ–è€…ç›´æ¥ç”¨`v .`æ‰‹åŠ¨ç¼–è¯‘.

NB: ä»»ä½•Vç¼–è¯‘å™¨çš„æ ‡å¿—éƒ½åº”è¯¥åœ¨`run`å‘½ä»¤ä¹‹å‰ä¼ é€’. æºæ–‡ä»¶/æ–‡ä»¶å¤¹ä¹‹åçš„æ‰€æœ‰å‚æ•°å†…å®¹éƒ½å°†åŸå°ä¸åŠ¨åœ°ä¼ é€’ç»™ç¼–å‡ºçš„ç¨‹åºï¼ŒVä¸ä¼šå¯¹å…¶è¿›è¡Œå¤„ç†.

## æ³¨é‡Š

```v
// This is a single line comment.
/*
This is a multiline comment.
   /* It can be nested. */
*/
```

## å‡½æ•°

```v
fn main() {
	println(add(77, 33))
	println(sub(100, 50))
}

fn add(x int, y int) int {
	return x + y
}

fn sub(x int, y int) int {
	return x - y
}
```

åŒæ ·ï¼Œç±»å‹ä¹Ÿæ˜¯åœ¨å‚æ•°åç§°ä¹‹å.

å°±åƒåœ¨Goå’ŒCä¸­ä¸€æ ·ï¼Œå‡½æ•°ä¸èƒ½è¢«é‡è½½. è¿™ç®€åŒ–äº†ä»£ç , æé«˜äº†å¯ç»´æŠ¤æ€§å’Œå¯è¯»æ€§.

å‡½æ•°å¯ä»¥åœ¨å£°æ˜ä¹‹å‰ä½¿ç”¨: `add`å’Œ`sub`åœ¨`main`ä¹‹åå£°æ˜ï¼Œä½†ä»ç„¶å¯ä»¥åœ¨`main`ä¸­è°ƒç”¨. è¿™å¯¹Vä¸­çš„æ‰€æœ‰å£°æ˜éƒ½æ˜¯å¦‚æ­¤, è€Œä¸”ä¸éœ€è¦å¤´æ–‡ä»¶æˆ–è€ƒè™‘æ–‡ä»¶å’Œå£°æ˜çš„é¡ºåº.

### å¤šå€¼è¿”å›

```v
fn foo() (int, int) {
	return 2, 3
}

a, b := foo()
println(a) // 2
println(b) // 3
c, _ := foo() // ignore values using `_`
```

## ç¬¦å·å¯è§æ€§

```v
pub fn public_function() {
}

fn private_function() {
}
```

å‡½æ•°é»˜è®¤æ˜¯ç§æœ‰çš„(ä¸å¯¼å‡º). è¦å…è®¸å…¶ä»–æ¨¡å—ä½¿ç”¨å®ƒä»¬, è¯·åœ¨å‰é¢åŠ ä¸Š`pub`. è¿™åŒæ ·é€‚ç”¨äºå¸¸é‡å’Œç±»å‹.

æ³¨æ„: `pub`åªèƒ½åœ¨ä¸€ä¸ªå‘½åçš„æ¨¡å—ä¸­ä½¿ç”¨. å…³äºåˆ›å»ºæ¨¡å—çš„ä¿¡æ¯ï¼Œè¯·å‚è§[Modules](#modules).

## å˜é‡

```v
name := 'Bob'
age := 20
large_number := i64(9999999999)
println(name)
println(age)
println(large_number)
```

å˜é‡æ˜¯ç”¨`:=`æ¥å£°æ˜å’Œåˆå§‹åŒ–çš„, è¿™æ˜¯Vä¸­å£°æ˜å˜é‡çš„å”¯ä¸€æ–¹å¼, è¿™æ„å‘³ç€å˜é‡æ€»æ˜¯æœ‰ä¸€ä¸ªåˆå§‹å€¼.

å˜é‡çš„ç±»å‹æ˜¯ç”±å³ä¾§çš„å€¼æ¨æ–­å‡ºæ¥çš„. è¦è½¬æ¢ä¸åŒçš„ç±»å‹, å¯ä»¥ä½¿ç”¨ç±»å‹è½¬æ¢: è¡¨è¾¾å¼T(v)å¯å°†å€¼vè½¬æ¢ä¸ºç±»å‹T.

ä¸å¤§å¤šæ•°å…¶ä»–è¯­è¨€ä¸åŒ, Våªå…è®¸åœ¨å‡½æ•°ä¸­å®šä¹‰å˜é‡. å…¨å±€(æ¨¡å—çº§)å˜é‡æ˜¯ä¸å…è®¸çš„. åœ¨Vä¸­æ²¡æœ‰å…¨å±€çŠ¶æ€(è¯¦è§[é»˜è®¤æƒ…å†µä¸‹çš„çº¯å‡½æ•°](#é»˜è®¤æƒ…å†µä¸‹çš„çº¯å‡½æ•°))

ä¸ºäº†åœ¨ä¸åŒçš„ä»£ç åº“ä¸­ä¿æŒä¸€è‡´, æ‰€æœ‰çš„å˜é‡å’Œå‡½æ•°åéƒ½å¿…é¡»ä½¿ç”¨`snake_case`é£æ ¼, è€Œç±»å‹ååˆ™å¿…é¡»ä½¿ç”¨`PascalCase`.

### å¯å˜å˜é‡

```v
mut age := 20
println(age)
age = 21
println(age)
```

æ”¹å˜å˜é‡çš„å€¼å¯ä½¿ç”¨`=`. åœ¨Vä¸­ï¼Œå˜é‡é»˜è®¤æ˜¯ä¸å¯æ”¹å˜çš„. ä¸ºäº†èƒ½å¤Ÿæ”¹å˜å˜é‡çš„å€¼ï¼Œä½ å¿…é¡»ç”¨`mut`å£°æ˜å®ƒ.

æŠŠç¬¬ä¸€è¡Œçš„`mut`å»æ‰å†è¯•ç€ç¼–è¯‘ä¸Šé¢çš„ç¨‹åº.

### åˆå§‹åŒ– vs èµ‹å€¼

æ³¨æ„: `:=` å’Œ `=` çš„é‡å¤§åŒºåˆ«: `:=`ç”¨äºå£°æ˜å’Œåˆå§‹åŒ–, `=`ç”¨äºèµ‹å€¼.

```v failcompile
fn main() {
    age = 21
}
```

ç”±äºæœªå£°æ˜å˜é‡`age`, å› æ­¤è¯¥ä»£ç å°†æ— æ³•ç¼–è¯‘. åœ¨Vä¸­æ‰€æœ‰å˜é‡éƒ½éœ€è¦å£°æ˜.

```v
fn main() {
	age := 21
}
```

å¯ä»¥åœ¨ä¸€è¡Œä¸­æ›´æ”¹å¤šä¸ªå˜é‡çš„å€¼. è¿™æ ·, å¯ä»¥åœ¨æ²¡æœ‰ä¸­é—´å˜é‡çš„æƒ…å†µä¸‹äº¤æ¢å®ƒä»¬çš„å€¼.

```v
mut a := 0
mut b := 1
println('$a, $b') // 0, 1
a, b = b, a
println('$a, $b') // 1, 0
```

### é”™è¯¯çš„å£°æ˜

åœ¨å¼€å‘æ¨¡å¼ä¸‹, ç¼–è¯‘å™¨å°†è­¦å‘Šä½ å°šæœªä½¿ç”¨çš„å˜é‡(ä½ å°†æ”¶åˆ°"unused variable"çš„è­¦å‘Š). åœ¨ç”Ÿäº§æ¨¡å¼ä¸‹(é€šè¿‡å°†`-prod`æ ‡å¿—ä¼ é€’ç»™v, æ¯”å¦‚`v -prod foo.v`), å®ƒæ ¹æœ¬ä¸ä¼šç¼–è¯‘(å°±åƒåœ¨Goä¸­ä¸€æ ·).

```v failcompile
fn main() {
    a := 10
    if true {
        a := 20 // error: redefinition of `a`
    }
    // warning: unused variable `a`
}
```

ä¸å¤§å¤šæ•°è¯­è¨€ä¸åŒï¼Œä¸å…è®¸ä½¿ç”¨å˜é‡è¦†ç›–. å£°æ˜ä¸€ä¸ªåœ¨çˆ¶ä½œç”¨åŸŸä¸­å·²å£°æ˜çš„åŒåå˜é‡å°†å¯¼è‡´ç¼–è¯‘é”™è¯¯.

ä½†æ˜¯ï¼Œå¯ä»¥å¯¹å¯¼å…¥çš„æ¨¡å—è¿›è¡Œè¦†ç›–å¤„ç†ï¼Œå› ä¸ºåœ¨æŸäº›æƒ…å†µä¸‹å®ƒéå¸¸æœ‰ç”¨ï¼š 
```v ignore
import ui
import gg

fn draw(ctx &gg.Context) {
    gg := ctx.parent.get_ui().gg
    gg.draw_rect(10, 10, 100, 50)
}
```

## ç±»å‹

### åŸºç¡€ç±»å‹

```v ignore
bool

string

i8    i16  int  i64      i128 (soon)
byte  u16  u32  u64      u128 (soon)

rune // represents a Unicode code point

f32 f64

byteptr, voidptr, charptr, size_t // these are mostly used for C interoperability

any // similar to C's void* and Go's interface{}
```

è¯·æ³¨æ„, ä¸Cå’ŒGoä¸åŒ, `int`æ€»æ˜¯ä¸€ä¸ª32ä½çš„æ•´æ•°.

Vä¸­çš„æ‰€æœ‰è¿ç®—ç¬¦çš„ä¸¤è¾¹å¿…é¡»æ˜¯ç›¸åŒç±»å‹çš„å€¼è¿™ä¸€è§„åˆ™æœ‰ä¸€ä¸ªä¾‹å¤–: å¦‚æœä¸€è¾¹çš„åŸºç¡€ç±»å‹å®Œå…¨é€‚åˆäºå¦ä¸€è¾¹ç±»å‹çš„æ•°æ®èŒƒå›´, å°±å¯ä»¥è‡ªåŠ¨æ¨å¯¼. ä¸‹é¢æ˜¯å…è®¸çš„å¯èƒ½æ€§:

```v ignore
   i8 â†’ i16 â†’ int â†’ i64
                  â†˜     â†˜
                    f32 â†’ f64
                  â†—     â†—
 byte â†’ u16 â†’ u32 â†’ u64 â¬
      â†˜     â†˜     â†˜      ptr
   i8 â†’ i16 â†’ int â†’ i64 â¬
```

ä¾‹å¦‚, ä¸€ä¸ª`int`å€¼å¯ä»¥è‡ªåŠ¨æå‡åˆ°`f64`æˆ–`i64`ï¼Œä½†ä¸èƒ½æå‡åˆ°u32(u32æ„å‘³ç€è´Ÿå€¼çš„ç¬¦å·ä¸¢å¤±). ç„¶è€Œ, ä»`int`åˆ°`f32`çš„æå‡ç›®å‰æ˜¯è‡ªåŠ¨å®Œæˆçš„(ä½†å¯¹äºå¤§å€¼æ¥è¯´å¯èƒ½ä¼šå¯¼è‡´ç²¾åº¦æŸå¤±).

åƒ`123`æˆ–`4.56`è¿™æ ·çš„å­—é¢é‡ä¼šä»¥ç‰¹æ®Šçš„æ–¹å¼å¤„ç†. å®ƒä»¬ä¸ä¼šå¯¼è‡´ç±»å‹æå‡, ä½†æ˜¯å½“å®ƒä»¬çš„ç±»å‹éœ€è¦ç¡®å®šæ—¶, å®ƒä»¬ä¼šåˆ†åˆ«é»˜è®¤ä¸ºintå’Œf64.


```v nofmt
u := u16(12)
v := 13 + u    // v is of type `u16` - no promotion
x := f32(45.6)
y := x + 3.14  // x is of type `f32` - no promotion
a := 75        // a is of type `int` - default for int literal
b := 14.7      // b is of type `f64` - default for float literal
c := u + a     // c is of type `int` - automatic promotion of `u`'s value
d := b + x     // d is of type `f64` - automatic promotion of `x`'s value
```

### å­—ç¬¦ä¸²

```v
name := 'Bob'
println(name.len)
println(name[0]) // indexing gives a byte B
println(name[1..3]) // slicing gives a string 'ob'
windows_newline := '\r\n' // escape special characters like in C
assert windows_newline.len == 2
```

åœ¨Vä¸­, å­—ç¬¦ä¸²æ˜¯ä¸€ä¸ªåªè¯»çš„å­—èŠ‚æ•°ç»„. å­—ç¬¦ä¸²æ•°æ®ä½¿ç”¨UTF-8ç¼–ç . å­—ç¬¦ä¸²çš„å€¼æ˜¯ä¸å¯æ”¹å˜çš„, ä½ ä¸èƒ½å¯¹å…ƒç´ è¿›è¡Œä¿®æ”¹:

```v failcompile
mut s := 'hello ğŸŒ'
s[0] = `H` // not allowed
```
> error: cannot assign to `s[i]` since V strings are immutable

è¯·æ³¨æ„: ç´¢å¼•ä¸€ä¸ªå­—ç¬¦ä¸²å°†äº§ç”Ÿä¸€ä¸ª`byte`, è€Œä¸æ˜¯ä¸€ä¸ª`rune`. ç´¢å¼•å¯¹åº”çš„æ˜¯å­—ç¬¦ä¸²ä¸­çš„å­—èŠ‚ï¼Œè€Œä¸æ˜¯Unicodeç ç‚¹.

å­—ç¬¦çš„ç±»å‹æ˜¯`rune`. è¦è¡¨ç¤ºå®ƒä»¬, ä½¿ç”¨ä½¿ç”¨"`"åŒ…è£¹:

```v
rocket := `ğŸš€`
assert 'aloha!'[0] == `a`
```

å•å¼•å·å’ŒåŒå¼•å·éƒ½å¯ä»¥ç”¨æ¥è¡¨ç¤ºå­—ç¬¦ä¸². ä¸ºäº†ä¿æŒä¸€è‡´æ€§, vfmtä¼šå°†åŒå¼•å·è½¬æ¢ä¸ºå•å¼•å·, é™¤éå­—ç¬¦ä¸²ä¸­åŒ…å«ä¸€ä¸ªå•å¼•å·å­—ç¬¦.

å¯¹äºrawå­—ç¬¦ä¸², è¯·åœ¨å‰é¢åŠ ä¸Šr, rawå­—ç¬¦ä¸²å°±ä¸ä¼šè¢«è½¬ä¹‰:

```v
s := r'hello\nworld'
println(s) // "hello\nworld"
```

å­—ç¬¦å¯ä»¥å¾ˆå®¹æ˜“åœ°è½¬æ¢ä¸ºæ•´æ•°:

```v
s := '42'
n := s.int() // 42
```

### å­—ç¬¦ä¸²æ’å€¼

åŸºç¡€çš„æ’å€¼è¯­æ³•éå¸¸ç®€å•: åœ¨å˜é‡åå‰ä½¿ç”¨`$`å³å¯. å˜é‡å°†è¢«è½¬æ¢ä¸ºä¸€ä¸ªå­—ç¬¦ä¸²å¹¶åµŒå…¥åˆ°å­—é¢é‡ä¸­:
```v
name := 'Bob'
println('Hello, $name!') // Hello, Bob!
```
å®ƒä¹Ÿé€‚ç”¨äºå­—æ®µ: `'age = $user.age'`.
å¦‚æœä½ éœ€è¦æ›´å¤æ‚çš„è¡¨è¾¾å¼è¯·ä½¿ç”¨`${}`: `'can register = ${user.age > 13}'`.

ä¹Ÿæ”¯æŒç±»ä¼¼äº C è¯­è¨€çš„`printf()`çš„æ ¼å¼æŒ‡å®šç¬¦. `f`, `g`, `x`ç­‰æ˜¯å¯é€‰çš„, ç”¨äºæŒ‡å®šäº†è¾“å‡ºæ ¼å¼. ç¼–è¯‘å™¨ä¼šè€ƒè™‘åˆ°å­˜å‚¨å¤§å°, æ‰€ä»¥æ²¡æœ‰`hd`å’Œ`llu`.

```v
x := 123.4567
println('x = ${x:4.2f}')
println('[${x:10}]') // pad with spaces on the left => [   123.457]
println('[${int(x):-10}]') // pad with spaces on the right => [123       ]
println('[${int(x):010}]') // pad with zeros on the left => [0000000123]
```

### å­—ç¬¦ä¸²æ“ä½œç¬¦

```v
name := 'Bob'
bobby := name + 'by' // + is used to concatenate strings
println(bobby) // "Bobby"
mut s := 'hello '
s += 'world' // `+=` is used to append to a string
println(s) // "hello world"
```

Vä¸­çš„æ‰€æœ‰è¿ç®—ç¬¦çš„ä¸¤è¾¹å¿…é¡»å…·æœ‰ç›¸åŒç±»å‹. ä½ ä¸èƒ½å°†æ•´æ•°ä¸å­—ç¬¦ä¸²è¿æ¥èµ·æ¥:

```v failcompile
age := 10
println('age = ' + age) // not allowed
```
> error: infix expr: cannot use `int` (right expression) as `string`

æˆ‘ä»¬å¯ä½¿ç”¨å°†`age`è½¬æˆ`string`:

```v
age := 11
println('age = ' + age.str())
```

æˆ–ä½¿ç”¨å­—ç¬¦ä¸²å†…æ’æ³•(é¦–é€‰):

```v
age := 12
println('age = $age')
```

### æ•°å€¼

```v
a := 123
```

è¿™å°†æŠŠ123çš„å€¼åˆ†é…ç»™`a`. é»˜è®¤æƒ…å†µä¸‹, `a`çš„å€¼ä¸º`int`ç±»å‹.

ä½ ä¹Ÿå¯ä»¥ç”¨åå…­è¿›åˆ¶, äºŒè¿›åˆ¶æˆ–å…«è¿›åˆ¶æ¥è¡¨ç¤ºæ•´æ•°:

```v
a := 0x7B
b := 0b01111011
c := 0o173
```

æ‰€æœ‰è¿™äº›éƒ½å°†è¢«åˆ†é…ç›¸åŒçš„å€¼123. å®ƒä»¬çš„ç±»å‹éƒ½æ˜¯`int`ï¼Œä¸ç®¡ä½ ç”¨ä»€ä¹ˆå†™æ³•.

Vè¿˜æ”¯æŒç”¨`_`ä½œä¸ºåˆ†éš”ç¬¦å†™æ•°å­—:

```v
num := 1_000_000 // same as 1000000
three := 0b0_11 // same as 0b11
float_num := 3_122.55 // same as 3122.55
hexa := 0xF_F // same as 255
oct := 0o17_3 // same as 0o173
```

å¦‚æœä½ æƒ³è¦ä¸€ä¸ªä¸åŒç±»å‹çš„æ•´æ•°, ä½ å¯ä»¥ä½¿ç”¨ç±»å‹è½¬æ¢:

```v
a := i64(123)
b := byte(42)
c := i16(12345)
```

æµ®ç‚¹æ•°çš„èµ‹å€¼æ–¹æ³•ä¹Ÿæ˜¯ä¸€æ ·çš„:

```v
f := 1.0
f1 := f64(3.14)
f2 := f32(3.14)
```

å¦‚æœä½ æ²¡æœ‰æ˜ç¡®æŒ‡å®šç±»å‹, é»˜è®¤æƒ…å†µä¸‹, æµ®ç‚¹å­—é¢é‡å°†æ˜¯`f64`çš„ç±»å‹.

### æ•°ç»„

```v
mut nums := [1, 2, 3]
println(nums) // "[1, 2, 3]"
println(nums[1]) // "2"
nums[1] = 5
println(nums) // "[1, 5, 3]"
println(nums.len) // "3"
nums = [] // The array is now empty
println(nums.len) // "0"
// Declare an empty array:
users := []int{}
```

æ•°ç»„çš„ç±»å‹ç”±ç¬¬ä¸€ä¸ªå…ƒç´ å†³å®š:
* `[1, 2, 3]` æ˜¯intç±»å‹çš„æ•°ç»„ (`[]int`).
* `['a', 'b']` æ˜¯stringç±»å‹çš„æ•°ç»„ (`[]string`).

ç”¨æˆ·å¯ä»¥æ˜ç¡®æŒ‡å®šç¬¬ä¸€ä¸ªå…ƒç´ çš„ç±»å‹ï¼š`[byte(16)ï¼Œ32ï¼Œ64ï¼Œ128]`. Væ•°ç»„æ˜¯åŒè´¨çš„(æ‰€æœ‰å…ƒç´ å¿…é¡»å…·æœ‰ç›¸åŒçš„ç±»å‹).
è¿™æ„å‘³ç€åƒ`[1, 'a']`è¿™æ ·çš„ä»£ç å°†æ— æ³•ç¼–è¯‘.

`.len`å­—æ®µä¼šè¿”å›æ•°ç»„çš„é•¿åº¦. æ³¨æ„è¿™æ˜¯ä¸€ä¸ªåªè¯»å­—æ®µ, å¹¶ä¸”ç”¨æˆ·ä¸èƒ½ä¿®æ”¹. åœ¨Vä¸­, å¯¼å‡ºçš„å­—æ®µé»˜è®¤ä¸ºåªè¯». å‚è€ƒ[è®¿é—®ä¿®æ”¹å™¨](#è®¿é—®ä¿®æ”¹å™¨)

#### æ•°å€¼æ“ä½œç¬¦

```v
mut nums := [1, 2, 3]
nums << 4
println(nums) // "[1, 2, 3, 4]"
// append array
nums << [5, 6, 7]
println(nums) // "[1, 2, 3, 4, 5, 6, 7]"
mut names := ['John']
names << 'Peter'
names << 'Sam'
// names << 10  <-- This will not compile. `names` is an array of strings.
println(names.len) // "3"
println('Alex' in names) // "false"
```

`<<<`æ˜¯ä¸€ä¸ªè¿ç®—ç¬¦, å®ƒå°†ä¸€ä¸ªå€¼è¿½åŠ åˆ°æ•°ç»„çš„æœ«å°¾, å®ƒä¹Ÿå¯ä»¥è¿½åŠ æ•´ä¸ªæ•°ç»„.

`val in array`è¡¨ç¤ºå¦‚æœæ•°ç»„ä¸­åŒ…å«`val`, åˆ™è¿”å›true. å‚è§[`in`è¿ç®—ç¬¦](#in-è¿ç®—ç¬¦).

#### åˆå§‹åŒ–æ•°ç»„å±æ€§

åœ¨åˆå§‹åŒ–è¿‡ç¨‹ä¸­, ä½ å¯ä»¥æŒ‡å®šæ•°ç»„çš„å®¹é‡(`cap`), åˆå§‹é•¿åº¦(`len`) å’Œé»˜è®¤å…ƒç´ (`init`):

```v
arr := []int{len: 5, init: -1}
// `[-1, -1, -1, -1, -1]`
```

è®¾ç½®å®¹é‡å¯ä»¥æé«˜æ’å…¥çš„æ€§èƒ½, å› ä¸ºå®ƒå‡å°‘äº†æ‰€éœ€çš„é‡æ–°åˆ†é…æ¬¡æ•°:

```v
mut numbers := []int{cap: 1000}
println(numbers.len) // 0
// Now appending elements won't reallocate
for i in 0 .. 1000 {
	numbers << i
}
```
æ³¨æ„: ä¸Šé¢çš„ä»£ç ä½¿ç”¨äº†[range `for`](#range-for)è¯­å¥.

#### æ•°ç»„æ–¹æ³•

æ‰€æœ‰çš„æ•°ç»„éƒ½å¯ä»¥å¾ˆå®¹æ˜“åœ°ç”¨`println(arr)`æ‰“å°å‡ºæ¥, å¹¶ç”¨`s :=arr.str()`è½¬æ¢ä¸ºä¸€ä¸ªå­—ç¬¦ä¸².

ç”¨`.clone()`å¯å¤åˆ¶æ•°ç»„ä¸­çš„æ•°æ®:

```v
nums := [1, 2, 3]
nums_copy := nums.clone()
```

æ•°ç»„å¯ä»¥é€šè¿‡`.filter()'å’Œ`.map()'æœ‰æ•ˆåœ°è¿‡æ»¤å’Œæ˜ å°„:

```v
nums := [1, 2, 3, 4, 5, 6]
even := nums.filter(it % 2 == 0)
println(even) // [2, 4, 6]
// filter can accept anonymous functions
even_fn := nums.filter(fn (x int) bool {
	return x % 2 == 0
})
println(even_fn)
words := ['hello', 'world']
upper := words.map(it.to_upper())
println(upper) // ['HELLO', 'WORLD']
// map can also accept anonymous functions
upper_fn := words.map(fn (w string) string {
	return w.to_upper()
})
println(upper_fn) // ['HELLO', 'WORLD']
```

`it`æ˜¯ä¸€ä¸ªå†…ç½®çš„å˜é‡, å®ƒæŒ‡çš„æ˜¯å½“å‰åœ¨filter/mapæ–¹æ³•ä¸­å¤„ç†çš„å…ƒç´ .

æ­¤å¤–, `.any()`å’Œ`.all()`å¯ä»¥ç”¨æ¥æ–¹ä¾¿åœ°æµ‹è¯•æ»¡è¶³æ¡ä»¶çš„å…ƒç´ .

```v
nums := [1, 2, 3]
println(nums.any(it == 2)) // true
println(nums.all(it >= 2)) // false
```

#### å¤šç»´æ•°ç»„

æ•°ç»„å¯ä»¥æœ‰å¤šä¸ªç»´åº¦.

äºŒç»´æ•°ç»„çš„ä¾‹å­:
```v
mut a := [][]int{len: 2, init: []int{len: 3}}
a[0][1] = 2
println(a) // [[0, 2, 0], [0, 0, 0]]
```

3ç»´æ•°ç»„çš„ä¾‹å­:
```v
mut a := [][][]int{len: 2, init: [][]int{len: 3, init: []int{len: 2}}}
a[0][1][1] = 2
println(a) // [[[0, 0], [0, 2], [0, 0]], [[0, 0], [0, 0], [0, 0]]]
```

#### æ•°ç»„æ’åº

å¯¹å„ç§æ•°ç»„è¿›è¡Œæ’åºæ˜¯éå¸¸ç®€å•å’Œç›´è§‚çš„. ç‰¹æ®Šå˜é‡`a`å’Œ`b`å¯ç”¨äºè‡ªå®šä¹‰æ’åºçš„æ¡ä»¶.

```v
mut numbers := [1, 3, 2]
numbers.sort() // 1, 2, 3
numbers.sort(a > b) // 3, 2, 1
```

```v
struct User {
	age  int
	name string
}

mut users := [User{21, 'Bob'}, User{20, 'Zarkon'}, User{25, 'Alice'}]
users.sort(a.age < b.age) // sort by User.age int field
users.sort(a.name > b.name) // reverse sort by User.name string field
```

#### æ•°ç»„slice

Sliceæ˜¯æ•°ç»„çš„éƒ¨åˆ†, å®ƒä»¬è¡¨ç¤ºä¸¤ä¸ªç”¨`...`è¿ç®—ç¬¦åˆ†éš”çš„ç´¢å¼•ä¹‹é—´çš„æ¯ä¸ªå…ƒç´ . å³è¾¹çš„æŒ‡æ•°å¿…é¡»å¤§äºæˆ–ç­‰äºå·¦è¾¹çš„ç´¢å¼•.

å¦‚æœæ²¡æœ‰å³ä¾§çš„ç´¢å¼•, åˆ™å‡å®šä¸ºæ•°ç»„çš„é•¿åº¦. å¦‚æœä¸€ä¸ªå·¦ä¾§æŒ‡æ•°ä¸å­˜åœ¨, åˆ™å‡è®¾ä¸º0.

```v
nums := [0, 10, 20, 30, 40]
println(nums[1..4]) // [10, 20, 30]
println(nums[..4]) // [0, 10, 20, 30]
println(nums[1..]) // [10, 20, 30, 40]
```

æ‰€æœ‰çš„æ•°ç»„æ“ä½œéƒ½é€‚ç”¨äºåˆ‡ç‰‡.
åˆ†ç‰‡å¯ä»¥è¢«è¿½åŠ åˆ°åŒä¸€ç±»å‹çš„æ•°ç»„ä¸Š:

```v
array_1 := [3, 5, 4, 7, 6]
mut array_2 := [0, 1]
array_2 << array_1[..3]
println(array_2) // [0, 1, 3, 5, 4]
```

### Fixed size arrays

Vè¿˜æ”¯æŒå›ºå®šå¤§å°çš„æ•°ç»„. ä¸æ™®é€šæ•°ç»„ä¸åŒï¼Œå®ƒä»¬çš„é•¿åº¦æ˜¯å›ºå®šçš„. ä½ ä¸èƒ½ç»™å®ƒä»¬è¿½åŠ å…ƒç´ ï¼Œä¹Ÿä¸èƒ½ç¼©å°å®ƒä»¬. ä½ åªèƒ½ä¿®æ”¹å®ƒä»¬çš„å…ƒç´ .

ä¸è¿‡, ä¸æ™®é€šæ•°ç»„ä¸åŒ, è®¿é—®å›ºå®šå¤§å°çš„æ•°ç»„å…ƒç´ çš„æ•ˆç‡æ›´é«˜, å®ƒä»¬æ¯”æ™®é€šæ•°ç»„å ç”¨æ›´å°‘çš„å†…å­˜. å®ƒä»¬çš„æ•°æ®åœ¨å †æ ˆä¸Š, æ‰€ä»¥ä½ å¯æŠŠå®ƒä»¬ä½œä¸ºç¼“å†²åŒºä½¿ç”¨, è€Œä¸éœ€è¦é¢å¤–çš„å †åˆ†é….

å¤§å¤šæ•°æ–¹æ³•éƒ½è¢«å®šä¹‰ä¸ºåœ¨æ™®é€šæ•°ç»„ä¸Šï¼Œè€Œä¸æ˜¯åœ¨å›ºå®šå¤§å°çš„æ•°ç»„ä¸Š. ä½†ä½ å¯ä»¥é€šè¿‡åˆ†ç‰‡å°†å›ºå®šå¤§å°çš„æ•°ç»„è½¬æ¢ä¸ºæ™®é€šæ•°ç»„:
```v
mut fnums := [3]int{} // fnums is a fixed size array with 3 elements.
fnums[0] = 1
fnums[1] = 10
fnums[2] = 100
println(fnums) // => [1, 10, 100]
println(typeof(fnums).name) // => [3]int

anums := fnums[0..fnums.len]
println(anums) // => [1, 10, 100]
println(typeof(anums).name) // => []int
```
è¯·æ³¨æ„: åˆ‡ç‰‡ä¼šå¯¼è‡´å›ºå®šå¤§å°çš„æ•°ç»„çš„æ•°æ®ä¼šè¢«å¤åˆ¶åˆ°æ–°åˆ›å»ºçš„æ™®é€šæ•°ç»„ä¸­.

### Map

```v
mut m := map[string]int{} // a map with `string` keys and `int` values
m['one'] = 1
m['two'] = 2
println(m['one']) // "1"
println(m['bad_key']) // "0"
println('bad_key' in m) // Use `in` to detect whether such key exists
m.delete('two')
```
mapå¯ä½¿ç”¨string, rune, integer, float æˆ– voidpträ½œä¸ºkey. 

æ•´ä¸ªmapå¯ä»¥ä½¿ç”¨è¿™ä¸ªç®€çŸ­çš„è¯­æ³•æ¥åˆå§‹åŒ–:
```v
numbers := map{
	1: 'one'
	2: 'two'
}
println(numbers)
```

å¦‚æœä¸€ä¸ªkeyæ²¡æ‰¾åˆ°å°±ä¼šè¿”å›å¯¹åº”å€¼çš„é›¶å€¼:

```v
sm := map{
	'abc': 'xyz'
}
val := sm['bad_key']
println(val) // ''
```
```v
intm := map{
	1: 1234
	2: 5678
}
s := intm[3]
println(s) // 0
```

ä¹Ÿå¯ä»¥ä½¿ç”¨`or {}`ä»£ç å—æ¥å¤„ç†ä¸¢å¤±çš„key:

```v
mm := map[string]int{}
val := mm['bad_key'] or { panic('key not found') }
```

åŒæ ·çš„å¯é€‰æ£€æŸ¥ä¹Ÿé€‚ç”¨äºæ•°ç»„:

```v
arr := [1, 2, 3]
large_index := 999
val := arr[large_index] or { panic('out of bounds') }
```

## Moduleå¯¼å…¥

å…³äºåˆ›å»ºmoduleå¯å‚è€ƒ [Modules](#modules).

Moduleå¯¼å…¥ä½¿ç”¨`import`å…³é”®è¯:

```v
import os

fn main() {
	// read text from stdin
	name := os.input('Enter your name: ')
	println('Hello, $name!')
}
```
è¿™ä¸ªç¨‹åºå¯ä»¥ä½¿ç”¨`os`æ¨¡å—ä¸­çš„ä»»ä½•å…¬å…±å®šä¹‰ï¼Œå¦‚`input`å‡½æ•°. è§[æ ‡å‡†åº“](https://modules.vlang.io/)æ–‡æ¡£ä¸­çš„å¸¸ç”¨æ¨¡å—åŠå…¶å…¬å…±ç¬¦å·çš„åˆ—è¡¨.

é»˜è®¤æƒ…å†µä¸‹, æ¯æ¬¡è°ƒç”¨å¤–éƒ¨å‡½æ•°æ—¶éƒ½å¿…é¡»æŒ‡å®šæ¨¡å—å‰ç¼€. è¿™åœ¨ä¸€å¼€å§‹å¯èƒ½ä¼šæ˜¾å¾—å¾ˆå•°å—¦ï¼Œä½†å®ƒä½¿ä»£ç æ›´æ˜“è¯», å¹¶ä¸”æ›´å®¹æ˜“ç†è§£ - æ€»æ˜¯å¾ˆæ¸…æ¥šåœ°çŸ¥é“å“ªä¸ªå‡½æ•°ä»
å“ªä¸ªæ¨¡å—ä¸­æ¥. è¿™åœ¨å¤§å‹ä»£ç åº“ä¸­ç‰¹åˆ«æœ‰ç”¨.

å¾ªç¯å¯¼å…¥æ˜¯ä¸å…è®¸çš„, è¿™ä¸Goä¸€æ ·.

### é€‰æ‹©æ€§å¯¼å…¥

ä½ ä¹Ÿå¯ä»¥ç›´æ¥ä»æ¨¡å—ä¸­å¯¼å…¥ç‰¹å®šçš„å‡½æ•°å’Œç±»å‹:

```v
import os { input }

fn main() {
	// read text from stdin
	name := input('Enter your name: ')
	println('Hello, $name!')
}
```
æ³¨æ„: å¸¸é‡ä¸å…è®¸è¿™æ ·åš, å®ƒä»¬å¿…é¡»æ€»æ˜¯æœ‰å‰ç¼€.

ä½ å¯ä»¥åŒæ—¶å¯¼å…¥å‡ ä¸ªç‰¹å®šçš„ç¬¦å·:

```v
import os { input, user_os }

name := input('Enter your name: ')
println('Name: $name')
os := user_os()
println('Your OS is ${os}.')
```

### Moduleå¯¼å…¥æ”¯æŒalias

ä»»ä½•å¯¼å…¥çš„æ¨¡å—åéƒ½å¯ä»¥ä½¿ç”¨`as`å…³é”®å­—è¿›è¡Œé‡å‘½å.

æ³¨æ„ï¼šé™¤éä½ åˆ›å»ºäº†`mymod/sha256.v`, å¦åˆ™è¿™ä¸ªä¾‹å­ä¸ä¼šè¢«ç¼–è¯‘.
```v failcompile
import crypto.sha256
import mymod.sha256 as mysha256

fn main() {
    v_hash := sha256.sum('hi'.bytes()).hex()
    my_hash := mysha256.sum('hi'.bytes()).hex()
    assert my_hash == v_hash
}
```

ä½ ä¸èƒ½å¯¹ä¸€ä¸ªå¯¼å…¥çš„å‡½æ•°æˆ–ç±»å‹è¿›è¡Œåˆ«å. ä½†æ˜¯, ä½ å¯ä»¥é‡æ–°å£°æ˜ä¸€ä¸ªç±»å‹.

```v
import time
import math

type MyTime = time.Time

fn (mut t MyTime) century() int {
	return int(1.0 + math.trunc(f64(t.year) * 0.009999794661191))
}

fn main() {
	mut my_time := MyTime{
		year: 2020
		month: 12
		day: 25
	}
	println(time.new_time(my_time).utc_string())
	println('Century: $my_time.century()')
}
```

## è¯­å¥å’Œè¡¨è¾¾å¼

### If

```v
a := 10
b := 20
if a < b {
	println('$a < $b')
} else if a > b {
	println('$a > $b')
} else {
	println('$a == $b')
}
```

`if`è¯­å¥éå¸¸ç›´æ¥, ä¸å…¶ä»–å¤§å¤šæ•°è¯­è¨€ç±»ä¼¼. ä¸å…¶ä»–ç±»ä¼¼Cè¯­è¨€ä¸åŒçš„æ˜¯, æ¡ä»¶å‘¨å›´æ²¡æœ‰æ‹¬å·, è€Œä¸”æ€»æ˜¯éœ€è¦æ‹¬å·(åŒ…è£¹ä»£ç å—).

`if`å¯ä»¥ä½œä¸ºè¡¨è¾¾å¼ä½¿ç”¨:

```v
num := 777
s := if num % 2 == 0 { 'even' } else { 'odd' }
println(s)
// "odd"
```

#### ç±»å‹æ£€æŸ¥å’Œå¼ºåˆ¶è½¬æ¢
You can check the current type of a sum type using `is` and its negated form `!is`.
ä½ å¯ä»¥ä½¿ç”¨`is`å’Œå®ƒçš„å¦å®šå½¢å¼`!is`æ¥æ£€æŸ¥ä¸€ä¸ªå’Œç±»å‹çš„å½“å‰ç±»å‹.

ä½ å¯ä»¥åœ¨`if`ä¸­è¿›è¡Œ:
```v
struct Abc {
	val string
}

struct Xyz {
	foo string
}

type Alphabet = Abc | Xyz

x := Alphabet(Abc{'test'}) // sum type
if x is Abc {
	// x is automatically casted to Abc and can be used here
	println(x)
}
if x !is Abc {
	println('Not Abc')
}
```
æˆ–ä½¿ç”¨`match`:
```v oksyntax
match x {
	Abc {
		// x is automatically casted to Abc and can be used here
		println(x)
	}
	Xyz {
		// x is automatically casted to Xyz and can be used here
		println(x)
	}
}
```

è¿™ä¹Ÿé€‚ç”¨äºstructçš„å­—æ®µ:
```v
struct MyStruct {
	x int
}

struct MyStruct2 {
	y string
}

type MySumType = MyStruct | MyStruct2

struct Abc {
	bar MySumType
}

x := Abc{
	bar: MyStruct{123} // MyStruct will be converted to MySumType type automatically
}
if x.bar is MyStruct {
	// x.bar is automatically casted
	println(x.bar)
}
match x.bar {
	MyStruct {
		// x.bar is automatically casted
		println(x.bar)
	}
	else {}
}
```

å¯å˜çš„å˜é‡å¯ä»¥å‘ç”Ÿå˜åŒ–, è¿›è¡Œç±»å‹è½¬æ¢æ˜¯ä¸å®‰å…¨çš„. ç„¶è€Œ, æœ‰æ—¶å°½ç®¡å˜é‡æ˜¯å¯å˜çš„, ä½†è¿˜æ˜¯éœ€è¦è¿›è¡Œç±»å‹è½¬æ¢. åœ¨è¿™ç§æƒ…å†µä¸‹, å¼€å‘è€…å¿…é¡»ç”¨`mut`å…³é”®å­—æ¥æ ‡è®°è¡¨è¾¾å¼, æ¥å‘Šè¯‰ç¼–è¯‘å™¨ä½ çŸ¥é“è‡ªå·±åœ¨åšä»€ä¹ˆ.

å®ƒçš„æ–¹å¼æ˜¯è¿™æ ·çš„:
```v oksyntax
mut x := MySumType(MyStruct{123})
if mut x is MyStruct {
	// x is casted to MyStruct even if it's mutable
	// without the mut keyword that wouldn't work
	println(x)
}
// same with match
match mut x {
	MyStruct {
		// x is casted to MyStruct even it's mutable
		// without the mut keyword that wouldn't work
		println(x)
	}
}
```

### Inæ“ä½œç¬¦

`in`å…è®¸æ£€æŸ¥ä¸€ä¸ªæ•°ç»„æˆ–ä¸€ä¸ªmapæ˜¯å¦åŒ…å«ä¸€ä¸ªå…ƒç´ .
ç›¸åæ“ä½œç”¨`!in`.

```v
nums := [1, 2, 3]
println(1 in nums) // true
println(4 !in nums) // true
m := map{
	'one': 1
	'two': 2
}
println('one' in m) // true
println('three' !in m) // true
```

å®ƒå¯¹äºä¹¦å†™å¸ƒå°”è¡¨è¾¾å¼ä¹Ÿå¾ˆæœ‰ç”¨, ä½¿å…¶æ›´æ¸…æ™°, æ›´ç´§å‡‘:

```v
enum Token {
	plus
	minus
	div
	mult
}

struct Parser {
	token Token
}

parser := Parser{}
if parser.token == .plus || parser.token == .minus || parser.token == .div || parser.token == .mult {
	// ...
}
if parser.token in [.plus, .minus, .div, .mult] {
	// ...
}
```

Vä¼˜åŒ–äº†è¿™ç§è¡¨è¾¾æ–¹å¼. æ‰€ä»¥ä¸Šé¢ä¸¤ä¸ª`if`è¯­å¥äº§ç”Ÿçš„æœºå™¨ä»£ç æ˜¯ä¸€æ ·çš„, éƒ½æ²¡æœ‰åˆ›å»ºæ•°ç»„.

### Forå¾ªç¯

Våªæœ‰ä¸€ä¸ªå¾ªç¯å…³é”®è¯: `for`, ä½†æœ‰å¤šç§å½¢å¼.

#### `for`/`in`

è¿™æ˜¯æœ€å¸¸è§çš„å½¢å¼. ä½ å¯ä»¥åœ¨æ•°ç»„, mapæˆ–æ•°å€¼èŒƒå›´ä¸­ä½¿ç”¨å®ƒ.

##### Array `for`

```v
numbers := [1, 2, 3, 4, 5]
for num in numbers {
	println(num)
}
names := ['Sam', 'Peter']
for i, name in names {
	println('$i) $name')
	// Output: 0) Sam
	//         1) Peter
}
```

`for value in arr`å½¢å¼ç”¨äºéå†ä¸€ä¸ªæ•°ç»„çš„å…ƒç´ . å¦‚æœéœ€è¦ç´¢å¼•, å¯ä»¥ä½¿ç”¨å¦ä¸€ç§å½¢å¼`for index, value in arr`.

æ³¨æ„: è¿™ä¸ªå€¼æ˜¯åªè¯»çš„. å¦‚æœä½ éœ€è¦åœ¨å¾ªç¯æ—¶ä¿®æ”¹æ•°ç»„, ä½ éœ€è¦å°†å…ƒç´ å£°æ˜ä¸ºå¯å˜çš„.

```v
mut numbers := [0, 1, 2]
for mut num in numbers {
	num++
}
println(numbers) // [1, 2, 3]
```
å½“ä¸€ä¸ªæ ‡è¯†ç¬¦åªæ˜¯ä¸€ä¸ªä¸‹åˆ’çº¿æ—¶, å®ƒå°†è¢«å¿½ç•¥.

##### Map `for`

```v
m := map{
	'one': 1
	'two': 2
}
for key, value in m {
	println('$key -> $value')
	// Output: one -> 1
	//         two -> 2
}
```

é€šè¿‡ä½¿ç”¨ä¸€ä¸ªä¸‹åˆ’çº¿ä½œä¸ºæ ‡è¯†ç¬¦, å¯ä»¥å¿½ç•¥ä»»ä½•ä¸€ä¸ªé”®æˆ–å€¼.
```v
m := map{
	'one': 1
	'two': 2
}
// iterate over keys
for key, _ in m {
	println(key)
	// Output: one
	//         two
}
// iterate over values
for _, value in m {
	println(value)
	// Output: 1
	//         2
}
```

##### Range `for`

```v
// Prints '01234'
for i in 0 .. 5 {
	print(i)
}
```
`low..high`æŒ‡çš„æ˜¯ä¸€ä¸ª*æ’ä»–æ€§*çš„èŒƒå›´, ä»£è¡¨ä»`low`å¼€å§‹åˆ°`high`(ä¸åŒ…æ‹¬`high`)ä¸­çš„æ‰€æœ‰æ•°å€¼.

#### Condition `for`

```v
mut sum := 0
mut i := 0
for i <= 100 {
	sum += i
	i++
}
println(sum) // "5050"
```

è¿™ç§å½¢å¼çš„å¾ªç¯ç±»ä¼¼äºå…¶ä»–è¯­è¨€ä¸­çš„`while`å¾ªç¯. ä¸€æ—¦å¸ƒå°”æ¡ä»¶å€¼ä¸ºfalse, å¾ªç¯å°†åœæ­¢è¿­ä»£. åŒæ ·åœ°, æ¡ä»¶å‘¨å›´æ²¡æœ‰æ‹¬å·, è€Œä¸”ä»£ç å—æ€»æ˜¯éœ€è¦æ‹¬å·.

#### Bare `for`

```v
mut num := 0
for {
	num += 2
	if num >= 10 {
		break
	}
}
println(num) // "10"
```

è¿™ä¸ªæ¡ä»¶å¯ä»¥çœç•¥, ä¼šå¯¼è‡´æ— é™å¾ªç¯.

#### C `for`

```v
for i := 0; i < 10; i += 2 {
	// Don't print 6
	if i == 6 {
		continue
	}
	println(i)
}
```

æœ€åæ˜¯ä¼ ç»Ÿçš„Cé£æ ¼çš„`for`å¾ªç¯. å®ƒæ¯”`while`å½¢å¼æ›´å®‰å…¨. å› ä¸ºä½¿ç”¨åè€…, å¾ˆå®¹æ˜“å¿˜è®°æ›´æ–°è®¡æ•°å™¨, è€Œå¯¼è‡´å¡åœ¨ä¸€ä¸ªæ— é™å¾ªç¯ä¸­.

è¿™é‡Œ`i`ä¸éœ€è¦ç”¨`mut`æ¥å£°æ˜, å› ä¸ºæ ¹æ®å®šä¹‰, å®ƒæ˜¯å¯å˜çš„.

#### å¸¦æ ‡ç­¾çš„break/continue

`break`å’Œ`continue`é»˜è®¤æ§åˆ¶çš„æ˜¯æœ€é‡Œé¢çš„`for`å¾ªç¯. ä½ ä¹Ÿå¯ä»¥ä½¿ç”¨`break` å’Œ `continue` åé¢çš„æ ‡ç­¾åæ¥è·³è½¬åˆ°å¤–éƒ¨çš„`for`å¾ªç¯:
å¾ªç¯ã€‚

```v
outer: for i := 4; true; i++ {
	println(i)
	for {
		if i < 7 {
			continue outer
		} else {
			break outer
		}
	}
}
```
labelå¿…é¡»ç´§æ¥åœ¨å¤–éƒ¨å¾ªç¯ä¹‹å‰.
ä¸Šé¢çš„ä»£ç ä¼šæ‰“å°:
```
4
5
6
7
```

### Match

```v
os := 'windows'
print('V is running on ')
match os {
	'darwin' { println('macOS.') }
	'linux' { println('Linux.') }
	else { println(os) }
}
```

matchè¯­å¥æ˜¯ç¼–å†™ä¸€ç³»åˆ—`if - else`è¯­å¥çš„è¾ƒçŸ­æ–¹æ³•. å½“æ‰¾åˆ°ä¸€ä¸ªåŒ¹é…çš„åˆ†æ”¯æ—¶, å°†æ‰§è¡Œå…¶ä¸­çš„è¯­å¥å—. å½“æ²¡æœ‰å…¶ä»–åˆ†æ”¯åŒ¹é…æ—¶åˆ™æ‰§è¡Œelseåˆ†æ”¯.

```v
number := 2
s := match number {
	1 { 'one' }
	2 { 'two' }
	else { 'many' }
}
```

matchè¡¨è¾¾å¼ä»åŒ¹é…åˆ†æ”¯è¿”å›æœ€ç»ˆè¡¨è¾¾å¼çš„å€¼.

```v
enum Color {
	red
	blue
	green
}

fn is_red_or_blue(c Color) bool {
	return match c {
		.red, .blue { true } // comma can be used to test multiple values
		.green { false }
	}
}
```

matchè¯­å¥ä¹Ÿå¯ä»¥ä½¿ç”¨ç®€å†™çš„`.variant_here`è¯­æ³•ä½œä¸º`enum`å˜ä½“çš„åˆ†æ”¯. å½“æ‰€æœ‰çš„åˆ†æ”¯éƒ½æ˜¯æ— ç©·å°½çš„æ—¶å€™, æ­¤æ—¶ä¸å…è®¸ä½¿ç”¨`else`åˆ†æ”¯.


```v
c := `v`
typ := match c {
	`0`...`9` { 'digit' }
	`A`...`Z` { 'uppercase' }
	`a`...`z` { 'lowercase' }
	else { 'other' }
}
println(typ)
// 'lowercase'
```

ä½ ä¹Ÿå¯ä»¥ä½¿ç”¨èŒƒå›´ä½œä¸º`match`æ¨¡å¼. å¦‚æœæ•°å€¼åœ¨åˆ†æ”¯çš„èŒƒå›´å†…, åˆ™è¯¥åˆ†æ”¯å°†è¢«æ‰§è¡Œ.

è¯·æ³¨æ„: èŒƒå›´ä½¿ç”¨`...`(ä¸‰ç‚¹)è€Œä¸æ˜¯`...`(ä¸¤ç‚¹). è¿™å°±æ˜¯å› ä¸ºèŒƒå›´æ˜¯*åŒ…å«*æœ€åä¸€ä¸ªå…ƒç´ çš„, è€Œä¸æ˜¯æ’ä»–æ€§çš„(æ¯”å¦‚`..`èŒƒå›´). åœ¨åŒ¹é…åˆ†æ”¯ä¸­ä½¿ç”¨`.`å°†å¼•å‘ä¸€ä¸ªé”™è¯¯.

æ³¨æ„: `match`ä½œä¸ºè¡¨è¾¾å¼ä¸èƒ½ç”¨äº`for`å¾ªç¯å’Œ`if`è¯­å¥.

### Defer

deferè¯­å¥ä¼šæ¨è¿Ÿæ‰§è¡Œä¸€ç»„è¯­å¥, ç›´åˆ°å¤–å›´å‡½æ•°è¿”å›.

```v
import os

fn read_log() {
	mut ok := false
	mut f := os.open('log.txt') or { panic(err.msg) }
	defer {
		f.close()
	}
	// ...
	if !ok {
		// defer statement will be called here, the file will be closed
		return
	}
	// ...
	// defer statement will be called here, the file will be closed
}
```

## Structs

```v
struct Point {
	x int
	y int
}

mut p := Point{
	x: 10
	y: 20
}
println(p.x) // Struct fields are accessed using a dot
// Alternative literal syntax for structs with 3 fields or fewer
p = Point{10, 20}
assert p.x == 10
```

### Heap structs

structæ˜¯åœ¨å †æ ˆä¸Šåˆ†é…çš„. è¦åœ¨å †ä¸Šåˆ†é…ä¸€ä¸ªstructå¹¶è·å¾—å¯¹å®ƒçš„å¼•ç”¨éœ€ä½¿ç”¨`&`å‰ç¼€:

```v
struct Point {
	x int
	y int
}

p := &Point{10, 10}
// References have the same syntax for accessing fields
println(p.x)
```

`p`çš„ç±»å‹æ˜¯`&Point`. å®ƒæ˜¯å¯¹`Point`çš„[å¼•ç”¨](#references). å¼•ç”¨ç±»ä¼¼äºGoæŒ‡é’ˆå’ŒC++å¼•ç”¨.

### åµŒå…¥å¼struct

Vä¸å…è®¸å­ç±»ï¼Œä½†å®ƒæ”¯æŒåµŒå…¥å¼struct:

```v
struct Widget {
mut:
	x int
	y int
}

struct Button {
	Widget
	title string
}

mut button := Button{
	title: 'Click me'
}
button.x = 3
```
å¦‚æœæ²¡æœ‰åµŒå…¥, æˆ‘ä»¬å°±å¿…é¡»ç»™`Widget`å­—æ®µå‘½å, ç„¶ååšä»¥ä¸‹æ“ä½œ:

```v oksyntax
button.widget.x = 3
```

### å­—æ®µé»˜è®¤å€¼

```v
struct Foo {
	n   int    // n is 0 by default
	s   string // s is '' by default
	a   []int  // a is `[]int{}` by default
	pos int = -1 // custom default value
}
```

åœ¨åˆ›å»ºç»“æ„çš„è¿‡ç¨‹ä¸­, æ‰€æœ‰çš„ç»“æ„å­—æ®µé»˜è®¤ä¸ºé›¶å€¼. æ•°ç»„å’Œmapå­—æ®µä¼šè¢«åˆ†é…. ä½†ä¹Ÿå¯ä»¥å®šä¹‰è‡ªå®šä¹‰çš„é»˜è®¤å€¼.

### Required fields

```v
struct Foo {
	n int [required]
}
```

ä½ å¯ä»¥ç”¨`[required]`å±æ€§æ ‡è®°ä¸€ä¸ªç»“æ„ä½“å­—æ®µ, å‘Šè¯‰Vå½“åˆ›å»ºè¯¥ç»“æ„çš„å®ä¾‹æ—¶, å¿…é¡»åˆå§‹åŒ–è¯¥å­—æ®µ.

ç”±äºå­—æ®µ`n`æ²¡æœ‰è¢«æ˜¾å¼åˆå§‹åŒ–, è¿™ä¸ªä¾‹å­å°†æ— æ³•ç¼–è¯‘:
```v failcompile
_ = Foo{}
```

<a id='short-struct-initialization-syntax' />

### ç®€çŸ­çš„structå­—é¢é‡è¯­æ³•

```v
struct Point {
	x int
	y int
}

mut p := Point{
	x: 10
	y: 20
}
// you can omit the struct name when it's already known
p = {
	x: 30
	y: 4
}
assert p.y == 4
```

çœç•¥structåä¹Ÿå¯ä»¥ç”¨äºè¿”å›ä¸€ä¸ªstructçš„å­—é¢é‡æˆ–ä½œä¸ºå‡½æ•°å‚æ•°ä¼ é€’ä¸€ä¸ªstructå­—é¢é‡.

#### å°¾éšstructçš„å­—é¢é‡å‚æ•°

V doesn't have default function arguments or named arguments, for that trailing struct
literal syntax can be used instead

Væ²¡æœ‰é»˜è®¤çš„å‡½æ•°å‚æ•°æˆ–å‘½åçš„å‚æ•°, ä½†å¯ç”¨å°¾éšstructçš„å­—é¢é‡å‚æ•°æ¥ä»£æ›¿:

```v
struct ButtonConfig {
	text        string
	is_disabled bool
	width       int = 70
	height      int = 20
}

struct Button {
	text   string
	width  int
	height int
}

fn new_button(c ButtonConfig) &Button {
	return &Button{
		width: c.width
		height: c.height
		text: c.text
	}
}

button := new_button(text: 'Click me', width: 100)
// the height is unset, so it's the default value
assert button.height == 20
```

å¦‚ä½ æ‰€è§, structåç§°å’ŒèŠ±æ‹¬å·éƒ½å¯ä»¥çœç•¥, è€Œä¸æ˜¯:

```v oksyntax nofmt
new_button(ButtonConfig{text:'Click me', width:100})
```

è¿™ä»…é€‚ç”¨äºä¸ºæœ€åä¸€ä¸ªå‚æ•°æ˜¯structçš„å‡½æ•°.

### è®¿é—®ä¿®æ”¹å™¨

structå­—æ®µé»˜è®¤æ˜¯ç§æœ‰çš„, ä¸å¯å˜çš„(ä½¿å¾—ç»“æ„ä¹Ÿæ˜¯ä¸å¯å˜çš„). å®ƒä»¬çš„è®¿é—®ä¿®é¥°ç¬¦å¯ä»¥ç”¨`pub`å’Œ`mut`, æ€»å…±æœ‰5ä¸ªå¯èƒ½çš„é€‰é¡¹.

```v
struct Foo {
	a int // private immutable (default)
mut:
	b int // private mutable
	c int // (you can list multiple fields with the same access modifier)
pub:
	d int // public immutable (readonly)
pub mut:
	e int // public, but mutable only in parent module
__global:
	// (not recommended to use, that's why the 'global' keyword starts with __)
	f int // public and mutable both inside and outside parent module
}
```

ä¾‹å¦‚, è¿™é‡Œæ˜¯åœ¨`builtin`æ¨¡å—ä¸­å®šä¹‰çš„`string`ç±»å‹:

```v ignore
struct string {
    str byteptr
pub:
    len int
}
```

ä»è¿™ä¸ªå®šä¹‰ä¸­ä¸éš¾çœ‹å‡º, `string`æ˜¯ä¸€ä¸ªä¸å¯æ”¹å˜çš„ç±»å‹, å®ƒåŒ…å«å­—ç¬¦ä¸²æ•°æ®çš„å­—èŠ‚æŒ‡é’ˆåœ¨`builtin`ä¹‹å¤–æ ¹æœ¬æ— æ³•è®¿é—®. `len`å­—æ®µæ˜¯å…¬å…±çš„, ä½†æ˜¯ä¸å¯å˜çš„:
```v failcompile
fn main() {
    str := 'hello'
    len := str.len // OK
    str.len++      // Compilation error
}
```

è¿™æ„å‘³ç€åœ¨Vä¸­å®šä¹‰å…¬å¼€å¯è¯»å­—æ®µéå¸¸å®¹æ˜“, ä¸éœ€è¦ä½¿ç”¨getters/settersæˆ–å±æ€§.

## Methods

```v
struct User {
	age int
}

fn (u User) can_register() bool {
	return u.age > 16
}

user := User{
	age: 10
}
println(user.can_register()) // "false"
user2 := User{
	age: 20
}
println(user2.can_register()) // "true"
```

Væ²¡æœ‰ç±», ä½†ä½ å¯ä»¥åœ¨ç±»å‹ä¸Šå®šä¹‰æ–¹æ³•. ä¸€ä¸ªæ–¹æ³•æ˜¯ä¸€ä¸ªå¸¦æœ‰ç‰¹æ®Šæ¥å—è€…å‚æ•°çš„å‡½æ•°, æ¥å—è€…ä¼šå‡ºç°åœ¨å®ƒè‡ªå·±çš„å‚æ•°åˆ—è¡¨ä¸­ï¼Œä½äº`fn`å…³é”®å­—å’Œæ–¹æ³•åä¹‹é—´.
æ¥æ”¶è€…å‡ºç°åœ¨å®ƒè‡ªå·±çš„å‚æ•°åˆ—è¡¨ä¸­ï¼Œä½äº`fn`å…³é”®å­—å’Œæ–¹æ³•åä¹‹é—´ã€‚
æ–¹æ³•å¿…é¡»ä¸æ¥å—è€…ç±»å‹åœ¨åŒä¸€ä¸ªæ¨¡å—ä¸­.

åœ¨è¿™ä¸ªä¾‹å­ä¸­, `can_register`æ–¹æ³•æœ‰ä¸€ä¸ªåä¸º`u`, ç±»å‹æ˜¯`User`çš„æ¥æ”¶å™¨. æƒ¯ä¾‹æ˜¯ä¸ä½¿ç”¨è¯¸å¦‚`self`æˆ–`this`è¿™æ ·çš„æ¥æ”¶è€…åç§°, 
ä½†åå­—è¦ç®€çŸ­ï¼Œæœ€å¥½æ˜¯ä¸€ä¸ªå­—æ¯é•¿åº¦.

## Unions

å°±åƒstructä¸€æ ·ï¼Œunionæ”¯æŒåµŒå…¥.

```v
struct Rgba32_Component {
	r byte
	g byte
	b byte
	a byte
}

union Rgba32 {
	Rgba32_Component
	value u32
}

clr1 := Rgba32{
	value: 0x008811FF
}

clr2 := Rgba32{
	Rgba32_Component: {
		a: 128
	}
}

sz := sizeof(Rgba32)
unsafe {
	println('Size: ${sz}B,clr1.b: $clr1.b,clr2.b: $clr2.b')
}
```

Output: `Size: 4B, clr1.b: 136, clr2.b: 0`

unionæˆå‘˜çš„è®¿é—®å¿…é¡»åœ¨ä¸€ä¸ª`unsafe`å—ä¸­è¿›è¡Œ.

è¯·æ³¨æ„: åµŒå…¥çš„structå‚æ•°ä¸ä¸€å®šæŒ‰æ‰€åˆ—é¡ºåºå­˜å‚¨.

## Functions 2

### é»˜è®¤æ˜¯çº¯å‡½æ•°

Vå‡½æ•°é»˜è®¤ä¸ºçº¯å‡½æ•°, è¿™æ„å‘³ç€å®ƒä»¬çš„è¿”å›å€¼åªç”±å®ƒä»¬çš„å‚æ•°å†³å®šï¼Œè€Œä¸”å‡½æ•°çš„è®¡ç®—æ±‚å€¼æ—¶æ²¡æœ‰ä»»ä½•å‰¯ä½œç”¨(é™¤äº†I/O).
å‚æ•°ï¼Œè€Œä¸”å®ƒä»¬çš„è¯„ä¼°æ²¡æœ‰ä»»ä½•å‰¯ä½œç”¨ï¼ˆï¼‰ã€‚

è¿™æ˜¯å› ä¸ºç¼ºå°‘å…¨å±€å˜é‡å’Œæ‰€æœ‰å‡½æ•°å‚æ•°åœ¨é»˜è®¤æƒ…å†µä¸‹æ˜¯ä¸å¯å˜çš„, å³ä½¿åœ¨ä¼ é€’[å¼•ç”¨](#å¼•ç”¨)æ—¶ä¹Ÿæ˜¯å¦‚æ­¤.

ç„¶è€Œ, Vå¹¶ä¸æ˜¯ä¸€ç§çº¯ç²¹çš„å‡½æ•°å¼è¯­è¨€.

æœ‰ä¸€ä¸ªç¼–è¯‘å™¨æ ‡å¿—æ¥å¯ç”¨å…¨å±€å˜é‡(`--enable-globals`), ä½†è¿™æ˜¯ä¸€ä¸ªå¾ˆé‡è¦çš„æ ‡å¿—, ç”¨äºä½çº§åº”ç”¨, å¦‚å†…æ ¸å’Œé©±åŠ¨ç¨‹åº.

### å¯å˜å‚æ•°

å¯ä»¥é€šè¿‡ä½¿ç”¨å…³é”®å­— `mut`æ¥ä¿®æ”¹å‡½æ•°å‚æ•°:

```v
struct User {
	name string
mut:
	is_registered bool
}

fn (mut u User) register() {
	u.is_registered = true
}

mut user := User{}
println(user.is_registered) // "false"
user.register()
println(user.is_registered) // "true"
```

åœ¨æœ¬ä¾‹ä¸­, receiver(å®ƒåªæ˜¯ç¬¬ä¸€ä¸ªå‚æ•°)è¢«æ ‡è®°ä¸ºå¯å˜çš„ï¼Œå› æ­¤register()å¯ä»¥æ›´æ”¹userå¯¹è±¡. åŒæ ·é€‚ç”¨äºæ²¡æœ‰æ¥æ”¶è€…å‚æ•°çš„å‡½æ•°:

```v
fn multiply_by_2(mut arr []int) {
	for i in 0 .. arr.len {
		arr[i] *= 2
	}
}

mut nums := [1, 2, 3]
multiply_by_2(mut nums)
println(nums)
// "[2, 4, 6]"
```

æ³¨æ„: è°ƒç”¨è¿™ä¸ªå‡½æ•°æ—¶, å¿…é¡»åœ¨`nums`å‰é¢åŠ ä¸Š`mut`. å¾ˆæ˜æ˜¾, è¿™ä½¿å¾—è¢«è°ƒç”¨çš„å‡½æ•°å¯ä¿®æ”¹å€¼.

æœ€å¥½æ˜¯è¿”å›å€¼è€Œä¸æ˜¯ä¿®æ”¹å‚æ•°. ä¿®æ”¹å‚æ•°åº”è¯¥åªåœ¨åº”ç”¨ç¨‹åºçš„æ€§èƒ½å…³é”®éƒ¨åˆ†è¿›è¡Œ, ä»¥å‡å°‘åˆ†é…å’Œå¤åˆ¶.

å‡ºäºè¿™ä¸ªåŸå› , Vä¸å…è®¸ä¿®æ”¹åŸºç¡€ç±»å‹çš„å‚æ•°(ä¾‹å¦‚æ•´æ•°). åªæœ‰æ›´å¤æ‚çš„ç±»å‹, å¦‚æ•°ç»„å’Œmapæ‰å¯ä»¥è¢«ä¿®æ”¹.

ä½¿ç”¨`user.register()` æˆ– `user = register(user)` ä»£æ›¿ `register(mut user)`.

#### structæ›´æ–°è¯­æ³•

Vä½¿å®ƒå¾ˆå®¹æ˜“è¿”å›ä¸€ä¸ªå¯¹è±¡çš„ä¿®æ”¹ç‰ˆæœ¬:

```v
struct User {
	name          string
	age           int
	is_registered bool
}

fn register(u User) User {
	return {
		...u
		is_registered: true
	}
}

mut user := User{
	name: 'abc'
	age: 23
}
user = register(user)
println(user)
```

### å¯å˜çš„å‚æ•°æ•°é‡

```v
fn sum(a ...int) int {
	mut total := 0
	for x in a {
		total += x
	}
	return total
}

println(sum()) // 0
println(sum(1)) // 1
println(sum(2, 3)) // 5
// using array decomposition
a := [2, 3, 4]
println(sum(...a)) // <-- using prefix ... here. output: 9
b := [5, 6, 7]
println(sum(...b)) // output: 18
```

### åŒ¿åå’Œé«˜é˜¶å‡½æ•°

```v
fn sqr(n int) int {
	return n * n
}

fn cube(n int) int {
	return n * n * n
}

fn run(value int, op fn (int) int) int {
	return op(value)
}

fn main() {
	// Functions can be passed to other functions
	println(run(5, sqr)) // "25"
	// Anonymous functions can be declared inside other functions:
	double_fn := fn (n int) int {
		return n + n
	}
	println(run(5, double_fn)) // "10"
	// Functions can be passed around without assigning them to variables:
	res := run(5, fn (n int) int {
		return n + n
	})
	println(res) // "10"
	// You can even have an array/map of functions:
	fns := [sqr, cube]
	println(fns[0](10)) // "100"
	fns_map := map{
		'sqr':  sqr
		'cube': cube
	}
	println(fns_map['cube'](2)) // "8"
}
```

## å¼•ç”¨

```v
struct Foo {}

fn (foo Foo) bar_method() {
	// ...
}

fn bar_function(foo Foo) {
	// ...
}
```

å¦‚æœä¸€ä¸ªå‡½æ•°å‚æ•°æ˜¯ä¸å¯æ”¹å˜çš„(æ¯”å¦‚ä¸Šé¢ä¾‹å­ä¸­çš„`foo`), Vå¯ä»¥é€šè¿‡å€¼æˆ–å¼•ç”¨æ¥ä¼ é€’, è¿™ç”±ç¼–è¯‘å™¨å†³å®š, è€Œå¼€å‘è€…ä¸éœ€è¦è€ƒè™‘è¿™ä¸ªé—®é¢˜.

ä½ ä¸å†éœ€è¦è€ƒè™‘æ˜¯å¦åº”è¯¥é€šè¿‡å€¼è¿˜æ˜¯å¼•ç”¨æ¥ä¼ é€’struct.

ä½ å¯ä»¥é€šè¿‡å¢åŠ `&`ç¡®ä¿ç»“æ„ä½“æ€»æ˜¯é€šè¿‡å¼•ç”¨æ¥ä¼ é€’çš„:

```v
struct Foo {
	abc int
}

fn (foo &Foo) bar() {
	println(foo.abc)
}
```

`foo`ä»ç„¶æ˜¯ä¸å¯æ”¹å˜çš„. ä¸ºæ­¤å¿…é¡»ä½¿ç”¨`(mut foo Foo)`.

ä¸€èˆ¬æ¥è¯´ï¼ŒVçš„å¼•ç”¨ç±»ä¼¼äºGoæŒ‡é’ˆå’ŒC++å¼•ç”¨. ä¾‹å¦‚, ä¸€ä¸ªé€šç”¨çš„æ ‘ç»“æ„çš„å®šä¹‰æ˜¯è¿™æ ·çš„:

```v wip
struct Node<T> {
    val   T
    left  &Node
    right &Node
}
```

## å¸¸äº®

```v
const (
	pi    = 3.14
	world = 'ä¸–ç•Œ'
)

println(pi)
println(world)
```

å¸¸é‡ç”¨`const`æ¥å£°æ˜. å®ƒä»¬åªèƒ½è¢«å®šä¹‰ä¸ºåœ¨æ¨¡å—çº§(éœ€åœ¨å‡½æ•°å¤–).
å¸¸é‡å€¼æ°¸è¿œä¸èƒ½è¢«æ”¹å˜. ä½ ä¹Ÿå¯ä»¥åœ¨æ¨¡å—ä¸­å£°æ˜å•ä¸ªå¸¸é‡:

```v
const e = 2.71828
```

Vå¸¸é‡æ¯”å¤§å¤šæ•°è¯­è¨€æ›´çµæ´». ä½ å¯ä»¥åˆ†é…æ›´å¤æ‚çš„å€¼:

```v
struct Color {
	r int
	g int
	b int
}

fn rgb(r int, g int, b int) Color {
	return Color{
		r: r
		g: g
		b: b
	}
}

const (
	numbers = [1, 2, 3]
	red     = Color{
		r: 255
		g: 0
		b: 0
	}
	// evaluate function call at compile-time*
	blue    = rgb(0, 0, 255)
)

println(numbers)
println(red)
println(blue)
```
\* WIP - ç›®å‰å‡½æ•°è°ƒç”¨åœ¨ç¨‹åºå¯åŠ¨æ—¶è¿›è¡Œè¯„ä¼°.

å…¨å±€å˜é‡é€šå¸¸æ˜¯ä¸è¢«å…è®¸çš„, æ‰€ä»¥è¿™å¯èƒ½çœŸçš„å¾ˆæœ‰ç”¨.

### æ‰€éœ€æ¨¡å—å‰ç¼€

åœ¨å‘½åå¸¸é‡æ—¶, å¿…é¡»ä½¿ç”¨`snake_case`. ä¸ºäº†åŒºåˆ†å¸¸é‡å’Œå±€éƒ¨å˜é‡, å¿…é¡»æŒ‡å®šconstsçš„å®Œæ•´è·¯å¾„. ä¾‹å¦‚è¦è®¿é—®PIå¸¸é‡, å¿…é¡»åœ¨`math`ä¹‹å¤–ä½¿ç”¨å®Œæ•´çš„`math.pi`åç§°. è¿™ä¸€é™åˆ¶ä»…å¯¹`main`æ¨¡å—æ”¾å®½(åŒ…å«ä½ çš„`fn main()`), åœ¨è¿™é‡Œä½ å¯ä»¥ä½¿ç”¨éé™å®šåç§°çš„å¸¸é‡, å³`numbers`, è€Œä¸æ˜¯`main.numbers`.

vfmtä¼šå¤„ç†è¿™ä¸ªè§„åˆ™, æ‰€ä»¥ä½ å¯ä»¥åœ¨`math`æ¨¡å—ä¸­ä½¿ç”¨`println(pi)`, è€Œvfmtä¼šè‡ªåŠ¨æ›´æ–°ä¸º`println(math.pi)`.

<!--
Many people prefer all caps consts: `TOP_CITIES`. This wouldn't work
well in V, because consts are a lot more powerful than in other languages.
They can represent complex structures, and this is used quite often since there
are no globals:

```v oksyntax
println('Top cities: ${top_cities.filter(.usa)}')
```
-->

## å†…ç½®å‡½æ•°

æœ‰äº›å‡½æ•°æ˜¯å†…ç½®çš„, å¦‚`println`. ä»¥ä¸‹æ˜¯å®Œæ•´çš„åˆ—è¡¨:

```v ignore
fn print(s string) // print anything on sdtout
fn println(s string) // print anything and a newline on sdtout

fn eprint(s string) // same as print(), but use stderr
fn eprintln(s string) // same as println(), but use stderr

fn exit(code int) // terminate the program with a custom error code
fn panic(s string) // print a message and backtraces on stderr, and terminate the program with error code 1
fn print_backtrace() // print backtraces on stderr
```

`println`æ˜¯ä¸€ä¸ªç®€å•è€Œå¼ºå¤§çš„å†…ç½®å‡½æ•°, å®ƒå¯ä»¥æ‰“å°ä»»ä½•ä¸œè¥¿: string, number, array, map, struct.

```v
struct User {
	name string
	age  int
}

println(1) // "1"
println('hi') // "hi"
println([1, 2, 3]) // "[1, 2, 3]"
println(User{ name: 'Bob', age: 20 }) // "User{name:'Bob', age:20}"
```

<a id='custom-print-of-types' />

## æ‰“å°è‡ªå®šä¹‰ç±»å‹

å¦‚æœä½ æƒ³ä¸ºä½ çš„ç±»å‹å®šä¹‰ä¸€ä¸ªè‡ªå®šä¹‰æ‰“å°å€¼, åªéœ€å®šä¹‰ä¸€ä¸ªç®€å•çš„`.str() string`æ–¹æ³•å³å¯:

```v
struct Color {
	r int
	g int
	b int
}

pub fn (c Color) str() string {
	return '{$c.r, $c.g, $c.b}'
}

red := Color{
	r: 255
	g: 0
	b: 0
}
println(red)
```

## æ¨¡å—

æ–‡ä»¶å¤¹ç›®å½•ä¸‹çš„æ¯ä¸ªæ–‡ä»¶éƒ½æ˜¯åŒä¸€ä¸ªæ¨¡å—çš„ä¸€éƒ¨åˆ†. ç®€å•çš„ç¨‹åºä¸éœ€è¦æŒ‡å®šæ¨¡å—å, åœ¨è¿™ç§æƒ…å†µä¸‹, å®ƒé»˜è®¤ä¸º'main'.

Væ˜¯ä¸€ç§éå¸¸æ¨¡å—åŒ–çš„è¯­è¨€. æˆ‘ä»¬é¼“åŠ±åˆ›å»ºå¯é‡å¤ä½¿ç”¨çš„æ¨¡å—, å¹¶ä¸”å¾ˆå®¹æ˜“åšåˆ°. è¦åˆ›å»ºä¸€ä¸ªæ–°çš„æ¨¡å—, å¯ç”¨æ¨¡å—åç§°åˆ›å»ºä¸€ä¸ªç›®å½•, å…¶ä¸­åŒ…å«äº†
å¸¦ä»£ç çš„.væ–‡ä»¶å³å¯:

```shell
cd ~/code/modules
mkdir mymodule
vim mymodule/myfile.v
```
```v failcompile
// myfile.v
module mymodule

// To export a function we have to use `pub`
pub fn say_hi() {
    println('hello from mymodule!')
}
```

ç°åœ¨ä½ å¯ç”¨åœ¨ä»£ç ä¸­ä½¿ç”¨`mymodule`äº†:

```v failcompile
import mymodule

fn main() {
    mymodule.say_hi()
}
```

* æ¨¡å—åç§°åº”ç®€çŸ­, 10ä¸ªå­—ç¬¦ä»¥ä¸‹ã€‚
* æ¨¡å—åç§°å¿…é¡»ä½¿ç”¨`snake_case`
* ä¸å…è®¸å¾ªç¯å¯¼å…¥
* ä¸€ä¸ªæ¨¡å—ä¸­å¯ä»¥æœ‰ä»»æ„å¤šçš„.væ–‡ä»¶
* ä½ å¯ä»¥åœ¨ä»»ä½•åœ°æ–¹åˆ›å»ºæ¨¡å—
* æ‰€æœ‰çš„æ¨¡å—éƒ½å¯è¢«é™æ€åœ°ç¼–è¯‘æˆä¸€ä¸ªå¯æ‰§è¡Œæ–‡ä»¶

### `init` å‡½æ•°

å¦‚æœä½ æƒ³è®©ä¸€ä¸ªæ¨¡å—åœ¨å¯¼å…¥æ—¶è‡ªåŠ¨è°ƒç”¨ä¸€äº›è®¾ç½®/åˆå§‹åŒ–ä»£ç , é‚£ä¹ˆä½ å¯ä»¥ä½¿ç”¨æ¨¡å—çš„`init`å‡½æ•°.

```v
fn init() {
	// your setup code here ...
}
```

`init`å‡½æ•°ä¸èƒ½æ˜¯å…¬å¼€çš„ - å®ƒå°†è¢«è‡ªåŠ¨è°ƒç”¨. è¿™ä¸€ç‰¹ç‚¹å¯¹åˆå§‹åŒ–Cåº“ç‰¹åˆ«æœ‰ç”¨.

## Types 2

### Interfaces

```v
struct Dog {
	breed string
}

struct Cat {
	breed string
}

fn (d Dog) speak() string {
	return 'woof'
}

fn (c Cat) speak() string {
	return 'meow'
}

// unlike Go and like TypeScript, V's interfaces can define fields, not just methods.
interface Speaker {
	breed string
	speak() string
}

dog := Dog{'Leonberger'}
cat := Cat{'Siamese'}

mut arr := []Speaker{}
arr << dog
arr << cat
for item in arr {
	println('a $item.breed says: $item.speak()')
}
```

ä¸€ä¸ªç±»å‹é€šè¿‡å®ç°å…¶æ–¹æ³•å’Œå­—æ®µæ¥å®ç°ä¸€ä¸ªæ¥å£. ä¸éœ€è¦æ˜ç¡®çš„æ„å›¾å£°æ˜, æ²¡æœ‰"implements"å…³é”®å­—.

#### æ¥å£æ–­è¨€

æˆ‘ä»¬å¯ä»¥ä½¿ç”¨`is`æ“ä½œç¬¦æ¥æµ‹è¯•ä¸€ä¸ªæ¥å£çš„åº•å±‚ç±»å‹:
```v oksyntax
interface Something {}

fn announce(s Something) {
	if s is Dog {
		println('a $s.breed dog') // `s` is automatically cast to `Dog` (smart cast)
	} else if s is Cat {
		println('a $s.breed cat')
	} else {
		println('something else')
	}
}
```
æ›´å¤šä¿¡æ¯å‚è€ƒ [åŠ¨æ€æ–­è¨€](#åŠ¨æ€æ–­è¨€).

#### æ¥å£æ–¹æ³•çš„å®šä¹‰

åŒæ ·ä¸Goä¸åŒ, ä¸€ä¸ªæ¥å£å¯ä»¥å®ç°ä¸€ä¸ªæ–¹æ³•. è¿™äº›æ–¹æ³•ä¸æ˜¯ç”±å®ç°äº†è¯¥æ¥å£çš„structå®ç°çš„.

å½“ä¸€ä¸ªstructè¢«å°è£…åœ¨ä¸€ä¸ªå·²ç»å®ç°äº†è¯¥æ–¹æ³•çš„æ¥å£ä¸­æ—¶. ä¸è¿™ä¸ªstructæ‰€å®ç°çš„åç§°ç›¸åŒï¼Œæ­¤æ—¶åªæœ‰æ¥å£ä¸Šçš„è¯¥æ–¹æ³•ä¼šè¢«è°ƒç”¨.

```v
struct Cat {}

fn (c Cat) speak() string {
	return 'meow!'
}

interface Adoptable {}

fn (a Adoptable) speak() string {
	return 'adopt me!'
}

fn new_adoptable() Adoptable {
	return Cat{}
}

fn main() {
	cat := Cat{}
	assert cat.speak() == 'meow!'
	a := new_adoptable()
	assert a.speak() == 'adopt me!'
	if a is Cat {
		println(a.speak()) // meow!
	}
}
```

### Enum

```v
enum Color {
	red
	green
	blue
}

mut color := Color.red
// V knows that `color` is a `Color`. No need to use `color = Color.green` here.
color = .green
println(color) // "green"
match color {
	.red { println('the color was red') }
	.green { println('the color was green') }
	.blue { println('the color was blue') }
}
```

æšä¸¾åŒ¹é…å¿…é¡»æ˜¯è¯¦å°½çš„, æˆ–è€…æœ‰ä¸€ä¸ª`else`åˆ†æ”¯. è¿™ç¡®ä¿äº†å¦‚æœå¢åŠ äº†ä¸€ä¸ªæ–°çš„æšä¸¾å­—æ®µ, å®ƒåœ¨ä»£ç ä¸­çš„æ‰€æœ‰åœ°æ–¹éƒ½ä¼šè¢«å¤„ç†.

æšä¸¾å­—æ®µä¸èƒ½ä½¿ç”¨ä¿ç•™å…³é”®å­—. ç„¶è€Œ, ä¿ç•™çš„å…³é”®å­—å¯ä»¥ç”¨`@`è½¬ä¹‰è€Œæ˜¯è¢«ä½¿ç”¨.

```v
enum Color {
	@none
	red
	green
	blue
}

color := Color.@none
println(color)
```

Integers may be assigned to enum fields.

```v
enum Grocery {
	apple
	orange = 5
	pear
}

g1 := int(Grocery.apple)
g2 := int(Grocery.orange)
g3 := int(Grocery.pear)
println('Grocery IDs: $g1, $g2, $g3')
```

Output: `Grocery IDs: 0, 5, 6`.

ä¸å…è®¸åœ¨æšä¸¾å˜é‡ä¸Šè¿›è¡Œæ“ä½œ, å®ƒä»¬å¿…é¡»è¢«æ˜ç¡®åœ°è½¬æ¢ä¸º`int`.

### å’Œç±»å‹

ä¸€ä¸ªå’Œç±»å‹çš„å®ä¾‹å¯ä»¥å®¹çº³å‡ ä¸ªä¸åŒç±»å‹çš„å€¼. ä½¿ç”¨`type`å…³é”®å­—æ¥å£°æ˜ä¸€ä¸ªå’Œç±»å‹:

```v
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

sum := World(Moon{})
assert sum.type_name() == 'Moon'
println(sum)
```

å†…ç½®æ–¹æ³•`type_name`è¿”å›å½“å‰æŒæœ‰çš„ç±»å‹.

ä½¿ç”¨å’Œç±»å‹, ä½ å¯ä»¥å»ºç«‹é€’å½’ç»“æ„, å¹¶åœ¨å…¶ä¸Šå†™å‡ºç®€æ´ä½†å¼ºå¤§çš„ä»£ç .
```v
// V's binary tree
struct Empty {}

struct Node {
	value f64
	left  Tree
	right Tree
}

type Tree = Empty | Node

// sum up all node values
fn sum(tree Tree) f64 {
	return match tree {
		Empty { f64(0) } // TODO: as match gets smarter just remove f64()
		Node { tree.value + sum(tree.left) + sum(tree.right) }
	}
}

fn main() {
	left := Node{0.2, Empty{}, Empty{}}
	right := Node{0.3, Empty{}, Node{0.4, Empty{}, Empty{}}}
	tree := Node{0.5, left, right}
	println(sum(tree)) // 0.2 + 0.3 + 0.4 + 0.5 = 1.4
}
```

#### åŠ¨æ€æ–­è¨€

è¦æ£€æŸ¥ä¸€ä¸ªå’Œç±»å‹å®ä¾‹æ˜¯å¦æ‹¥æœ‰æŸä¸ªç±»å‹ï¼Œä½¿ç”¨`sum is Type`.
è¦å°†ä¸€ä¸ªå’Œç±»å‹è½¬æ¢ä¸ºå®ƒçš„ä¸€ä¸ªå˜ä½“, ä½¿ç”¨`sum as Type`.

```v
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

fn (m Mars) dust_storm() bool {
	return true
}

fn main() {
	mut w := World(Moon{})
	assert w is Moon
	w = Mars{}
	// use `as` to access the Mars instance
	mars := w as Mars
	if mars.dust_storm() {
		println('bad weather!')
	}
}
```

å¦‚æœ`w`æ²¡æœ‰æŒæœ‰`Mars`å®ä¾‹, `as`ä¼španic. æ›´å®‰å…¨çš„æ–¹æ³•æ˜¯ä½¿ç”¨æ™ºèƒ½è½¬æ¢.

#### æ™ºèƒ½è½¬æ¢

```v oksyntax
if w is Mars {
	assert typeof(w).name == 'Mars'
	if w.dust_storm() {
		println('bad weather!')
	}
}
```
if`è¯­å¥é‡Œçš„`w`å†…æœ‰`Mars`ç±»å‹. è¿™å°±æ˜¯æ‰€è°“çš„ *æµæ•æ„Ÿç±»å‹*.
å› ä¸º`w`æ˜¯ä¸€ä¸ªå¯å˜çš„æ ‡è¯†ç¬¦, å¦‚æœç¼–è¯‘å™¨æ™ºèƒ½è½¬æ¢å®ƒè€Œä¸å‘å‡ºè­¦å‘Š, é‚£å°†æ˜¯ä¸å®‰å…¨çš„.
è¿™å°±æ˜¯ä¸ºä»€ä¹ˆä½ å¿…é¡»åœ¨`is`è¡¨è¾¾å¼ä¹‹å‰å£°æ˜ä¸€ä¸ª`mut`.

```v ignore
if mut w is Mars {
	assert typeof(w).name == 'Mars'
	if w.dust_storm() {
		println('bad weather!')
	}
}
```
å¦åˆ™`w`å°†ä¿æŒå…¶åŸºç¡€ç±»å‹.
> è¿™æ—¢é€‚ç”¨äºç®€å•çš„å˜é‡, ä¹Ÿé€‚ç”¨äºå¤æ‚çš„è¡¨è¾¾å¼, å¦‚`user.name`.

#### åŒ¹é…å’Œç±»å‹

ä½ ä¹Ÿå¯ä»¥ä½¿ç”¨ `match` æ¥ç¡®å®šå˜ç§:

```v
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

fn open_parachutes(n int) {
	println(n)
}

fn land(w World) {
	match w {
		Moon {} // no atmosphere
		Mars {
			// light atmosphere
			open_parachutes(3)
		}
		Venus {
			// heavy atmosphere
			open_parachutes(1)
		}
	}
}
```

`match`å¿…é¡»ä¸ºæ¯ä¸€ä¸ªå˜ä½“æä¾›ä¸€ä¸ªæ¨¡å¼æˆ–æœ‰ä¸€ä¸ª`else`åˆ†æ”¯.

```v ignore
struct Moon {}
struct Mars {}
struct Venus {}

type World = Moon | Mars | Venus

fn (m Moon) moon_walk() {}
fn (m Mars) shiver() {}
fn (v Venus) sweat() {}

fn pass_time(w World) {
    match w {
        // using the shadowed match variable, in this case `w` (smart cast)
        Moon { w.moon_walk() }
        Mars { w.shiver() }
        else {}
    }
}
```

### Option/Result ç±»å‹ å’Œ errorå¤„ç†

Optionç±»å‹ç”¨`?Type`å®šä¹‰:
```v
struct User {
	id   int
	name string
}

struct Repo {
	users []User
}

fn (r Repo) find_user_by_id(id int) ?User {
	for user in r.users {
		if user.id == id {
			// V automatically wraps this into an option type
			return user
		}
	}
	return error('User $id not found')
}

fn main() {
	repo := Repo{
		users: [User{1, 'Andrew'}, User{2, 'Bob'}, User{10, 'Charles'}]
	}
	user := repo.find_user_by_id(10) or { // Option types must be handled by `or` blocks
		return
	}
	println(user.id) // "10"
	println(user.name) // "Charles"
}
```

Vå°†`Option`å’Œ`Result`åˆå¹¶ä¸ºä¸€ç§ç±»å‹, å› æ­¤ä½ æ— éœ€å†³å®šè¦ä½¿ç”¨å“ªç§ç±»å‹.

å°†ä¸€ä¸ªå‡½æ•°"å‡çº§"ä¸ºå¯é€‰å‡½æ•°æ‰€éœ€çš„å·¥ä½œé‡æ˜¯å¾ˆå°çš„. ä½ å¿…é¡»åœ¨è¿”å›ç±»å‹ä¸­æ·»åŠ `ï¼Ÿ`, å¹¶åœ¨å‡ºç°é—®é¢˜æ—¶è¿”å›é”™è¯¯.

å¦‚æœä½ ä¸éœ€è¦è¿”å›é”™è¯¯æ¶ˆæ¯, åˆ™åªéœ€`return none`(è¿™ä¸`return error("")`ç­‰æ•ˆ).

è¿™æ˜¯Vä¸­é”™è¯¯å¤„ç†çš„ä¸»è¦æœºåˆ¶. å®ƒä»¬ä»ç„¶æ˜¯å€¼, ç±»ä¼¼äºGo, ä½†ä¼˜ç‚¹æ˜¯errorå¿…é¡»å¤„ç†ï¼Œè€Œä¸”å¤„ç†èµ·æ¥ä¹Ÿä¸å†—é•¿. ä¸å…¶ä»–è¯­è¨€ä¸åŒ, Vä¸ç”¨`throw/try/catch`å—æ¥å¤„ç†å¼‚å¸¸.

erræ˜¯åœ¨orä»£ç å—ä¸­å®šä¹‰çš„, å¹¶è®¾ç½®ä¸ºä¼ é€’å­—ç¬¦ä¸²æ¶ˆæ¯
ç»™`error()`å‡½æ•°. å¦‚æœè¿”å›äº†`none`åˆ™`err`ä¸ºç©º.

```v oksyntax
user := repo.find_user_by_id(7) or {
	println(err) // "User 7 not found"
	return
}
```

### å¤„ç†å¯é€‰

æœ‰å››ç§æ–¹æ³•å¯ä»¥å¤„ç†ä¸€ä¸ªå¯é€‰. ç¬¬ä¸€ç§æ–¹æ³•æ˜¯ä¼ æ’­é”™è¯¯:

```v
import net.http

fn f(url string) ?string {
	resp := http.get(url) ?
	return resp.text
}
```

`http.get`è¿”å›`?http.Response`. å› ä¸º`?`è·Ÿåœ¨è°ƒç”¨åé¢, æ‰€ä»¥å¯¼è‡´äº†errorå°†è¢«ä¼ æ’­ç»™`f`çš„è°ƒç”¨è€…. å½“ä½¿ç”¨`?`å, å‡½æ•°è°ƒç”¨äº§ç”Ÿä¸€ä¸ªå¯é€‰å‡½æ•°, åˆ™å¤–å±‚å‡½æ•°å¿…é¡»è¿”å›ä¹Ÿæ˜¯ä¸€ä¸ªå¯é€‰é¡¹. å¦‚æœåœ¨`main()`ä¸­ä½¿ç”¨äº†é”™è¯¯ä¼ æ’­, å®ƒå°†`panic`, å› ä¸ºæ­¤æ—¶é”™è¯¯ä¸èƒ½è¿›ä¸€æ­¥ä¼ æ’­.

`f`çš„bodyåŸºæœ¬ä¸Šæ˜¯ä»¥ä¸‹å†…å®¹çš„æµ“ç¼©ç‰ˆ:

```v ignore
    resp := http.get(url) or { return err }
    return resp.text
```

---
ç¬¬äºŒç§æ–¹æ³•æ˜¯æå‰è„±ç¦»æ‰§è¡Œ:

```v oksyntax
user := repo.find_user_by_id(7) or { return }
```

åœ¨è¿™é‡Œï¼Œä½ å¯ä»¥è°ƒç”¨`panic()`æˆ–`exit()`, è¿™æ ·æ•´ä¸ªç¨‹åºå°±ä¼šåœæ­¢æ‰§è¡Œ. æˆ–ä½¿ç”¨æ§åˆ¶æµè¯­å¥(`return`, `break`, `continue`ç­‰)è·³å‡ºå½“å‰çš„ä»£ç å—. æ³¨æ„`break`å’Œ`continue`åªèƒ½åœ¨`for`ä¸­ä½¿ç”¨.

Væ²¡æœ‰åŠæ³•å¼ºåˆ¶"unwrap"ä¸€ä¸ªoptional(åƒå…¶ä»–è¯­è¨€é‚£æ ·, ä¾‹å¦‚Rustçš„`unwrap()`æˆ–Swiftçš„`!`). è¦åšåˆ°è¿™ä¸€ç‚¹, å¯ä»¥ä½¿ç”¨`or { panic(err.msg) }`ä»£æ›¿.

---
ç¬¬ä¸‰ç§æ–¹æ³•æ˜¯åœ¨`or`ä»£ç å—çš„æœ«å°¾æä¾›ä¸€ä¸ªé»˜è®¤å€¼. å¦‚æœå‡ºç°é”™è¯¯, å°†ä»¥è¯¥å€¼ä»£æ›¿. æ‰€ä»¥å®ƒå¿…é¡»ä¸è¢«å¤„ç†çš„`Option`çš„å†…å®¹å…·æœ‰ç›¸åŒçš„ç±»å‹.

```v
fn do_something(s string) ?string {
	if s == 'foo' {
		return 'foo'
	}
	return error('invalid string') // Could be `return none` as well
}

a := do_something('foo') or { 'default' } // a will be 'foo'
b := do_something('bar') or { 'default' } // b will be 'default'
println(a)
println(b)
```

---
ç¬¬å››ç§æ–¹æ³•æ˜¯ä½¿ç”¨`if`æ‹†åŒ…:

```v
import net.http

if resp := http.get('https://google.com') {
	println(resp.text) // resp is a http.Response, not an optional
} else {
	println(err)
}
```
ä¸Šé¢`http.get`è¿”å›ä¸€ä¸ª`?http.Response`. `resp`åªåœ¨`if`åˆ†æ”¯ä½œç”¨åŸŸå†…, è€Œ`err'åªå±äº`else'åˆ†æ”¯çš„ä½œç”¨åŸŸ.

## æ³›å‹

```v wip

struct Repo<T> {
    db DB
}

struct User {
	id   int
	name string
}

struct Post {
	id   int
	user_id int
	title string
	body string
}

fn new_repo<T>(db DB) Repo<T> {
    return Repo<T>{db: db}
}

// This is a generic function. V will generate it for every type it's used with.
fn (r Repo<T>) find_by_id(id int) ?T {
    table_name := T.name // in this example getting the name of the type gives us the table name
    return r.db.query_one<T>('select * from $table_name where id = ?', id)
}

db := new_db()
users_repo := new_repo<User>(db) // returns Repo<User>
posts_repo := new_repo<Post>(db) // returns Repo<Post>
user := users_repo.find_by_id(1)? // find_by_id<User>
post := posts_repo.find_by_id(1)? // find_by_id<Post>
```

ç›®å‰é€šç”¨å‡½æ•°å®šä¹‰å¿…é¡»å£°æ˜å…¶ç±»å‹å‚æ•°, ä½†åœ¨æœªæ¥Vå¯ä»¥åœ¨è¿è¡Œæ—¶ä»å•å­—æ¯ç±»å‹åæ¨æ–­å‡ºé€šç”¨ç±»å‹å‚æ•°. è¿™å°±æ˜¯ä¸ºä»€ä¹ˆ`find_by_id`å¯ä»¥çœç•¥`<T>`, å› ä¸º
æ¥æ”¶å™¨å‚æ•°`r`ä½¿ç”¨é€šç”¨ç±»å‹`T`.

å¦ä¸€ä¸ªä¾‹å­:
```v
fn compare<T>(a T, b T) int {
	if a < b {
		return -1
	}
	if a > b {
		return 1
	}
	return 0
}

// compare<int>
println(compare(1, 0)) // Outputs: 1
println(compare(1, 1)) //          0
println(compare(1, 2)) //         -1
// compare<string>
println(compare('1', '0')) // Outputs: 1
println(compare('1', '1')) //          0
println(compare('1', '2')) //         -1
// compare<f64>
println(compare(1.1, 1.0)) // Outputs: 1
println(compare(1.1, 1.1)) //          0
println(compare(1.1, 1.2)) //         -1
```


## å¹¶å‘
### ç”Ÿæˆå¹¶å‘ä»»åŠ¡
Vçš„å¹¶å‘æ¨¡å‹å’ŒGoçš„æ¨¡å‹éå¸¸ç›¸ä¼¼. åœ¨Vä¸­è¦åœ¨å…¶ä»–çº¿ç¨‹å¹¶å‘æ‰§è¡Œ`foo()`, ç”¨`go foo()`å³å¯:

```v
import math

fn p(a f64, b f64) { // ordinary function without return value
	c := math.sqrt(a * a + b * b)
	println(c)
}

fn main() {
	go p(3, 4)
	// p will be run in parallel thread
}
```

æœ‰æ—¶éœ€è¦ç­‰å¾…ä¸€ä¸ªå¹¶è¡Œçº¿ç¨‹å®Œæˆ. è¿™å¯ä»¥é€šè¿‡ç»™å¯åŠ¨çš„çº¿ç¨‹åˆ†é…ä¸€ä¸ª*handle*, å¹¶è°ƒç”¨å®ƒçš„`wait()`æ–¹æ³•æ¥å®ç°:

```v
import math

fn p(a f64, b f64) { // ordinary function without return value
	c := math.sqrt(a * a + b * b)
	println(c) // prints `5`
}

fn main() {
	h := go p(3, 4)
	// p() runs in parallel thread
	h.wait()
	// p() has definitely finished
}
```

è¿™ç§æ–¹æ³•ä¹Ÿå¯ä»¥ç”¨æ¥ä»ä¸€ä¸ªåœ¨å¹¶è¡Œçº¿ç¨‹ä¸­è¿è¡Œçš„å‡½æ•°ä¸­è·å–è¿”å›å€¼, è€Œå¹¶å‘è°ƒç”¨æ—¶ä¸éœ€è¦ä¿®æ”¹å‡½æ•°æœ¬èº«.

```v
import math { sqrt }

fn get_hypot(a f64, b f64) f64 { //       ordinary function returning a value
	c := sqrt(a * a + b * b)
	return c
}

fn main() {
	g := go get_hypot(54.06, 2.08) // spawn thread and get handle to it
	h1 := get_hypot(2.32, 16.74) //   do some other calculation here
	h2 := g.wait() //                 get result from spawned thread
	println('Results: $h1, $h2') //   prints `Results: 16.9, 54.1`
}
```

å¦‚æœæœ‰å¤§é‡çš„ä»»åŠ¡, ä½¿ç”¨çº¿ç¨‹æ•°ç»„æ¥ç®¡ç†å®ƒä»¬å¯èƒ½ä¼šæ›´å®¹æ˜“.

```v
import time

fn task(id int, duration int) {
	println('task $id begin')
	time.sleep(duration * time.millisecond)
	println('task $id end')
}

fn main() {
	mut threads := []thread{}
	threads << go task(1, 500)
	threads << go task(2, 900)
	threads << go task(3, 100)
	threads.wait()
	println('done')
}

// Output:
// task 1 begin
// task 2 begin
// task 3 begin
// task 3 end
// task 1 end
// task 2 end
// done
```

å¦å¤–å¯¹äºè¿”å›ç›¸åŒç±»å‹çš„çº¿ç¨‹, åœ¨çº¿ç¨‹æ•°ç»„æ‰€åœ¨çº¿ç¨‹ä¸Šè°ƒç”¨`wait()`å°†è¿”å›æ‰€æœ‰è®¡ç®—å€¼.

```v
fn expensive_computing(i int) int {
	return i * i
}

fn main() {
	mut threads := []thread int{}
	for i in 1 .. 10 {
		threads << go expensive_computing(i)
	}
	// Join all tasks
	r := threads.wait()
	println('All jobs finished: $r')
}

// Output: All jobs finished: [1, 4, 9, 16, 25, 36, 49, 64, 81]
```

### Channels
é€šé“æ˜¯coroutineä¹‹é—´çš„é¦–é€‰é€šä¿¡æ–¹å¼. Vçš„é€šé“çš„å·¥ä½œåŸç†åŸºæœ¬ä¸Šå°±åƒGo. ä½ å¯ä»¥åœ¨ä¸€ç«¯å°†å¯¹è±¡æ¨å…¥ä¸€ä¸ªé€šé“, å¹¶ä»å¦ä¸€ç«¯å¼¹å‡ºå¯¹è±¡.
é€šé“å¯ä»¥æ˜¯ç¼“å†²çš„æˆ–æ— ç¼“å†²çš„, å¹¶ä¸”å¯ä»¥ç”¨`select`å¯¹å¤šä¸ªé€šé“è¿›è¡Œé€‰æ‹©.

#### è¯­æ³•å’Œç”¨æ³•
é€šé“çš„ç±»å‹ä¸º`chan objtype`. ä¸€ä¸ªå¯é€‰çš„ç¼“å†²åŒºé•¿åº¦å¯ä»¥åœ¨å£°æ˜ä¸­æŒ‡å®šä¸º`cap`å±æ€§:

```v
ch := chan int{} // unbuffered - "synchronous"
ch2 := chan f64{cap: 100} // buffer length 100
```

channelä¸å¿…å£°æ˜ä¸º`mut`. ç¼“å†²åŒºé•¿åº¦ä¸æ˜¯ç±»å‹çš„ä¸€éƒ¨åˆ†, è€Œæ˜¯å•ä¸ªchannelå¯¹è±¡çš„ä¸€ä¸ªå±æ€§. channelå¯ä»¥åƒæ™®é€šçš„ä¸€æ ·ä¼ é€’ç»™coroutineå˜é‡:

```v
fn f(ch chan int) {
	// ...
}

fn main() {
	ch := chan int{}
	go f(ch)
	// ...
}
```

å¯ä»¥ä½¿ç”¨ç®­å¤´æ“ä½œç¬¦å°†å¯¹è±¡æ¨é€åˆ°é€šé“. åŒæ ·çš„æ“ä½œç¬¦å¯ä»¥ç”¨æ¥ä»å¦ä¸€ç«¯å¼¹å‡ºå¯¹è±¡:

```v
ch := chan int{}
ch2 := chan f64{}
n := 5
x := 7.3
ch <- n
// push
ch2 <- x
mut y := f64(0.0)
m := <-ch // pop creating new variable
y = <-ch2 // pop into existing variable
```

å…³é—­channelè¡¨ç¤ºä¸èƒ½å†æ¨å…¥å…¶ä»–å¯¹è±¡. ä»»ä½•è¿™æ ·çš„å°è¯•éƒ½ä¼šå¯¼è‡´è¿è¡Œæ—¶çš„panic(é™¤äº†`select`å’Œ`try_push()`--è§ä¸‹æ–‡). å¦‚æœç›¸å…³çš„channelå·²ç»å…³é—­å¹¶ä¸”ç¼“å†²åŒºæ˜¯ç©ºçš„, é‚£ä¹ˆå¼¹å‡ºçš„å°è¯•å°†ç«‹å³è¿”å›. è¿™ç§æƒ…å†µå¯ä»¥ä½¿ç”¨oråˆ†æ”¯æ¥å¤„ç†(å‚è§[å¤„ç†é€‰é¡¹](#å¤„ç†é€‰é¡¹)).

```v wip
ch := chan int{}
ch2 := chan f64{}
// ...
ch.close()
// ...
m := <-ch or {
    println('channel has been closed')
}

// propagate error
y := <-ch2 ?
```

#### Channelé€‰æ‹©

`select`å‘½ä»¤å…è®¸åœ¨æ²¡æœ‰æ˜æ˜¾çš„CPUè´Ÿè½½çš„æƒ…å†µä¸‹åŒæ—¶ç›‘æ§å‡ ä¸ªé€šé“. å®ƒç”±ä¸€ä¸ªå¯èƒ½çš„ä¼ è¾“åˆ—è¡¨å’Œç›¸å…³çš„è¯­å¥åˆ†æ”¯ç»„æˆ--ç±»ä¼¼äº[match](#match)å‘½ä»¤:
```v wip
import time
fn main () {
  c := chan f64{}
  ch := chan f64{}
  ch2 := chan f64{}
  ch3 := chan f64{}
  mut b := 0.0
  // ...
  select {
    a := <-ch {
        // do something with `a`
    }
    b = <-ch2 {
        // do something with predeclared variable `b`
    }
    ch3 <- c {
        // do something if `c` was sent
    }
    > 500 * time.millisecond {
        // do something if no channel has become ready within 0.5s
    }
  }
}
```
è¶…æ—¶åˆ†æ”¯æ˜¯å¯é€‰çš„. å¦‚æœæ²¡æœ‰è¶…æ—¶åˆ†æ”¯, åˆ™`select`ç­‰å¾…çš„æ—¶é—´ä¸å—é™åˆ¶. å¦‚æœåœ¨è°ƒç”¨`select`æ—¶æ²¡æœ‰é€šé“å‡†å¤‡å¥½, ä¹Ÿå¯ä»¥ç«‹å³è¿›è¡Œ, å¢åŠ ä¸€ä¸ª`else { .... }`åˆ†æ”¯å³å¯. `else`å’Œ`> timeout`æ˜¯æ’æ–¥çš„.

`select`å‘½ä»¤å¯ä»¥ä½œä¸º`bool`ç±»å‹çš„*è¡¨è¾¾å¼*ä½¿ç”¨, å¦‚æœæ‰€æœ‰é€šé“éƒ½å…³é—­, åˆ™ä¼šå˜ä¸ºfalse:
```v wip
if select {
    ch <- a {
        // ...
    }
} {
    // channel was open
} else {
    // channel is closed
}
```

#### ç‰¹æ®Šçš„ChannelåŠŸèƒ½

å¯¹äºç‰¹æ®Šç”¨é€”, æœ‰ä¸€äº›å†…ç½®çš„å±æ€§å’Œæ–¹æ³•:
```v
struct Abc {
	x int
}

a := 2.13
ch := chan f64{}
res := ch.try_push(a) // try to perform `ch <- a`
println(res)
l := ch.len // number of elements in queue
c := ch.cap // maximum queue length
is_closed := ch.closed // bool flag - has `ch` been closed
println(l)
println(c)
mut b := Abc{}
ch2 := chan Abc{}
res2 := ch2.try_pop(b) // try to perform `b = <-ch2`
```

`try_push/pop()`æ–¹æ³•å°†ç«‹å³è¿”å›å…¶ä¸­ä¸€ä¸ªç»“æœ: `.success`, `.not_ready`æˆ–`.closed` - å–å†³äºå¯¹è±¡æ˜¯å¦å·²è¢«è½¬ç§», æˆ–ä¸ºä»€ä¹ˆä¸è¿™æ ·åš.

ä¸å»ºè®®åœ¨ç”Ÿäº§ä¸­ä½¿ç”¨è¿™äº›æ–¹æ³•å’Œå±æ€§ - åŸºäºå®ƒä»¬çš„ç®—æ³•å¾€å¾€å—åˆ¶äºç«èµ›æ¡ä»¶, ç‰¹åˆ«æ˜¯`.len`å’Œ `.closed`ä¸åº”è¢«ç”¨æ¥åšå†³å®š.

ä½¿ç”¨`or`åˆ†æ”¯, é”™è¯¯ä¼ æ’­æˆ–`select`ä»£æ›¿(è§[è¯­æ³•å’Œç”¨æ³•](#è¯­æ³•å’Œç”¨æ³•) å’Œ ä¸Šé¢çš„[é€šé“é€‰æ‹©](#é€šé“é€‰æ‹©)).

### å…±äº«å¯¹è±¡

æ•°æ®å¯ä»¥é€šè¿‡å…±äº«å˜é‡åœ¨coroutineå’Œè°ƒç”¨çº¿ç¨‹ä¹‹é—´å…±äº«.

è¿™æ ·çš„å˜é‡åº”è¯¥åˆ›å»ºä¸º`shared`, å¹¶ä¸”ä¹Ÿä»¥è¿™æ ·çš„æ–¹å¼ä¼ é€’ç»™coroutine.

åº•å±‚çš„`struct`åŒ…å«ä¸€ä¸ªéšè—çš„*mutex*, å…è®¸é”å®šå¹¶å‘è®¿é—®: ä½¿ç”¨`rlock`ä»£è¡¨åªè¯», ä½¿ç”¨`lock`ä»£è¡¨è¯»/å†™è®¿é—®.

```v
struct St {
mut:
	x int // data to shared
}

fn (shared b St) g() {
	lock b {
		// read/modify/write b.x
	}
}

fn main() {
	shared a := St{
		x: 10
	}
	go a.g()
	// ...
	rlock a {
		// read a.x
	}
}
```
å…±äº«å˜é‡å¿…é¡»æ˜¯struct, array æˆ– map.

## è§£æJSON

```v
import json

struct Foo {
	x int
}

struct User {
	name string
	age  int
	// Use the `skip` attribute to skip certain fields
	foo Foo [skip]
	// If the field name is different in JSON, it can be specified
	last_name string [json: lastName]
}

data := '{ "name": "Frodo", "lastName": "Baggins", "age": 25 }'
user := json.decode(User, data) or {
	eprintln('Failed to decode json')
	return
}
println(user.name)
println(user.last_name)
println(user.age)
// You can also decode JSON arrays:
sfoos := '[{"x":123},{"x":456}]'
foos := json.decode([]Foo, sfoos) ?
println(foos[0].x)
println(foos[1].x)
```

ç”±äºJSONçš„æ™®éæ€§ï¼ŒVä¸­ç›´æ¥å†…ç½®äº†å¯¹å®ƒçš„æ”¯æŒ.

`json.decode`å‡½æ•°æœ‰ä¸¤ä¸ªå‚æ•°: ç¬¬ä¸€ä¸ªæ˜¯JSONå€¼åº”è¯¥è¢«è§£ç æˆçš„ç±»å‹, ç¬¬äºŒä¸ªæ˜¯åŒ…å«JSONæ•°æ®çš„å­—ç¬¦ä¸².

Vä¼šç”ŸæˆJSONç¼–ç å’Œè§£ç çš„ä»£ç , æ²¡æœ‰ä½¿ç”¨è¿è¡Œæ—¶åå°„, è¿™å¯ä»¥æœ‰æ›´å¥½çš„æ€§èƒ½.

## æµ‹è¯•

### Assert

```v
fn foo(mut v []int) {
	v[0] = 1
}

mut v := [20]
foo(mut v)
assert v[0] < 4
```
`assert`è¯­å¥æ£€æŸ¥å…¶è¡¨è¾¾å¼æ˜¯å¦ä¸º`true`. å¦‚æœæ–­è¨€å¤±è´¥, ç¨‹åºå°†è¢«ä¸­æ­¢. æ–­è¨€åªèƒ½ç”¨äºæ£€æµ‹ç¼–ç¨‹é”™è¯¯. å½“ä¸€ä¸ªassertå¤±è´¥å, ä¼šè¢«æŠ¥å‘Šç»™*stderr*, è€Œä¸”æ¯”è¾ƒæ“ä½œç¬¦(å¦‚`<`ã€`==`)ä¸¤è¾¹çš„æ•°å€¼å°†å°½å¯èƒ½åœ°è¢«æ‰“å°å‡ºæ¥. è¿™æœ‰åˆ©äºè½»æ¾æ‰¾åˆ°ä¸€ä¸ªæ„å¤–å€¼. æ–­è¨€è¯­å¥å¯ä»¥åœ¨ä»»ä½•å‡½æ•°ä¸­ä½¿ç”¨.

### æµ‹è¯•æ–‡ä»¶

```v
// hello.v
module main

fn hello() string {
	return 'Hello world'
}

fn main() {
	println(hello())
}
```

```v failcompile
module main
// hello_test.v
fn test_hello() {
    assert hello() == 'Hello world'
}
```
ä½¿ç”¨`v hello_test.v`æ‰§è¡Œä¸Šè¿°æµ‹è¯•. è¿™å°†æ£€æŸ¥å‡½æ•°`hello`æ˜¯å¦æ˜¯äº§ç”Ÿæ­£ç¡®çš„è¾“å‡º. Vä¼šæ‰§è¡Œæ–‡ä»¶ä¸­çš„æ‰€æœ‰æµ‹è¯•å‡½æ•°.

* æ‰€æœ‰çš„æµ‹è¯•å‡½æ•°å¿…é¡»åœ¨æµ‹è¯•æ–‡ä»¶ä¸­, æ–‡ä»¶åå¿…é¡»ä»¥`_test.v`ç»“å°¾
* æµ‹è¯•å‡½æ•°çš„åå­—å¿…é¡»ä»¥`test_`å¼€å¤´ï¼Œä»¥æ ‡è®°å®ƒä»¬çš„æ‰§è¡Œ
* æ™®é€šå‡½æ•°ä¹Ÿå¯ä»¥åœ¨æµ‹è¯•æ–‡ä»¶ä¸­å®šä¹‰ï¼Œå¹¶åº”æ‰‹åŠ¨è°ƒç”¨. å…¶å®ƒç¬¦å·ä¹Ÿå¯ä»¥åœ¨æµ‹è¯•æ–‡ä»¶ä¸­å®šä¹‰, ä¾‹å¦‚ç±»å‹.
* æœ‰ä¸¤ç§æµ‹è¯•ï¼šå¤–éƒ¨æµ‹è¯•å’Œå†…éƒ¨æµ‹è¯•
* å†…éƒ¨æµ‹è¯•å¿…é¡»*å£°æ˜*å®ƒä»¬çš„æ¨¡å—ï¼Œå°±åƒå°±åƒæ¥è‡ªåŒä¸€æ¨¡å—çš„æ‰€æœ‰å…¶ä»–.væ–‡ä»¶ä¸€æ ·. å†…éƒ¨æµ‹è¯•ç”šè‡³å¯ä»¥è°ƒç”¨åŒä¸€æ¨¡å—ä¸­çš„ç§æœ‰å‡½æ•°.
* å¤–éƒ¨æµ‹è¯•å¿…é¡»*å¯¼å…¥*éœ€è¦æµ‹è¯•çš„æ¨¡å—. å®ƒä»¬ä¸èƒ½è®¿é—®æ¨¡å—çš„ç§æœ‰å‡½æ•°/ç±»å‹. å®ƒä»¬åªèƒ½æµ‹è¯•æ¨¡å—æä¾›çš„å¤–éƒ¨/å…¬å…± API.

åœ¨ä¸Šé¢çš„ä¾‹å­ä¸­, `test_hello`æ˜¯ä¸€ä¸ªå†…éƒ¨æµ‹è¯•, å®ƒè°ƒç”¨ç§æœ‰å‡½æ•°`hello()`, å› ä¸º`hello_test.v`æœ‰`module main`.
å°±åƒ`hello.v`ä¸€æ ·ï¼Œå³ä¸¤è€…éƒ½æ˜¯åŒä¸€ä¸ªæ¨¡å—çš„ä¸€éƒ¨åˆ†, è¿˜è¯·æ³¨æ„å› ä¸º`module main`å’Œå…¶ä»–æ¨¡å—ä¸€æ ·æ˜¯ä¸€ä¸ªå¸¸è§„æ¨¡å—, æ‰€ä»¥å†…éƒ¨æµ‹è¯•å¯ä»¥ç”¨æ¥æµ‹è¯•ä¸»ç¨‹åº.væ–‡ä»¶ä¸­çš„ç§æœ‰å‡½æ•°.

ä½ ä¹Ÿå¯ä»¥åœ¨æµ‹è¯•æ–‡ä»¶ä¸­å®šä¹‰ç‰¹æ®Šçš„æµ‹è¯•å‡½æ•°:
* `testsuite_begin`å°†åœ¨æ‰€æœ‰å…¶ä»–æµ‹è¯•å‡½æ•°ä¹‹å‰è¿è¡Œ
* `testsuite_end`å°†åœ¨æ‰€æœ‰å…¶ä»–æµ‹è¯•å‡½æ•°ä¹‹åè¿è¡Œ

#### æ‰§è¡Œæµ‹è¯•

è¦åœ¨å•ä¸ªæµ‹è¯•æ–‡ä»¶ä¸­è¿è¡Œæµ‹è¯•å‡½æ•°, ä½¿ç”¨`v foo_test.v`.

è¦æµ‹è¯•æ•´ä¸ªæ¨¡å—, ä½¿ç”¨`v test mymodule`. ä½ ä¹Ÿå¯ä»¥ä½¿ç”¨`v test .`æ¥æµ‹è¯•ä½ å½“å‰æ–‡ä»¶å¤¹(å’Œå­æ–‡ä»¶å¤¹)å†…çš„æ‰€æœ‰å†…å®¹. ä½ å¯ä»¥é€šè¿‡`-stats`é€‰é¡¹æ¥æŸ¥çœ‹å…³äºå•ä¸ªæµ‹è¯•è¿è¡Œçš„æ›´å¤šç»†èŠ‚.

## Memory management

Vé€šè¿‡ä½¿ç”¨å€¼ç±»å‹å’Œå­—ç¬¦ä¸²ç¼“å†²åŒº, é¦–å…ˆé¿å…äº†åšä¸å¿…è¦çš„åˆ†é…, ä¿ƒè¿›äº†ç®€å•çš„æ— æŠ½è±¡ä»£ç é£æ ¼.

å¤§å¤šæ•°å¯¹è±¡(çº¦90-100%)éƒ½è¢«Vçš„è‡ªåŠ¨é‡Šæ”¾å¼•æ“é‡Šæ”¾äº†: ç¼–è¯‘å™¨åœ¨ç¼–è¯‘è¿‡ç¨‹ä¸­æ’å…¥äº†è‡ªåŠ¨è¿›è¡Œå¿…è¦çš„freeè°ƒç”¨, å‰©ä½™çš„å°éƒ¨åˆ†
çš„å¯¹è±¡æ˜¯é€šè¿‡å¼•ç”¨è®¡æ•°é‡Šæ”¾çš„.

å¼€å‘è€…ä¸éœ€è¦åœ¨ä»–ä»¬çš„ä»£ç ä¸­æ”¹å˜ä»»ä½•ä¸œè¥¿. "å®ƒåªæ˜¯å·¥ä½œ", å°±åƒåœ¨
Python, Goæˆ–Java, é™¤äº†æ²¡æœ‰ç¹é‡çš„GCè·Ÿè¸ªæ‰€æœ‰å¯¹è±¡, ä¹Ÿæ²¡æœ‰ä¸ºæ¯ä¸ªå¯¹è±¡æä¾›æ˜‚è´µçš„RC.

### æ§åˆ¶

ä½ å¯ä»¥åˆ©ç”¨Vçš„è‡ªåŠ¨é‡Šæ”¾å¼•æ“, åœ¨è‡ªå®šä¹‰æ•°æ®ç±»å‹ä¸Šå®šä¹‰ä¸€ä¸ª`free()`æ–¹æ³•å³å¯:

```v
struct MyType {}

[unsafe]
fn (data &MyType) free() {
	// ...
}
```

å°±åƒç¼–è¯‘å™¨ä½¿ç”¨Cçš„`free()`é‡Šæ”¾Cæ•°æ®ç±»å‹ä¸€æ ·, å®ƒä¼šåœ¨æ¯ä¸ªå˜é‡çš„ç”Ÿå­˜æœŸæœ«ä¸ºæ•°æ®ç±»å‹é™æ€æ’å…¥`free()`è°ƒç”¨.

å¯¹äºæ„¿æ„è¿›è¡Œæ›´å¤šä½çº§æ§åˆ¶çš„å¼€å‘äººå‘˜, å¯ä»¥ä½¿ç”¨`-manualfree`ç¦ç”¨è‡ªåŠ¨é‡Šæ”¾, æˆ–åœ¨è¦æ‰‹åŠ¨ç®¡ç†å…¶å†…å­˜çš„æ¯ä¸ªå‡½æ•°ä¸Šæ·»åŠ ä¸€ä¸ª`[manualfree]`æ¥ç¦ç”¨è‡ªåŠ¨é‡Šæ”¾, è¯·å‚é˜…[å±æ€§](#å±æ€§).

_æ³¨æ„ï¼šç°åœ¨, è‡ªåŠ¨é‡Šæ”¾éšè—åœ¨`-autofree`æ ‡å¿—çš„åé¢. é»˜è®¤æƒ…å†µä¸‹, å®ƒå°†åœ¨V 0.3ä¸­å¯ç”¨. å¦‚æœä¸ä½¿ç”¨autofree, åˆ™Vç¨‹åºå°†ä¼šå‡ºç°å†…å­˜æ³„æ¼.

### ä¾‹å­

```v
import strings

fn draw_text(s string, x int, y int) {
	// ...
}

fn draw_scene() {
	// ...
	name1 := 'abc'
	name2 := 'def ghi'
	draw_text('hello $name1', 10, 10)
	draw_text('hello $name2', 100, 10)
	draw_text(strings.repeat(`X`, 10000), 10, 50)
	// ...
}
```

å­—ç¬¦ä¸²ä¸ä¼šè½¬ä¹‰`draw_text`, å› æ­¤å½“å‡½æ•°é€€å‡ºæ—¶å®ƒä»¬ä¼šè¢«æ¸…é™¤.

å®é™…ä¸Š, ä½¿ç”¨`-prealloc`æ ‡å¿—, å‰ä¸¤ä¸ªè°ƒç”¨æ ¹æœ¬ä¸ä¼šå¯¼è‡´ä»»ä½•åˆ†é…. è¿™ä¸¤ä¸ªå­—ç¬¦ä¸²å¾ˆå°, å› æ­¤Vå°†ä¸ºå®ƒä»¬ä½¿ç”¨é¢„åˆ†é…çš„ç¼“å†²åŒº.

```v
struct User {
	name string
}

fn test() []int {
	number := 7 // stack variable
	user := User{} // struct allocated on stack
	numbers := [1, 2, 3] // array allocated on heap, will be freed as the function exits
	println(number)
	println(user)
	println(numbers)
	numbers2 := [4, 5, 6] // array that's being returned, won't be freed here
	return numbers2
}
```

## ORM

(ç›®å‰ä»å¤„äºalphaçŠ¶æ€)

Vå†…ç½®äº†ORM(å¯¹è±¡å…³ç³»æ˜ å°„), æ”¯æŒSQLite, å¹¶å°†å¾ˆå¿«æ”¯æŒMySQL, Postgres, MS SQLå’ŒOracle.

Vçš„ORMæä¾›äº†è®¸å¤šå¥½å¤„:

- æ‰€æœ‰çš„SQLæ–¹è¨€éƒ½ç”¨ä¸€ç§è¯­æ³• (åœ¨æ•°æ®åº“ä¹‹é—´çš„è¿ç§»å˜å¾—æ›´åŠ å®¹æ˜“)
- ä½¿ç”¨Vçš„è¯­æ³•æ„å»ºæŸ¥è¯¢ (ä¸éœ€è¦å­¦ä¹ å¦ä¸€ç§è¯­æ³•)
- å®‰å…¨æ€§ (æ‰€æœ‰çš„æŸ¥è¯¢éƒ½ä¼šè‡ªåŠ¨å¤„ç†, ä»¥é˜²æ­¢SQLæ³¨å…¥)
- ç¼–è¯‘æ—¶çš„æ£€æŸ¥ (è¿™å¯ä»¥é˜²æ­¢åªæœ‰åœ¨è¿è¡Œæ—¶æ‰èƒ½å‘ç°çš„æ’ç‰ˆé”™è¯¯)
- å¯è¯»æ€§å’Œç®€å•æ€§ (ä½ ä¸éœ€è¦æ‰‹åŠ¨è§£ææŸ¥è¯¢çš„ç»“æœ, ä¹Ÿä¸éœ€è¦ä»è§£æç»“æœä¸­æ‰‹åŠ¨æ„é€ å¯¹è±¡)

```v
import sqlite

struct Customer {
	// struct name has to be the same as the table name (for now)
	id        int // a field named `id` of integer type must be the first field
	name      string
	nr_orders int
	country   string
}

db := sqlite.connect('customers.db') ?
// select count(*) from Customer
nr_customers := sql db {
	select count from Customer
}
println('number of all customers: $nr_customers')
// V syntax can be used to build queries
// db.select returns an array
uk_customers := sql db {
	select from Customer where country == 'uk' && nr_orders > 0
}
println(uk_customers.len)
for customer in uk_customers {
	println('$customer.id - $customer.name')
}
// by adding `limit 1` we tell V that there will be only one object
customer := sql db {
	select from Customer where id == 1 limit 1
}
println('$customer.id - $customer.name')
// insert a new customer
new_customer := Customer{
	name: 'Bob'
	nr_orders: 10
}
sql db {
	insert new_customer into Customer
}
```

æ›´å¤šä¾‹å­è§ <a href='https://github.com/vlang/v/blob/master/vlib/orm/orm_test.v'>vlib/orm/orm_test.v</a>.

## æ–‡æ¡£ç¼–å†™

å®ƒçš„å·¥ä½œæ–¹å¼ä¸Goéå¸¸ç›¸ä¼¼. å¾ˆç®€å•: ä¸éœ€è¦ä¸ºä½ çš„ä»£ç å•ç‹¬å†™æ–‡æ¡£, vdocä¼šæ ¹æ®æºä»£ç ä¸­çš„docstringsç”Ÿæˆæ–‡æ¡£.

æ¯ä¸ªå‡½æ•°/ç±»å‹/constçš„æ–‡æ¡£å¿…é¡»æ”¾åœ¨å£°æ˜ä¹‹å‰:

```v
// clearall clears all bits in the array
fn clearall() {
}
```

æ³¨é‡Šå¿…é¡»ä»¥å®šä¹‰çš„åç§°å¼€å§‹.

æœ‰æ—¶ä¸€è¡Œä¸è¶³ä»¥è§£é‡Šä¸€ä¸ªå‡½æ•°çš„ä½œç”¨, åœ¨è¿™ç§æƒ…å†µä¸‹, æ³¨é‡Šåº”è¯¥ä½¿ç”¨å•è¡Œæ³¨é‡Šè·¨è¶Šåˆ°æ–‡æ¡£ä¸­çš„å‡½æ•°å‰:

```v
// copy_all recursively copies all elements of the array by their value,
// if `dupes` is false all duplicate values are eliminated in the process.
fn copy_all(dupes bool) {
	// ...
}
```

æŒ‰ç…§æƒ¯ä¾‹, æœ€å¥½ç”¨*ç°åœ¨æ—¶*å†™è¯„è®º.

æ¨¡å—çš„æ¦‚è¿°å¿…é¡»æ”¾åœ¨æ¨¡å—åç§°ä¹‹åçš„ç¬¬ä¸€æ¡è¯„è®ºä¸­.

è¦ç”Ÿæˆæ–‡æ¡£ï¼Œè¯·ä½¿ç”¨vdoc, ä¾‹å¦‚`v doc net.http`.

## å·¥å…·

### v fmt

ä½ ä¸éœ€è¦æ‹…å¿ƒä½ çš„ä»£ç æ ¼å¼åŒ–æˆ–è®¾ç½®é£æ ¼å‡†åˆ™. `v fmt`ä¼šå¤„ç†è¿™äº›é—®é¢˜:

```shell
v fmt file.v
```

å»ºè®®è®¾ç½®ä½ çš„ç¼–è¾‘å™¨, åœ¨æ¯æ¬¡ä¿å­˜æ—¶æ‰§è¡Œ`v fmt -w`. vfmtæ‰§è¡Œæˆæœ¬é€šå¸¸å¾ˆä¾¿å®œ(éœ€è¦<30ms).

åœ¨æ¨é€ä»£ç ä¹‹å‰, ä¸€å®šè¦è¿è¡Œ`v fmt -w file.v`.

### å‰–æ

Vå¯¹ç¨‹åºå‰–ææœ‰å¾ˆå¥½çš„æ”¯æŒ: `v -profile profile.txt run file.v`. è¿™å°†äº§ç”Ÿä¸€ä¸ªprofile.txtæ–‡ä»¶, ä½ å¯ä»¥å¯¹å…¶è¿›è¡Œåˆ†æ.

ç”Ÿæˆçš„profile.txtæ–‡ä»¶ä¼šæœ‰4åˆ—:
a) ä¸€ä¸ªå‡½æ•°çš„è°ƒç”¨æ¬¡æ•°
b) ä¸€ä¸ªå‡½æ•°æ€»å…±éœ€è¦å¤šå°‘æ—¶é—´(æ¯«ç§’)
c) è°ƒç”¨ä¸€ä¸ªå‡½æ•°å¹³å‡éœ€è¦å¤šå°‘æ—¶é—´(çº³ç§’)
d) vå‡½æ•°çš„åç§°

ä½ å¯ä»¥ä½¿ç”¨ä»¥ä¸‹æ–¹æ³•å¯¹ç¬¬3åˆ—(æ¯ä¸ªå‡½æ•°çš„å¹³å‡æ—¶é—´)è¿›è¡Œæ’åº:
`sort -n -k3 profile.txt|tail`

ä½ ä¹Ÿå¯ä»¥ä½¿ç”¨ç§’è¡¨æ¥ç²¾ç¡®åœ°æµ‹é‡ä»£ç çš„ä¸€éƒ¨åˆ†:
```v
import time

fn main() {
	sw := time.new_stopwatch({})
	println('Hello world')
	println('Greeting the world took: ${sw.elapsed().nanoseconds()}ns')
}
```

# é«˜çº§åŠŸèƒ½

## éå†…å­˜å®‰å…¨çš„ä»£ç 
æœ‰æ—¶ä¸ºäº†æ•ˆç‡, ä½ å¯èƒ½ä¼šæƒ³å†™ä¸€äº›ä½çº§åˆ«çš„ä»£ç , å°½ç®¡è¿™äº›ä»£ç å¯èƒ½ä¼šç ´åå†…å­˜æˆ–å®¹æ˜“è¢«å®‰å…¨æ¼æ´åˆ©ç”¨. Væ”¯æŒç¼–å†™è¿™æ ·çš„ä»£ç , ä½†ä¸æ˜¯é»˜è®¤çš„.

Vè¦æ±‚æœ‰æ„æ ‡è®°ä»»ä½•æ½œåœ¨çš„ä¸å®‰å…¨çš„å†…å­˜æ“ä½œ. æ ‡è®°è¿™äº›æ“ä½œä¹Ÿå‘ä»»ä½•é˜…è¯»ä»£ç çš„äººè¡¨æ˜, è¿™é‡Œè¿åäº†å†…å­˜å®‰å…¨, å¯èƒ½å¯¼è‡´é”™è¯¯.

æ½œåœ¨çš„å†…å­˜ä¸å®‰å…¨æ“ä½œçš„ä¾‹å­æœ‰:
* æŒ‡é’ˆè¿ç®—
* æŒ‡é’ˆç´¢å¼•
* ä»ä¸å…¼å®¹ç±»å‹è½¬æ¢ä¸ºæŒ‡é’ˆ
* è°ƒç”¨æŸäº›Cå‡½æ•°ï¼Œå¦‚`free`, `strlen`å’Œ`strncmp`

è¦æ ‡è®°æ½œåœ¨çš„ä¸å®‰å…¨å†…å­˜æ“ä½œ, è¯·å°†å…¶æ”¾åœ¨`unsafe`å—ä¸­:

```v wip
// allocate 2 uninitialized bytes & return a reference to them
mut p := unsafe { malloc(2) }
p[0] = `h` // Error: pointer indexing is only allowed in `unsafe` blocks
unsafe {
    p[0] = `h` // OK
    p[1] = `i`
}
p++ // Error: pointer arithmetic is only allowed in `unsafe` blocks
unsafe {
    p++ // OK
}
assert *p == `i`
```

æœ€å¥½çš„åšæ³•æ˜¯é¿å…å°†å†…å­˜å®‰å…¨è¡¨è¾¾å¼æ”¾åœ¨`unsafe` å—ä¸­, ä»¥ä¾¿å°½å¯èƒ½æ˜ç¡®ä½¿ç”¨`unsafe`çš„åŸå› . ä¸€èˆ¬æ¥è¯´, ä»»ä½•ä»£ç ä½ è®¤ä¸ºæ˜¯å†…å­˜å®‰å…¨çš„å°±ä¸åº”è¯¥æ”¾åœ¨ä¸€ä¸ª`unsafe` å—ä¸­, å› ä¸ºç¼–è¯‘å™¨å¯ä»¥éªŒè¯å®ƒ.

å¦‚æœä½ æ€€ç–‘ä½ çš„ç¨‹åºè¿åäº†å†…å­˜å®‰å…¨è§„å®š, ä½ å°±æœ‰äº†ä¸€ä¸ªå¥½çš„å¼€ç«¯æ‰¾å‡ºåŸå› : æŸ¥çœ‹`unsafe`ä»£ç å—(ä»¥åŠå®ƒä»¬æ˜¯å¦‚ä½•ä¸å‘¨è¾¹ç›¸äº’ä½œç”¨çš„).

* æ³¨: è¿™æ˜¯ä¸€é¡¹æ­£åœ¨è¿›è¡Œçš„å·¥ä½œ.

### å¸¦å‚è€ƒå­—æ®µçš„struct

å¸¦æœ‰å¼•ç”¨çš„structéœ€è¦æ˜ç¡®åœ°å°†åˆå§‹å€¼è®¾ç½®ä¸ºä¸€ä¸ªå¼•ç”¨å€¼, é™¤éstructå·²ç»å®šä¹‰äº†è‡ªå·±çš„åˆå§‹å€¼.

é›¶å€¼å¼•ç”¨æˆ–è€…nilæŒ‡é’ˆ, æœªæ¥å°†**ä¸**æ”¯æŒ. ç›®å‰ä¾èµ–äºå¯ä»¥ä½¿ç”¨å€¼'0'çš„å¼•ç”¨å­—æ®µçš„æ•°æ®ç»“æ„Linked Listsæˆ–Binary Treeå°†è¢«ç†è§£ä¸ºä¸å®‰å…¨, å¹¶ä¸”ä¼šå¼•èµ·panic.

```v
struct Node {
	a &Node
	b &Node = 0 // Auto-initialized to nil, use with caution!
}

// Reference fields must be initialized unless an initial value is declared.
// Zero (0) is OK but use with caution, it's a nil pointer.
foo := Node{
	a: 0
}
bar := Node{
	a: &foo
}
baz := Node{
	a: 0
	b: 0
}
qux := Node{
	a: &foo
	b: &bar
}
println(baz)
println(qux)
```

## sizeof å’Œ __offsetof

* `sizeof(Type)` è¿”å›ä¸€ä¸ªç±»å‹å¤§å°.
* `__offsetof(Struct, field_name)` è¿”å›structå­—æ®µçš„åç§»é‡

```v
struct Foo {
	a int
	b int
}

assert sizeof(Foo) == 8
assert __offsetof(Foo, a) == 0
assert __offsetof(Foo, b) == 4
```

## åœ¨ V ä¸­è°ƒç”¨ C

### ä¾‹å­

```v
#flag -lsqlite3
#include "sqlite3.h"
// See also the example from https://www.sqlite.org/quickstart.html
struct C.sqlite3 {
}

struct C.sqlite3_stmt {
}

type FnSqlite3Callback = fn (voidptr, int, &charptr, &charptr) int

fn C.sqlite3_open(charptr, &&C.sqlite3) int

fn C.sqlite3_close(&C.sqlite3) int

fn C.sqlite3_column_int(stmt &C.sqlite3_stmt, n int) int

// ... you can also just define the type of parameter and leave out the C. prefix
fn C.sqlite3_prepare_v2(&C.sqlite3, charptr, int, &&C.sqlite3_stmt, &charptr) int

fn C.sqlite3_step(&C.sqlite3_stmt)

fn C.sqlite3_finalize(&C.sqlite3_stmt)

fn C.sqlite3_exec(db &C.sqlite3, sql charptr, cb FnSqlite3Callback, cb_arg voidptr, emsg &charptr) int

fn C.sqlite3_free(voidptr)

fn my_callback(arg voidptr, howmany int, cvalues &charptr, cnames &charptr) int {
	unsafe {
		for i in 0 .. howmany {
			print('| ${cstring_to_vstring(cnames[i])}: ${cstring_to_vstring(cvalues[i]):20} ')
		}
	}
	println('|')
	return 0
}

fn main() {
	db := &C.sqlite3(0) // this means `sqlite3* db = 0`
	// passing a string literal to a C function call results in a C string, not a V string
	C.sqlite3_open('users.db', &db)
	// C.sqlite3_open(db_path.str, &db)
	query := 'select count(*) from users'
	stmt := &C.sqlite3_stmt(0)
	// NB: you can also use the `.str` field of a V string,
	// to get its C style zero terminated representation
	C.sqlite3_prepare_v2(db, query.str, -1, &stmt, 0)
	C.sqlite3_step(stmt)
	nr_users := C.sqlite3_column_int(stmt, 0)
	C.sqlite3_finalize(stmt)
	println('There are $nr_users users in the database.')
	//
	error_msg := charptr(0)
	query_all_users := 'select * from users'
	rc := C.sqlite3_exec(db, query_all_users.str, my_callback, 7, &error_msg)
	if rc != C.SQLITE_OK {
		eprintln(cstring_to_vstring(error_msg))
		C.sqlite3_free(error_msg)
	}
	C.sqlite3_close(db)
}
```

### ä¼ é€’Cç¼–è¯‘å‚æ•°

åœ¨Væ–‡ä»¶çš„é¡¶éƒ¨æ·»åŠ `ï¼ƒflag`æŒ‡ä»¤, ä»¥æä¾›Cç¼–è¯‘æ ‡å¿—, ä¾‹å¦‚ï¼š

-`-I'ç”¨äºæ·»åŠ CåŒ…å«æ–‡ä»¶æœç´¢è·¯å¾„
-`-l`ç”¨äºæ·»åŠ è¦é“¾æ¥çš„Cåº“åç§°
-`-L`ç”¨äºæ·»åŠ Cåº“æ–‡ä»¶çš„æœç´¢è·¯å¾„
-`-D`è®¾ç½®ç¼–è¯‘æ—¶é—´å˜é‡

ä½ å¯ä»¥(å¯é€‰)å¯¹ä¸åŒçš„ç›®æ ‡ä½¿ç”¨ä¸åŒçš„æ ‡å¿—. å½“å‰æ”¯æŒ`linux`, `darwin`, `freebsd`å’Œ`windows`æ ‡å¿—.

æ³¨æ„ï¼šæ¯ä¸ªæ ‡å¿—å¿…é¡»ç”¨è‡ªå·±çš„è¡Œï¼ˆæš‚æ—¶ï¼‰ 

```v oksyntax
#flag linux -lsdl2
#flag linux -Ivig
#flag linux -DCIMGUI_DEFINE_ENUMS_AND_STRUCTS=1
#flag linux -DIMGUI_DISABLE_OBSOLETE_FUNCTIONS=1
#flag linux -DIMGUI_IMPL_API=
```

åœ¨æ§åˆ¶å°æ„å»ºå‘½ä»¤ä¸­, ä½ å¯ä»¥ä½¿ç”¨:
* `-cflags`ä¼ é€’è‡ªå®šä¹‰æ ‡å¿—ç»™åç«¯Cè¯­è¨€ç¼–è¯‘å™¨
* `-cc`æ¥æ”¹å˜é»˜è®¤çš„Cè¯­è¨€åç«¯ç¼–è¯‘å™¨
* ä¾‹å¦‚: `-cc gcc-9 -cflags -fsanitize=thread`

ä½ å¯ä»¥åœ¨ä½ çš„ç»ˆç«¯ä¸­å®šä¹‰ä¸€ä¸ª `VFLAGS` ç¯å¢ƒå˜é‡æ¥å­˜å‚¨ä½ çš„`-cc`å’Œ`-cflags`è®¾ç½®, è€Œä¸ç”¨æ¯æ¬¡åœ¨ç¼–è¯‘å‘½ä»¤ä¸­åŒ…å«å®ƒä»¬.

### #pkgconfig

æ·»åŠ `#pkgconfig`æŒ‡ä»¤æ˜¯ç”¨æ¥å‘Šè¯‰ç¼–è¯‘å™¨åº”è¯¥ä½¿ç”¨å“ªäº›æ¨¡å—æ¥ç¼–è¯‘, å¹¶ä½¿ç”¨å„è‡ªä¾èµ–æä¾›çš„pkg-configæ–‡ä»¶è¿›è¡Œé“¾æ¥.

åœ¨`#flag`ä¸­ä¸èƒ½ä½¿ç”¨backticksï¼Œè€Œä¸”å‡ºäºå®‰å…¨å’Œå¯ç§»æ¤æ€§çš„è€ƒè™‘, ä¸å¸Œæœ›äº§ç”Ÿè¿›ç¨‹ï¼ŒVä½¿ç”¨è‡ªå·±çš„pkgconfigåº“, å®ƒä¸æ ‡å‡†çš„freedesktopåº“å…¼å®¹.

å¦‚æœæ²¡æœ‰ä¼ é€’flags, å®ƒä¼šæ·»åŠ `--cflags`å’Œ`--libs`, ä¸‹é¢ä¸¤è¡Œéƒ½æ˜¯ä¸€æ ·çš„:

```v oksyntax
#pkgconfig r_core
#pkgconfig --cflags --libs r_core
```

pkgconfigä¼šä»ä¸€ä¸ªç¡¬ç¼–ç çš„é»˜è®¤ pkg-config è·¯å¾„åˆ—è¡¨æŸ¥æ‰¾`.pc`æ–‡ä»¶. ç”¨æˆ·å¯ä»¥é€šè¿‡ä½¿ç”¨`PKG_CONFIG_PATH`ç¯å¢ƒå˜é‡æ¥æ·»åŠ é¢å¤–çš„è·¯å¾„. pkgconfigå¯ä»¥ä¼ å…¥å¤šä¸ªæ¨¡å—.

### åŒ…å«Cä»£ç 

ä½ ä¹Ÿå¯ä»¥ç›´æ¥åœ¨ä½ çš„Væ¨¡å—ä¸­åŒ…å«Cä»£ç .
ä¾‹å¦‚, å‡è®¾ä½ çš„Cä»£ç ä½äºä½ çš„æ¨¡å—æ–‡ä»¶å¤¹ä¸­çš„ä¸€ä¸ªåä¸º'c'çš„æ–‡ä»¶å¤¹ä¸­. é‚£ä¹ˆ, æˆ‘ä»¬å¯ä»¥è¿™æ ·åš:

* åœ¨top levelæ–‡ä»¶å¤¹ä¸­æ”¾ä¸€ä¸ªv.mod æ–‡ä»¶(å¦‚æœä½ ä½¿ç”¨`v new`åˆ›å»ºæ¨¡å—, åˆ™ä½ å·²ç»æœ‰äº†v.modæ–‡ä»¶), ä¾‹å¦‚:
```v ignore
Module {
	name: 'mymodule',
	description: 'My nice module wraps a simple C library.',
	version: '0.0.1'
	dependencies: []
}
```


* åœ¨moduleçš„é¡¶éƒ¨æ·»åŠ ä»¥ä¸‹å‡ è¡Œ:
```v oksyntax
#flag -I @VROOT/c
#flag @VROOT/c/implementation.o
#include "header.h"
```
NB: @VROOT å°†è¢« V æ›¿æ¢ä¸º *æœ€æ¥è¿‘çš„çˆ¶æ–‡ä»¶å¤¹ï¼Œé‚£é‡Œæœ‰ v.mod æ–‡ä»¶*. v.modæ–‡ä»¶æ‰€åœ¨æ–‡ä»¶å¤¹åŒçº§æˆ–ä¸‹é¢çš„ä»»ä½•`.v`æ–‡ä»¶å‡å¯ä»¥ä½¿ç”¨`#flag @VROOT/abc`æ¥å¼•ç”¨è¿™ä¸ªæ–‡ä»¶å¤¹. @VROOTæ–‡ä»¶å¤¹ä¹Ÿæ˜¯æ¨¡å—æŸ¥æ‰¾è·¯å¾„çš„*å‰ç¼€*, æ‰€ä»¥ä½ å¯ä»¥åœ¨ä½ çš„@VROOTä¸‹*å¯¼å…¥*å…¶ä»–æ¨¡å—, åªéœ€ç»™å®ƒä»¬å‘½åå³å¯.

ä¸Šé¢çš„è¯´æ˜å°†ä½¿Våœ¨ä½ çš„@VROOTä¸­å¯»æ‰¾ä¸€ä¸ªç¼–è¯‘è¿‡çš„`.o`æ–‡ä»¶, è·¯å¾„æ˜¯ä½ çš„æ¨¡å—`folder/c/implementation.o`. 
å¦‚æœVæ‰¾åˆ°å®ƒ, `.o`æ–‡ä»¶å°†è¢«é“¾æ¥åˆ°ä½¿ç”¨è¯¥æ¨¡å—çš„ä¸»å¯æ‰§è¡Œæ–‡ä»¶. å¦‚æœæ²¡æœ‰æ‰¾åˆ°, Vä¼šè®¤ä¸ºæœ‰ä¸€ä¸ª`@VROOT/c/implementation.c`æ–‡ä»¶, å…ˆå°è¯•å°†å…¶ç¼–è¯‘æˆ.oæ–‡ä»¶, ç„¶åå†ä½¿ç”¨è¯¥æ–‡ä»¶.

è¿™å…è®¸ä½ æŠŠCä»£ç åŒ…å«åœ¨Væ¨¡å—ä¸­, è¿™æ ·å®ƒçš„å‘å¸ƒå°±æ›´å®¹æ˜“äº†. ä½ å¯ä»¥åœ¨è¿™é‡Œçœ‹åˆ°ä¸€ä¸ªå®Œæ•´çš„åœ¨VåŒ…è£…æ¨¡å—ä¸­ä½¿ç”¨Cä»£ç çš„æœ€å°ä¾‹å­[project_with_c_code](https://github.com/vlang/v/tree/master/vlib/v/tests/project_with_c_code). å¦ä¸€ä¸ªä¾‹å­, æ¼”ç¤ºäº†å°†structä»Cåˆ°Vå†ä»Vä¼ é€’å›å»
[interoperate between C to V to C](https://github.com/vlang/v/tree/master/vlib/v/tests/project_with_c_code_2).

### Cç±»å‹
æ™®é€šçš„cå­—ç¬¦ä¸²å¯ç”¨`unsafe { charptr(cstring).vstring() }`è½¬æˆV string, å¦‚æœå·²çŸ¥é“å…¶é•¿åº¦è¿˜å¯ç”¨`unsafe { charptr(cstring).vstring_with_len(len) }`.

NB: .vstring()å’Œ.vstring_with_len()æ–¹æ³•ä¸åˆ›å»º`cstring`çš„å‰¯æœ¬. æ‰€ä»¥ä½ ä¸åº”è¯¥åœ¨è°ƒç”¨`.vstring()`æ–¹æ³•åé‡Šæ”¾å®ƒ. å¦‚æœä½ éœ€è¦å¤åˆ¶ä¸€ä¸ªCè¯­è¨€å­—ç¬¦ä¸²(ä¸€äº›libc API, æ¯”å¦‚`getenv`å°±éœ€è¦è¿™æ ·åš, å› ä¸ºå®ƒä»¬è¿”å›æŒ‡å‘å†…éƒ¨libcå†…å­˜çš„æŒ‡é’ˆ), ä½ å¯ä»¥ä½¿ç”¨`cstring_to_vstring(cstring)`.

åœ¨Windowsä¸Šï¼ŒC APIç»å¸¸è¿”å›æ‰€è°“çš„`wide`å­—ç¬¦ä¸²(utf16ç¼–ç ). è¿™äº›å­—ç¬¦ä¸²å¯ä»¥ç”¨`string_from_wide(&u16(cwidestring))`è½¬æ¢ä¸ºVå­—ç¬¦ä¸².

V å­—ç¬¦ä¸²æœ‰è¿™äº›ç±»å‹æ˜¯ä¸ºäº†æ›´å®¹æ˜“ä¸ C å­—ç¬¦ä¸²äº’æ“ä½œ:

- `voidptr` for C's `void*`,
- `byteptr` for C's `byte*` and
- `charptr` for C's `char*`.
- `&charptr` for C's `char**`

è¦å°†`voidptr`è½¬ä¸ºVå¼•ç”¨, å¯ä½¿ç”¨`user := &User(user_void_ptr)`.

`voidptr`ä¹Ÿå¯ä»¥è§£å¼•ç”¨åˆ°V structä¸­: `user := User(user_void_ptr)`.

[ä¸€ä¸ªåœ¨Vä¸­è°ƒç”¨Cä»£ç çš„ä¾‹å­](https://github.com/vlang/v/blob/master/vlib/v/tests/project_with_c_code/mod1/wrapper.v)

### C å£°æ˜

Cæ ‡è¯†ç¬¦çš„è®¿é—®æ–¹å¼ä¸ç‰¹å®šæ¨¡å—æ ‡è¯†ç¬¦çš„è®¿é—®æ–¹å¼ç±»ä¼¼ï¼Œä½¿ç”¨`C`å‰ç¼€. å‡½æ•°åœ¨ä½¿ç”¨å‰å¿…é¡»åœ¨Vä¸­é‡æ–°å£°æ˜. ä»»ä½•Cç±»å‹éƒ½å¯ä»¥åœ¨`C`å‰ç¼€åé¢ä½¿ç”¨, ä½†ç±»å‹å¿…é¡»åœ¨Vä¸­é‡æ–°å£°æ˜æ‰èƒ½è®¿é—®ç±»å‹æˆå‘˜.

è¦é‡æ–°å£°æ˜å¤æ‚çš„ç±»å‹, ä¾‹å¦‚åœ¨ä¸‹é¢çš„Cä»£ç ä¸­:

```c
struct SomeCStruct {
	uint8_t implTraits;
	uint16_t memPoolData;
	union {
		struct {
			void* data;
			size_t size;
		};

		DataView view;
	};
};
```

å­æ•°æ®ç»“æ„çš„æˆå‘˜å¯ä»¥ç›´æ¥åœ¨åŒ…å«çš„structä¸­å£°æ˜, å¦‚ä¸‹æ‰€ç¤º:

```v
struct C.SomeCStruct {
	implTraits  byte
	memPoolData u16
	// These members are part of sub data structures that can't currently be represented in V.
	// Declaring them directly like this is sufficient for access.
	// union {
	// struct {
	data voidptr
	size size_t
	// }
	view C.DataView
	// }
}
```

VçŸ¥é“äº†æ•°æ®æˆå‘˜çš„å­˜åœ¨, å¯ä»¥ä¸å®Œå…¨é‡æ–°åˆ›å»ºåŸå§‹ç»“æ„è€Œä½¿ç”¨å®ƒä»¬.

å¦å¤–, å¯ä»¥å°†å­æ•°æ®ç»“æ„[åµŒå…¥](#åµŒå…¥struct), ä»¥ä¿æŒä¸€ä¸ªå¹¶è¡Œçš„ä»£ç ç»“æ„.

## è°ƒè¯•ç”Ÿæˆçš„Cä»£ç 

ä¸ºäº†è°ƒè¯•ç”Ÿæˆçš„Cä»£ç ä¸­çš„é—®é¢˜ï¼Œä½ å¯ä»¥ä¼ é€’è¿™äº›æ ‡å¿—:

- `-g` : äº§ç”Ÿä¸€ä¸ªä¼˜åŒ–ç¨‹åº¦è¾ƒä½çš„å¯æ‰§è¡Œæ–‡ä»¶, å…¶ä¸­åŒ…å«æ›´å¤šçš„è°ƒè¯•ä¿¡æ¯.

    Vå°†å¼ºåˆ¶æ‰§è¡Œå †æ ˆè·Ÿè¸ªä¸­.væ–‡ä»¶çš„è¡Œå·ï¼Œä»¥ä¾¿å¯æ‰§è¡Œæ–‡ä»¶panicæ—¶å¯çœ‹åˆ°. é€šå¸¸æœ€å¥½æ˜¯é€šè¿‡`-g, é™¤éæ˜¯ä½ æ­£åœ¨ç¼–å†™ä½çº§ä»£ç , åœ¨è¿™ç§æƒ…å†µä¸‹, ä½¿ç”¨ä¸‹ä¸€ä¸ªé€‰é¡¹`-cg`
- `-cg` : äº§ç”Ÿä¸€ä¸ªä¼˜åŒ–ç¨‹åº¦è¾ƒä½çš„å¯æ‰§è¡Œæ–‡ä»¶ï¼Œå…¶ä¸­åŒ…å«æ›´å¤šçš„è°ƒè¯•ä¿¡æ¯.


    åœ¨è¿™ç§æƒ…å†µä¸‹, å¯æ‰§è¡Œæ–‡ä»¶å°†ä½¿ç”¨Cæºä»£ç è¡Œå·, å®ƒç»å¸¸ä¸`-keepc`ç»“åˆä½¿ç”¨, è¿™æ ·ä½ å°±å¯ä»¥æ£€æŸ¥ç”Ÿæˆçš„æˆ–è€…è®©ä½ çš„è°ƒè¯•å™¨(`gdb`, `lldb`ç­‰) å¯ä»¥å‘ä½ å±•ç¤ºç”Ÿæˆçš„Cæºä»£ç 
- `-showcc` : æ‰“å°ç”¨äºæ„å»ºç¨‹åºçš„Cå‘½ä»¤
- `-show-c-output` : æ‰“å°ç¼–è¯‘ç¨‹åºæ—¶Cç¼–è¯‘å™¨äº§ç”Ÿçš„è¾“å‡º
- `-keepc` : åœ¨ç¼–è¯‘æˆåŠŸåä¸åˆ é™¤ç”Ÿæˆçš„Cæºä»£ç æ–‡ä»¶. åŒæ—¶ç»§ç»­ä½¿ç”¨ç›¸åŒçš„æ–‡ä»¶è·¯å¾„, è¿™æ ·æ›´ç¨³å®š. å¹¶ä¸”æ›´å®¹æ˜“åœ¨ç¼–è¾‘å™¨/IDEä¸­ä½¿ç”¨.

ä¸ºäº†è·å¾—æœ€ä½³çš„è°ƒè¯•ä½“éªŒ, å¦‚æœä½ æ­£åœ¨å°è£…ä¸€ä¸ªç°æœ‰çš„Cåº“ï¼Œä½ å¯ä»¥åŒæ—¶ä¼ é€’å‡ ä¸ªè¿™æ ·çš„æ ‡å¿—: `v -keepc -cg -showcc yourprogram.v`, ç„¶åè¿è¡Œä½ çš„è°ƒè¯•å™¨(gdb/lldb)æˆ–IDE.

å¦‚æœä½ åªæ˜¯æƒ³æ£€æŸ¥ç”Ÿæˆçš„Cä»£ç ï¼Œé‚£ä¹ˆæ— éœ€è¿›ä¸€æ­¥ç¼–è¯‘, ä½ ä¹Ÿå¯ä»¥ä½¿ç”¨`-o`æ ‡å¿—(ä¾‹å¦‚`-o file.c`), è¿™å°†ä½¿Väº§ç”Ÿ`file.c`ååœæ­¢.

å¦‚æœä½ ä»…æƒ³çœ‹Cæºä»£ç ç”Ÿæˆçš„æŸä¸ªCå‡½æ•°ï¼Œä¾‹å¦‚`main`, ä½ å¯ä»¥ä½¿ç”¨`-o`æ ‡å¿—(ä¾‹å¦‚`-o file.c`), å³`-printfn main -o file.c`.

è¦æŸ¥çœ‹Væ”¯æŒçš„æ‰€æœ‰æ ‡å¿—çš„è¯¦ç»†åˆ—è¡¨ï¼Œå¯ä½¿ç”¨`v help`, `v help build`å’Œ`v help build-c`.

## æœ‰æ¡ä»¶çš„æ±‡ç¼–

### ç¼–è¯‘æ—¶ä»£ç 

`$` è¢«ç”¨ä½œç¼–è¯‘æ—¶æ“ä½œçš„å‰ç¼€.

#### $if
```v
// Support for multiple conditions in one branch
$if ios || android {
	println('Running on a mobile device!')
}
$if linux && x64 {
	println('64-bit Linux.')
}
// Usage as expression
os := $if windows { 'Windows' } $else { 'UNIX' }
println('Using $os')
// $else-$if branches
$if tinyc {
	println('tinyc')
} $else $if clang {
	println('clang')
} $else $if gcc {
	println('gcc')
} $else {
	println('different compiler')
}
$if test {
	println('testing')
}
// v -cg ...
$if debug {
	println('debugging')
}
// v -prod ...
$if prod {
	println('production build')
}
// v -d option ...
$if option ? {
	println('custom option')
}
```

å¦‚æœä½ æƒ³è®©ä¸€ä¸ª`if`åœ¨ç¼–è¯‘æ—¶è¢«è¯„ä¼°, å®ƒå¿…é¡»åœ¨å‰é¢åŠ ä¸Š`$`ç¬¦å·. ç°åœ¨å®ƒå¯ä»¥ç”¨æ¥æ£€æµ‹æ“ä½œç³»ç»Ÿ, ç¼–è¯‘å™¨, å¹³å°æˆ–ç¼–è¯‘é€‰é¡¹. `$if debug`æ˜¯ä¸€ä¸ªç‰¹æ®Šçš„é€‰é¡¹, åƒ`$if windows`æˆ–`$if x32`, å¦‚æœä½ ä½¿ç”¨çš„æ˜¯è‡ªå®šä¹‰çš„ifdef, é‚£ä¹ˆä½ ç¡®å®éœ€è¦`$ifé€‰é¡¹? {}`å¹¶ä½¿ç”¨`v -d option`ç¼–è¯‘.

å®Œæ•´çš„å†…ç½®é€‰é¡¹åˆ—è¡¨:
| OS                            | Compilers         | Platforms             | Other                     |
| ---                           | ---               | ---                   | ---                       |
| `windows`, `linux`, `macos`   | `gcc`, `tinyc`    | `amd64`, `aarch64`    | `debug`, `prod`, `test`   |
| `mac`, `darwin`, `ios`,       | `clang`, `mingw`  | `x64`, `x32`          | `js`, `glibc`, `prealloc` |
| `android`,`mach`, `dragonfly` | `msvc`            | `little_endian`       | `no_bounds_checking`      |
| `gnu`, `hpux`, `haiku`, `qnx` | `cplusplus`       | `big_endian`          | |
| `solaris`, `linux_or_macos`   | | | |

#### $embed_file

```v ignore
import os
fn main() {
	embedded_file := $embed_file('v.png')
	os.write_file('exported.png', embedded_file.to_string()) ?
}
```

Vå¯ä»¥ç”¨`$embed_file(<path>)`å°†ä»»æ„æ–‡ä»¶åµŒå…¥åˆ°å¯æ‰§è¡Œæ–‡ä»¶ä¸­, æ˜¯ç¼–è¯‘æ—¶è°ƒç”¨çš„, è·¯å¾„å¯ä»¥æ˜¯æºæ–‡ä»¶çš„ç»å¯¹è·¯å¾„æˆ–ç›¸å¯¹è·¯å¾„.

å½“ä½ ä¸ä½¿ç”¨`-prod`æ—¶, æ–‡ä»¶å°†ä¸ä¼šè¢«åµŒå…¥. ç›¸åï¼Œå®ƒå°†åœ¨ä½ çš„ç¨‹åºè¿è¡Œæ—¶ç¬¬ä¸€æ¬¡è°ƒç”¨`f.data()`æ—¶è¢«åŠ è½½, ä½¿å¾—æ›´å®¹æ˜“åœ¨å¤–éƒ¨ç¼–è¾‘ç¨‹åºä¸­è¿›è¡Œä¿®æ”¹, è€Œä¸éœ€è¦é‡æ–°ç¼–è¯‘å¯æ‰§è¡Œæ–‡ä»¶.

å½“ä½ ç”¨`-prod`ç¼–è¯‘æ—¶, è¯¥æ–‡ä»¶*ä¼šè¢«åµŒå…¥*åˆ°å¯æ‰§è¡Œæ–‡ä»¶ï¼Œå¢åŠ äºŒè¿›åˆ¶æ–‡ä»¶çš„å¤§å°, ä½†ä½¿å®ƒæ›´åŠ è‡ªç”±, ä»è€Œæ›´å®¹æ˜“åˆ†å‘. åœ¨è¿™ç§æƒ…å†µä¸‹ï¼Œ`f.data()`æ²¡æœ‰ioæ“ä½œ, ä¸”å§‹ç»ˆè¿”å›ç›¸åŒçš„æ•°æ®.

#### $tmpl: å†…åµŒå’Œè§£æV templateæ–‡ä»¶

Væœ‰ä¸€ä¸ªç®€å•çš„æ¨¡æ¿è¯­è¨€, ç”¨äºæ–‡æœ¬å’Œhtmlæ¨¡æ¿, å®ƒä»¬å¯ä»¥é€šè¿‡`$tmpl('path/to/template.txt')`è½»æ¾åµŒå…¥:

```v ignore
fn build() string {
	name := 'Peter'
	age := 25
	numbers := [1, 2, 3]
	return $tmpl('1.txt')
}

fn main() {
	println(build())
}
```

1.txt:

```
name: @name

age: @age

numbers: @numbers

@for number in numbers
  @number
@end
```

output:

```
name: Peter

age: 25

numbers: [1, 2, 3]

1
2
3
```




#### $env

```v
module main

fn main() {
	compile_time_env := $env('ENV_VAR')
	println(compile_time_env)
}
```

Vå¯ä»¥åœ¨ç¼–è¯‘æ—¶ä»ç¯å¢ƒå˜é‡ä¸­å¼•å…¥å€¼. `$env('ENV_VAR')`ä¹Ÿå¯ä»¥ç”¨åœ¨é¡¶çº§çš„`#flag`å’Œ`#include`è¯­å¥ä¸­: `#flag linux -I $env('JAVA_HOME')/include`.

### ç‰¹å®šç¯å¢ƒæ–‡ä»¶

å¦‚æœä¸€ä¸ªæ–‡ä»¶æœ‰ä¸€ä¸ªç‰¹å®šç¯å¢ƒçš„åç¼€, å®ƒå°†åªé’ˆå¯¹è¯¥ç¯å¢ƒè¿›è¡Œç¼–è¯‘.

- `.js.v` => å°†ä»…ç”±JSåç«¯ä½¿ç”¨. è¿™äº›æ–‡ä»¶å¯ä»¥åŒ…å«JSä»£ç 
- `.c.v` => ä»…ä¾›Cåç«¯ä½¿ç”¨, è¿™äº›æ–‡ä»¶å¯ä»¥åŒ…å«Cä»£ç 
- `.x64.v` => ä»…ä¾›Vçš„x64åç«¯ä½¿ç”¨
- `_nix.c.v` => ä»…ä¾›Unixç³»ç»Ÿ(éWindows)ä½¿ç”¨
- `_${os}.c.v` => å°†åªåœ¨ç‰¹å®šçš„`os`ç³»ç»Ÿä¸Šä½¿ç”¨.

    ä¾‹å¦‚, `_windows.c.v`å°†åªåœ¨Windowsç³»ç»Ÿä¸Šç¼–è¯‘æ—¶ä½¿ç”¨, æˆ–è€…ä½¿ç”¨`-os windows`
- `_default.c.v` => åªæœ‰åœ¨æ²¡æœ‰æ›´å…·ä½“çš„å¹³å°æ–‡ä»¶æ—¶æ‰ä¼šä½¿ç”¨.

    ä¾‹å¦‚ï¼Œå¦‚æœä½ åŒæ—¶æœ‰`file_linux.c.v`å’Œ`file_default.c.v, è€Œä½ æ˜¯ä¸ºlinuxç¼–è¯‘çš„, é‚£ä¹ˆåªéœ€è¦ä½¿ç”¨`file_linux.c.v`, è€Œ`file_default.c.v`å°†è¢«å¿½ç•¥

è¿™é‡Œæœ‰ä¸€ä¸ªæ›´å®Œæ•´çš„ä¾‹å­:
main.v:
```v ignore
module main
fn main() { println(message) }
```

main_default.c.v:
```v ignore
module main
const ( message = 'Hello world' )
```

main_linux.c.v:
```v ignore
module main
const ( message = 'Hello linux' )
```

main_windows.c.v:
```v ignore
module main
const ( message = 'Hello windows' )
```

ä½¿ç”¨ä¸Šé¢çš„ä¾‹å­:
- å½“ä½ ä¸ºwindowsç¼–è¯‘æ—¶, ä½ ä¼šå¾—åˆ°'Hello windows'
- å½“ä½ ä¸ºlinuxç¼–è¯‘æ—¶ï¼Œä½ ä¼šå¾—åˆ°'Hello linux'
- å½“ä½ åœ¨å…¶ä»–å¹³å°ä¸Šç¼–è¯‘æ—¶, ä½ ä¼šå¾—åˆ°çš„æ˜¯éç‰¹å®šçš„'Hello world'ä¿¡æ¯
- `_d_customflag.v` => åªæœ‰å½“ä½ æŠŠ`-d customflag`ä¼ ç»™Væ—¶æ‰ä¼šä½¿ç”¨

    è¿™ç›¸å½“äº`$if customflag ? {}`, ä½†æ˜¯å¯¹äºæ•´ä¸ªæ–‡ä»¶, è€Œä¸ä»…ä»…æ˜¯ä¸€ä¸ªä»£ç å—. `customflag`åº”è¯¥æ˜¯ä¸€ä¸ªsnake_caseæ ‡è¯†ç¬¦, ä¸èƒ½åŒ…å«ä»»æ„å­—ç¬¦(åªèƒ½æ˜¯å°å†™æ‹‰ä¸å­—æ¯+æ•°å­—+`_`).

    NB: ç»„åˆå¼çš„`_d_customflag_linux.c.v` postfixå°†æ— æ³•å·¥ä½œ. å¦‚æœä½ ç¡®å®éœ€è¦ä¸€ä¸ªè‡ªå®šä¹‰çš„flagæ–‡ä»¶, å…¶ä¸­æœ‰ä¾èµ–äºå¹³å°çš„ä»£ç , è¯·ä½¿ç”¨`_d_customflag.v`, ç„¶ååœ¨é‡Œé¢ä½¿ç”¨plaftormä¾èµ–ç¼–è¯‘æ—¶çš„æ¡ä»¶å—, å³`$if linux {}`ç­‰.
- `_notd_customflag.v` => ç±»ä¼¼äº_d_customflag.v, ä½†ä¼šè¢«ç”¨äº *åªæœ‰å½“ä½ ä¸å‘Vä¼ é€’`-d customflag`æ—¶æ‰ä¼šå‡ºç°

## ç¼–è¯‘æ—¶ä¼ªå˜é‡

Vè¿˜å…è®¸ä½ çš„ä»£ç è®¿é—®ä¸€ç»„ä¼ªå­—ç¬¦ä¸²å˜é‡, å…¶åœ¨ç¼–è¯‘æ—¶ä¼šè¢«æ›¿æ¢.

- `@FN` => ç”¨å½“å‰Vå‡½æ•°çš„åç§°ä»£æ›¿
- `@METHOD` => ç”¨ReceiverType.MethodNameæ›¿æ¢
- `@MOD` => ç”¨å½“å‰Væ¨¡å—çš„åç§°ä»£æ›¿
- `@STRUCT` => ç”¨å½“å‰Vç»“æ„çš„åç§°ä»£æ›¿
- `@FILE` => ç”¨Væºæ–‡ä»¶çš„è·¯å¾„æ›¿æ¢
- `@LINE` => æ›¿æ¢ä¸ºå‡ºç°çš„Vè¡Œå·(ä½œä¸ºå­—ç¬¦ä¸²)
- `@COLUMN` => æ›¿æ¢ä¸ºå‡ºç°çš„åˆ—å·(ä½œä¸ºä¸€ä¸ªå­—ç¬¦ä¸²)
- `@VEXE` => æ›¿æ¢ä¸ºVç¼–è¯‘å™¨çš„è·¯å¾„
- `@VHASH` => æ›¿æ¢ä¸ºVç¼–è¯‘å™¨çš„ç¼©çŸ­æäº¤å“ˆå¸Œå€¼(ä½œä¸ºä¸€ä¸ªå­—ç¬¦ä¸²)
- `@VMOD_FILE` => ç”¨æœ€è¿‘çš„v.modæ–‡ä»¶çš„å†…å®¹(ä½œä¸ºä¸€ä¸ªå­—ç¬¦ä¸²)æ›¿æ¢

è¿™å…è®¸ä½ åšä¸‹é¢çš„ä¾‹å­, åœ¨è°ƒè¯•/è®°å½•/è·Ÿè¸ªä½ çš„ä»£ç æ—¶å¾ˆæœ‰ç”¨:
```v
eprintln('file: ' + @FILE + ' | line: ' + @LINE + ' | fn: ' + @MOD + '.' + @FN)
```

å¦ä¸€ä¸ªä¾‹å­æ˜¯ï¼Œå¦‚æœä½ æƒ³æŠŠv.modçš„ç‰ˆæœ¬/åç§°åµŒå…¥åˆ°ä½ çš„å¯æ‰§è¡Œæ–‡ä»¶ä¸­:
```v ignore
import v.vmod
vm := vmod.decode( @VMOD_FILE ) or { panic(err.msg) }
eprintln('$vm.name $vm.version\n $vm.description')
```

## æ€§èƒ½è°ƒæ•´

å½“ç¼–è¯‘ä»£ç æ—¶ä½¿ç”¨`-prod`, ç”Ÿæˆçš„Cä»£ç ä¸€èˆ¬éƒ½æ˜¯å¤Ÿå¿«çš„. ä½†åœ¨æŸäº›æƒ…å†µä¸‹, ä½ å¯èƒ½æ·»åŠ é¢å¤–çš„æç¤ºç»™ç¼–è¯‘å™¨, è¿™æ ·å®ƒå°±å¯ä»¥è¿›ä¸€æ­¥ä¼˜åŒ–æŸäº›ä»£ç å—.

NB: è¿™äº›æ˜¯å¾ˆ*å°‘*ç”¨åˆ°, ç”šè‡³ä¸åº”è¯¥ä½¿ç”¨, é™¤é *å‰–æä»£ç *åçœ‹åˆ°å®ƒä»¬æœ‰æ˜¾è‘—çš„å¥½å¤„. å¼•ç”¨gccçš„æ–‡æ¡£: "ç¨‹åºå‘˜åœ¨é¢„æµ‹ä»–ä»¬çš„ç¨‹åºæ˜¯å¦‚ä½•å®é™…æ‰§è¡Œçš„æ–¹é¢æ˜¯å‡ºäº†åçš„å·®åŠ²".

`[inline]` - ä½ å¯ä»¥ç”¨`[inline]`æ ‡è®°å‡½æ•°, è¿™æ ·Cç¼–è¯‘å™¨ä¼šå°½é‡å°†å…¶å†…åµŒ, åœ¨æŸäº›æƒ…å†µä¸‹, å¯èƒ½å¯¹æ€§èƒ½æœ‰åˆ©. ä½†å¯èƒ½ä¼šå½±å“ä½ çš„å¯æ‰§è¡Œæ–‡ä»¶çš„å¤§å°.

`[direct_array_access]` - åœ¨å¸¦æœ‰`[direct_array_access]`æ ‡è®°çš„å‡½æ•°ä¸­, ç¼–è¯‘å™¨ä¼šå°†æ•°ç»„æ“ä½œç›´æ¥ç¿»è¯‘æˆCæ•°ç»„æ“ä½œ(çœç•¥è¾¹ç•Œæ£€æŸ¥), è¿™å¯èƒ½ä¼šåœ¨è¿­ä»£ä¸€ä¸ªæ•°ç»„çš„å‡½æ•°ä¸­èŠ‚çœå¾ˆå¤šæ—¶é—´, ä½†ä»£ä»·æ˜¯ä½¿å‡½æ•°ä¸å®‰å…¨, é™¤éç”±ç”¨æˆ·è‡ªå·±æ£€æŸ¥è¾¹ç•Œ.

`if _likely_(boolè¡¨è¾¾å¼) {`è¿™æš—ç¤ºäº†Cç¼–è¯‘å™¨å¸ƒå°”è¡¨è¾¾å¼å¤§æ¦‚ç‡ä¸ºçœŸ, æ‰€ä»¥å®ƒç”Ÿæˆçš„æ±‡ç¼–ä»£ç , åˆ†æ”¯è¯¯åˆ¤çš„å‡ ç‡è¾ƒå°. åœ¨JSåç«¯, ä»€ä¹ˆéƒ½ä¸åš.

`if _unlikely_(boolè¡¨è¾¾å¼) {`ç±»ä¼¼äº`_likely_(x)`, ä½†å®ƒæš—ç¤ºäº†çš„å¸ƒå°”è¡¨è¾¾å¼æ˜¯éå¸¸ä¸å¯èƒ½æ‰§è¡Œåˆ°çš„. åœ¨JSåç«¯, è¿™ä»€ä¹ˆä¹Ÿåšä¸äº†.

<a id='Reflection via codegen'>

## ç¼–è¯‘æ—¶åæ€

æ‹¥æœ‰å†…ç½®çš„JSONæ”¯æŒæ˜¯ä¸é”™çš„, ä½†Vä¹Ÿå…è®¸ä½ ä¸ºä»»ä½•æ•°æ®æ ¼å¼åˆ›å»ºé«˜æ•ˆçš„åºåˆ—åŒ–å™¨. Væœ‰ç¼–è¯‘æ—¶çš„`if`å’Œ`for`ç»“æ„:

```v wip
// TODO: not fully implemented

struct User {
    name string
    age  int
}

// Note: T should be passed a struct name only
fn decode<T>(data string) T {
    mut result := T{}
    // compile-time `for` loop
    // T.fields gives an array of a field metadata type
    $for field in T.fields {
        $if field.typ is string {
            // $(string_expr) produces an identifier
            result.$(field.name) = get_string(data, field.name)
        } $else $if field.typ is int {
            result.$(field.name) = get_int(data, field.name)
        }
    }
    return result
}

// `decode<User>` generates:
fn decode_User(data string) User {
    mut result := User{}
    result.name = get_string(data, 'name')
    result.age = get_int(data, 'age')
    return result
}
```

## æœ‰é™çš„æ“ä½œç¬¦é‡è½½

```v
struct Vec {
	x int
	y int
}

fn (a Vec) str() string {
	return '{$a.x, $a.y}'
}

fn (a Vec) + (b Vec) Vec {
	return Vec{a.x + b.x, a.y + b.y}
}

fn (a Vec) - (b Vec) Vec {
	return Vec{a.x - b.x, a.y - b.y}
}

fn main() {
	a := Vec{2, 3}
	b := Vec{4, 5}
	mut c := Vec{1, 2}
	println(a + b) // "{6, 8}"
	println(a - b) // "{-2, -2}"
	c += a
	println(c) // "{3, 5}"
}
```

æ“ä½œç¬¦è¶…è½½è¿èƒŒäº†Vçš„ç®€å•å’Œå¯é¢„æµ‹æ€§çš„ç†å¿µ. ä½†ç”±äºç§‘å­¦å’Œå›¾å½¢åº”ç”¨æ˜¯Vçš„é¢†åŸŸä¹‹ä¸€. ä¸ºäº†æé«˜å¯è¯»æ€§ï¼Œè¿ç®—ç¬¦é‡è½½æ˜¯ä¸€ä¸ªé‡è¦çš„ç‰¹å¾.

`a.add(b).add(c.mul(d))`æ¯”`a + b + c * d`å¯è¯»æ€§è¦å·®å¾—å¤š.

ä¸ºäº†æé«˜å®‰å…¨æ€§å’Œå¯ç»´æŠ¤æ€§, å¯¹æ“ä½œç¬¦çš„é‡è½½è¿›è¡Œäº†é™åˆ¶:

- åªèƒ½é‡è½½`+, -, *, /, %, <, >, ==, !=, <=, >=`è¿ç®—ç¬¦
- `==`å’Œ`!=`ç”±ç¼–è¯‘å™¨è‡ªè¡Œç”Ÿæˆ, ä½†å¯ä»¥é‡è½½
- ä¸å…è®¸åœ¨è¿ç®—ç¬¦å‡½æ•°é‡Œé¢è°ƒç”¨å…¶ä»–å‡½æ•°
- è¿ç®—ç¬¦å‡½æ•°ä¸èƒ½ä¿®æ”¹å…¶å‚æ•°
- å½“ä½¿ç”¨`<`å’Œ`==`è¿ç®—ç¬¦æ—¶, è¿”å›ç±»å‹å¿…é¡»æ˜¯`bool`
- å½“å®šä¹‰äº†`==`å’Œ`<`æ—¶ï¼Œ`ï¼=`ã€`>`ã€`<=`å’Œ`>=`ä¼šè‡ªåŠ¨ç”Ÿæˆ
- ä¸¤ä¸ªå‚æ•°å¿…é¡»å…·æœ‰ç›¸åŒçš„ç±»å‹(å°±åƒVä¸­çš„æ‰€æœ‰æ“ä½œç¬¦ä¸€æ ·)
- èµ‹å€¼è¿ç®—ç¬¦(`*=`, `+=`, `/=`ç­‰)åœ¨å®šä¹‰è¿ç®—ç¬¦æ—¶, ä¼šè‡ªåŠ¨ç”Ÿæˆ, ä½†å®ƒä»¬å¿…é¡»è¿”å›ç›¸åŒçš„ç±»å‹.

## å†…è”ç¼–è¯‘
<!-- ignore because it doesn't pass fmt test (why?) --> 
```v ignore
a := 100
b := 20
mut c := 0
asm amd64 {
    mov eax, a
    add eax, b
    mov c, eax
    ; =r (c) as c // output 
    ; r (a) as a // input 
      r (b) as b
}
println('a: $a') // 100 
println('b: $b') // 20 
println('c: $c') // 120
```

æ›´å¤šä¾‹å­å¯è§ [github.com/vlang/v/tree/master/vlib/v/tests/assembly/asm_test.amd64.v](https://github.com/vlang/v/tree/master/vlib/v/tests/assembly/asm_test.amd64.v)

## å°†Cç¿»è¯‘æˆV

TODOï¼šåœ¨V 0.3ä¸­å¯ä»¥å°†Cè¯­è¨€ç¿»è¯‘æˆVè¯­è¨€.

Vå¯ä»¥å°†ä½ çš„Cä»£ç ç¿»è¯‘æˆäººç±»å¯è¯»çš„Vä»£ç ï¼Œå¹¶åœ¨Cåº“ä¹‹ä¸Šç”ŸæˆVåŒ…è£…å™¨.

æˆ‘ä»¬å…ˆåˆ›å»ºä¸€ä¸ªç®€å•çš„ç¨‹åº`test.c`:

```c
#include "stdio.h"

int main() {
	for (int i = 0; i < 10; i++) {
		printf("hello world\n");
	}
        return 0;
}
```

æ‰§è¡Œ`v translate test.c`, ç„¶åVä¼šç”Ÿæˆ `test.v`:

```v
fn main() {
	for i := 0; i < 10; i++ {
		println('hello world')
	}
}
```

è¦åœ¨Cåº“çš„åŸºç¡€ä¸Šç”Ÿæˆä¸€ä¸ªå°è£…å™¨, è¯·ä½¿ç”¨ä»¥ä¸‹å‘½ä»¤:

```bash
v wrapper c_code/libsodium/src/libsodium
```

è¿™å°†ç”Ÿæˆä¸€ä¸ªå¸¦æœ‰Væ¨¡å—çš„ç›®å½•`libsodium`.

C2Vç”Ÿæˆçš„libsodiumåŒ…è£…å™¨çš„ä¾‹å­, è§https://github.com/medvednikov/libsodium

<br>

ä»€ä¹ˆæ—¶å€™åº”è¯¥ç¿»è¯‘Cä»£ç , ä»€ä¹ˆæ—¶å€™åº”è¯¥ç®€å•åœ°ä»Vä¸­è°ƒç”¨Cä»£ç ?

å¦‚æœä½ çš„Cä»£ç å†™å¾—å¾ˆå¥½ï¼Œç»è¿‡å¾ˆå¥½çš„æµ‹è¯•, é‚£ä¹ˆå½“ç„¶ä½ å¯ä»¥ä¸€ç›´ç®€å•åœ°ä»Vä¸­è°ƒç”¨è¿™ä¸ªCä»£ç .

å°†å®ƒç¿»è¯‘æˆVç»™ä½ å¸¦æ¥äº†å‡ ä¸ªå¥½å¤„:

- å¦‚æœä½ æ‰“ç®—å¼€å‘é‚£ä¸ªä»£ç åº“, ä½ ç°åœ¨å·²ç»åœ¨ä¸€ç§è¯­è¨€ä¸­å¾—åˆ°äº†æ‰€æœ‰çš„ä¸œè¥¿, æ¯”Cè¯­è¨€æ›´å®‰å…¨ï¼Œæ›´å®¹æ˜“å¼€å‘
- äº¤å‰ç¼–è¯‘å˜å¾—æ›´åŠ å®¹æ˜“. ä½ æ ¹æœ¬ä¸ç”¨æ‹…å¿ƒè¿™ä¸ªé—®é¢˜.
- ä¹Ÿæ²¡æœ‰æ›´å¤šçš„æ„å»ºæ ‡å¿—å’ŒåŒ…å«æ–‡ä»¶

## çƒ­æ›´æ–°

```v live
module main

import time
import os

[live]
fn print_message() {
	println('Hello! Modify this message while the program is running.')
}

fn main() {
	for {
		print_message()
		time.sleep(500 * time.millisecond)
	}
}
```

ç”¨`v -live message.v`æ„å»ºè¿™ä¸ªä¾‹å­.

ä½ æƒ³é‡è½½çš„å‡½æ•°åœ¨å®šä¹‰å‰å¿…é¡»æœ‰`[live]`å±æ€§.

ç›®å‰, åœ¨ç¨‹åºè¿è¡Œæ—¶è¿˜ä¸èƒ½ä¿®æ”¹ç±»å‹.

æ›´å¤šçš„ä¾‹å­(åŒ…æ‹¬ä¸€ä¸ªå›¾å½¢åº”ç”¨ç¨‹åº)å¯è§
[github.com/vlang/v/tree/master/examples/hot_code_reload](https://github.com/vlang/v/tree/master/examples/hot_reload)

## äº¤å‰æ±‡ç¼–

è¦äº¤å‰ç¼–è¯‘ä½ çš„é¡¹ç›®, åªéœ€è¿è¡Œ:

```shell
v -os windows .
```

æˆ–

```shell
v -os linux .
```

(macOSçš„äº¤å‰ç¼–è¯‘æš‚æ—¶æ— æ³•å®ç°ã€‚)

å¦‚æœä½ æ²¡æœ‰ä»»ä½•Cè¯­è¨€çš„ä¾èµ–, é‚£å°±åªéœ€è¦è¿™æ ·åš. è¿™ç”šè‡³åœ¨ä½¿ç”¨`ui`æ¨¡å—ç¼–è¯‘GUIåº”ç”¨ç¨‹åºæˆ–ä½¿ç”¨`gg`ç¼–è¯‘å›¾å½¢åº”ç”¨ç¨‹åºæ—¶ä¹Ÿå¯è¿™æ ·.

ä½ éœ€è¦å®‰è£…Clang, LLDé“¾æ¥å™¨, å¹¶æ ¹æ®Væä¾›çš„ä¸€ä¸ªé“¾æ¥ä¸‹è½½ä¸€ä¸ªåŒ…å«ä»¥ä¸‹å†…å®¹çš„zipæ–‡ä»¶, å®ƒåŒ…å«äº†ä¸ºWindowså’ŒLinuxæä¾›æ”¯æŒçš„libå’Œincludeæ–‡ä»¶.

## Vä¸­çš„è·¨å¹³å°shellè„šæœ¬

Vå¯ä»¥ä½œä¸ºBashçš„æ›¿ä»£å“æ¥ç¼–å†™éƒ¨ç½²è„šæœ¬, æ„å»ºè„šæœ¬ç­‰.

ä½¿ç”¨Vçš„ä¼˜åŠ¿åœ¨äºè¯­è¨€çš„ç®€å•æ€§å’Œå¯é¢„æµ‹æ€§, ä»¥åŠè·¨å¹³å°æ”¯æŒ. "V script"æ—¢å¯ä»¥åœ¨ç±»ä¼¼Unixçš„ç³»ç»Ÿä¸Šè¿è¡Œ, ä¹Ÿå¯ä»¥åœ¨Windowsä¸Šè¿è¡Œ.

ä½¿ç”¨`.vsh`æ–‡ä»¶æ‰©å±•å, å¹¶å°†`os`æ¨¡å—ä¸­çš„æ‰€æœ‰å‡½æ•°æˆä¸ºå…¨å±€å‡½æ•°(è¿™æ ·ä½ å°±å¯ä»¥ä½¿ç”¨`mkdir()`è€Œä¸æ˜¯`os.mkdir()`).

`deploy.vsh`ä¾‹å­:
```v wip
#!/usr/bin/env -S v run
// The shebang above associates the file to V on Unix-like systems,
// so it can be run just by specifying the path to the file
// once it's made executable using `chmod +x`.

// Remove if build/ exits, ignore any errors if it doesn't
rmdir_all('build') or { }

// Create build/, never fails as build/ does not exist
mkdir('build') ?

// Move *.v files to build/
result := exec('mv *.v build/') ?
if result.exit_code != 0 {
	println(result.output)
}
// Similar to:
// files := ls('.') ?
// mut count := 0
// if files.len > 0 {
//     for file in files {
//         if file.ends_with('.v') {
//              mv(file, 'build/') or {
//                  println('err: $err')
//                  return
//              }
//         }
//         count++
//     }
// }
// if count == 0 {
//     println('No files')
// }
```

ç°åœ¨ä½ å¯ä»¥åƒç¼–è¯‘æ™®é€šçš„Vç¨‹åºä¸€æ ·ç¼–è¯‘è¿™ä¸ªç¨‹åº, ç„¶åå¾—åˆ°ä¸€ä¸ªå¯æ‰§è¡Œæ–‡ä»¶, å¹¶å¯ä»¥åœ¨ä»»ä½•åœ°æ–¹éƒ¨ç½²å’Œè¿è¡Œ:
`v deploy.vsh && ./deploy`

æˆ–è€…å°±åƒä¼ ç»Ÿçš„Bashè„šæœ¬ä¸€æ ·è¿è¡Œå®ƒ:
`v run deploy.vsh`

åœ¨ç±»ä¼¼Unixçš„å¹³å°ä¸Š, ä½¿ç”¨`chmod +x`ä½¿æ–‡ä»¶å¯æ‰§è¡Œå, å¯ç›´æ¥è¿è¡Œ:
`./deploy.vsh`

## å±æ€§
Væœ‰å‡ ä¸ªå±æ€§å¯ä»¥ä¿®æ”¹å‡½æ•°å’Œstructçš„è¡Œä¸º.

å±æ€§æ˜¯æŒ‡åœ¨`[]`å†…æŒ‡å®šçš„ç¼–è¯‘å™¨æŒ‡ä»¤ï¼Œå®ƒä½äºfunction/struct/enumå£°æ˜, å¹¶ä¸”åªé€‚ç”¨äºä»¥ä¸‹å£°æ˜.

```v
// Calling this function will result in a deprecation warning
[deprecated]
fn old_function() {
}

// It can also display a custom deprecation message
[deprecated: 'use new_function() instead']
fn legacy_function() {}

// This function's calls will be inlined.
[inline]
fn inlined_function() {
}

// The following struct must be allocated on the heap. Therefore, it can only be used as a
// reference (`&Window`) or inside another reference (`&OuterStruct{ Window{...} }`).
[heap]
struct Window {
}

// V will not generate this function and all its calls if the provided flag is false.
// To use a flag, use `v -d flag`
[if debug]
fn foo() {
}

fn bar() {
	foo() // will not be called if `-d debug` is not passed
}

// Calls to following function must be in unsafe{} blocks.
// Note that the code in the body of `risky_business()` will still be
// checked, unless you also wrap it in `unsafe {}` blocks.
// This is usefull, when you want to have an `[unsafe]` function that
// has checks before/after a certain unsafe operation, that will still
// benefit from V's safety features.
[unsafe]
fn risky_business() {
	// code that will be checked, perhaps checking pre conditions
	unsafe {
		// code that *will not be* checked, like pointer arithmetic,
		// accessing union fields, calling other `[unsafe]` fns, etc...
		// Usually, it is a good idea to try minimizing code wrapped
		// in unsafe{} as much as possible.
		// See also [Memory-unsafe code](#memory-unsafe-code)
	}
	// code that will be checked, perhaps checking post conditions and/or
	// keeping invariants
}

// V's autofree engine will not take care of memory management in this function.
// You will have the responsibility to free memory manually yourself in it.
[manualfree]
fn custom_allocations() {
}

// For C interop only, tells V that the following struct is defined with `typedef struct` in C
[typedef]
struct C.Foo {
}

// Used in Win32 API code when you need to pass callback function
[windows_stdcall]
fn C.DefWindowProc(hwnd int, msg int, lparam int, wparam int)

// Windows only:
// If a default graphics library is imported (ex. gg, ui), then the graphical window takes
// priority and no console window is created, effectively disabling println() statements.
// Use to explicity create console window. Valid before main() only.
[console]
fn main() {
}
```

## Goto

Vå…è®¸ç”¨`goto`æ— æ¡ä»¶åœ°è·³è½¬åˆ°ä¸€ä¸ªæ ‡ç­¾. æ ‡ç­¾åç§°å¿…é¡»ä¸`goto`è¯­å¥åŒ…å«åœ¨åŒä¸€ä¸ªå‡½æ•°ä¸­. ç¨‹åºå¯ä»¥gotoåˆ°å½“å‰ä½œç”¨åŸŸä¹‹å¤–æˆ–æ›´æ·±çš„åœ°æ–¹. `goto`å…è®¸è·³è¿‡å˜é‡åˆå§‹åŒ–æˆ–è·³å›è®¿é—®å·²ç»é‡Šæ”¾çš„å†…å­˜çš„ä»£ç , æ‰€ä»¥å®ƒéœ€è¦`unsafe`.

```v ignore
if x {
	// ...
	if y {
		unsafe {
			goto my_label
		}
	}
	// ...
}
my_label:
```
åº”é¿å…ä½¿ç”¨`goto`, ç‰¹åˆ«æ˜¯åœ¨å¯ä»¥ä½¿ç”¨ `for`çš„æƒ…å†µä¸‹. [å¸¦æ ‡ç­¾çš„break/continue](#å¸¦æ ‡ç­¾çš„break/continue)å¯ä»¥ç”¨æ¥è„±ç¦»åµŒå¥—å¾ªç¯, è¿™äº›ä¸ä¼šæœ‰è¿åå†…å­˜å®‰å…¨çš„é£é™©.

# é™„å½•

## é™„å½•ä¸€ï¼šå…³é”®è¯

Væœ‰41ä¸ªä¿ç•™å…³é”®è¯(3ä¸ªæ˜¯å­—è¯):

```v ignore
as
asm
assert
atomic
break
const
continue
defer
else
embed
enum
false
fn
for
go
goto
if
import
in
interface
is
lock
match
module
mut
none
or
pub
return
rlock
select
shared
sizeof
static
struct
true
type
typeof
union
unsafe
__offsetof
```
å¯è§[ç±»å‹](#ç±»å‹).

## é™„å½•äºŒ: è¿ç®—ç¬¦

è¿™é‡Œåªåˆ—å‡ºäº†[åŸºç¡€ç±»å‹](#åŸºç¡€ç±»å‹)çš„è¿ç®—ç¬¦.

```v ignore
+    sum                    integers, floats, strings
-    difference             integers, floats
*    product                integers, floats
/    quotient               integers, floats
%    remainder              integers

~    bitwise NOT            integers
&    bitwise AND            integers
|    bitwise OR             integers
^    bitwise XOR            integers

!    logical NOT            bools
&&   logical AND            bools
||   logical OR             bools
!=   logical XOR            bools

<<   left shift             integer << unsigned integer
>>   right shift            integer >> unsigned integer


Precedence    Operator
    5             *  /  %  <<  >>  &
    4             +  -  |  ^
    3             ==  !=  <  <=  >  >=
    2             &&
    1             ||


Assignment Operators
+=   -=   *=   /=   %=
&=   |=   ^=
>>=  <<=
```
